function	script	CapitaoMirc	{
	mes "[Capitão Mirc]";
	if(Sex==1) mes "\"Olá meu jovem! Deseja viajar para alguma cidade? Meu navio pode ser lento, mas para você a viagem é de graça... :D \"";
	if(Sex==0) mes "\"Olá minha jovem! Deseja viajar para alguma cidade? Meu navio pode ser lento, mas para você a viagem é de graça... :D \"";
	next;
	mes "["+ strcharinfo(0) +"]";
	menu
		"Sim, estava mesmo precisando economizar dinheiro!", _queroViajar,
		"Estou sem tempo, prefiro algum meio mais rápido.", -;
	close;

_queroViajar:
	mes "[Capitão Mirc]";
	mes "\"Excelente! Nada como relaxar durante uma calma viagem de navio, não acha? Mas me diga, para onde você pretende ir?\"";
	next;
	goto _menuEscolha;

_menuEscolha:
	mes "["+ strcharinfo(0) +"]";
	menu
		"Para o Polo Norte.", _poloNorte,
		"Para a Ilha de Bhramir.", _fortaleza,
		"Para as Terras Desconhecidas.", _desconhecidas,
		"Desisti de viajar.", -;
	close;

_boaViagem:
	mes "[Capitão Mirc]";
	mes "\"Espere a próxima viagem próximo à rampa de desembarque. Sairemos em breve.";
	mes "Tenha uma boa viagem!\"";
	close;

//----------------------------------------------------------------------

_fortaleza:
	if (@porto==1) goto _jaFortaleza;
	set PC_DEST, 1;
	goto _boaViagem;

_poloNorte:
	if (@porto==2) goto _jaPoloNorte;
	set PC_DEST, 2;
	goto _boaViagem;

_desconhecidas:
	if (@porto==3) goto _jaDesconhecidas;
	set PC_DEST, 3;
	goto _boaViagem;

_jaFortaleza:
	mes "[Capitão Mirc]";
	mes "\"Hahaha, mas você já está na Ilha de Bhramir! Escolha um outro destino.\"";
	next;
	goto _menuEscolha;

_jaPoloNorte:
	mes "[Capitão Mirc]";
	mes "\"Hahaha, mas você já está no Polo Norte! Escolha um outro destino.\"";
	next;
	goto _menuEscolha;

_jaDesconhecidas:
	mes "[Capitão Mirc]";
	mes "\"Hahaha, mas você já está nas terras desconhecidas! Escolha um outro destino.\"";
	next;
	goto _menuEscolha;

}
