//

function	script	SoulMenhir	{
	mes "[Menir da Alma]";
	//mes "(A mystical aura surrounds this stone. You feel mysteriously attracted to it. Something tells you to touch it. What do you do?)";
	mes "Uma aura mística envolve essa pedra. Você se sente misteriosamente atraído por ela.";
	mes "Algo lhe diz para tocar-lá. O que você fazer?";
	next;
	if (@fragmentoMenir==0) goto _menu1;
	goto _menu2;

_menu1:
	menu
		"Tocar a pedra", _tocar,
		"Tentar cortar a pedra.", _cortar,
		"Dar uma pancada na pedra.", _bater,
		"Resistir", _return;
	goto _return;

_menu2:
	menu
		"Tocar a pedra", _tocar,
		"Resistir", _return;
	goto _return;

_tocar:
	if (Menhir_Activated == 1) goto _shortVersion;

	mes "[Menir da Alma]";
	//mes "(You touch the mysteriouse stone. Somehow it feels warm and cold at the same time.)";
	mes "Você toca na misteriosa pedra. De alguma forma se sente quente e frio ao mesmo tempo.";
	next;

	mes "[Menir da Alma]";
	//mes "(Suddenly a strange sensation flows through you. It feels like your soul leaves your body and becomes one with the stone.)";
	mes "De repente, uma estranha sensação flui através de você.";
	mes "Parece que sua alma deixa o seu corpo e se funde à pedra.";
	next;

	mes "[Menir da Alma]";
	//mes "(As suddenly as the feeling started it stops. The strange attraction is away from one moment to the next and the menhir feels like just an ordinary stone.)";
	mes "Tão de repente como o sentimento começou ele se vai.";
	mes "A estranha atração é afastada de um momento para o outro e seus sentimentos pela menir não são mais que os de uma pedra ordinária.";

	set Menhir_Activated, 1;
	goto _save;

_shortVersion:
	mes "[Menir da Alma]";
	//mes "(A strange sensation flows through you. It feels like your soul leaves your body and becomes one with the stone. as suddenly as the feeling started it stops.)";
	mes "Uma estranha sensação flui através de você. Parece que sua alma deixa o seu corpo e se funde à pedra.";
	mes "Tão de repente como o sentimento começou ele se vai.";
	goto _save;

_save:
	savepoint @map$, @x, @y;
	goto _return;

_cortar:
	if (@cortarMenir==1) goto _naoCortar;
	mes "[Menir da Alma]";
	mes "Você empunha sua espada e golpeia a pedra com toda a sua força...";
	next;
	specialeffect 28;
	set @cortarMenir, 1;
	mes "[Menir da Alma]";
	mes "Faíscas voam por todos os lados, mas nada acontece com a pedra. Ela é tão resistente quanto o ferro.";
	return;

_naoCortar:
	mes "[Menir da Alma]";
	mes "Assim você vai danificar a sua espada... melhor pensar em outra coisa.";
	return;

_bater:
	if (@fragmentoMenir==1) goto _return;
	set @fragmentoMenir, 1;
	mes "[Menir da Alma]";
	mes "Você bate na pedra com a ponta do cabo de sua espada e um pequeno fragmento se solta.";
	if (TICK_fragmentoMenir==0) goto _energizado;
	specialeffect 26;
	getitem 3379, 1;
	next;
	mes "[Menir da Alma]";
	mes "O fragmento não possui qualquer energia... mas resolve guardá-lo para ver o que acontece.";
	return;

_energizado:
	specialeffect 26;
	getitem 3380, 1;
	next;
	mes "[Menir da Alma]";
	mes "Assim como a menir, o fragmento também emana uma energia mística.";
	mes "O que será que isto pode significar?";
	return;

_return:
	return;

}
