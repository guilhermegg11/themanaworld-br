// Script para Debug

//007-4.gat,95,26,0	script	Debug-Camelia	119,{
	if (getgmlevel(0) < 80) end;
	menu
		"Resetar quest", -,
		"Pular 1 minuto", -,
		"Sair.", -;
	if (@menu == 1) goto L_Resetar;
	if (@menu == 2) goto L_Pular;
	close;

L_Resetar:
	set QUEST_bauCamelia, 0;
	mes "resetado.";
	close;

L_Pular:
	set QUEST_bauCameliaTime, QUEST_bauCameliaTime-60;
	mes "passou 1 min.";
	close;
}

//////////////////////////////////////////////////////////////////////////////////////////
// @script Camélia a Enfermeira
// @autor Diogo_RBG - http://diogorbg.blogspot.com
// @desc Uma enfermeira pronta a lhe ajudar em tudo que puder.
// #quest <desc>, <getItens>, <delItens>, <extra>
// @quest 'Chave para abrir o baú.', '1 chaveBau',, 'Espera de 3:00'
// @quest 'Chave para abrir o baú.', '1 chaveBau',, 'Espera de 10:00'
// @quest 'Chave para abrir o baú.', '1 chaveBau', '1 SharpKnife', 'Espera de 1:00:00'
//////////////////////////////////////////////////////////////////////////////////////////

007-4.gat,97,26,0	script	Camélia	119,{

	if (Sex == 0) set @meu_minha$, "minha";
	if (Sex == 1) set @meu_minha$, "meu";

	if (QUEST_bauCamelia==0) goto _inicio;
	if (QUEST_bauCamelia==1) goto _procuraEquipamento;
	if (QUEST_bauCamelia==2) goto _abraBau;
	if (QUEST_bauCamelia==3) goto _abriuBau;
	if (QUEST_bauCamelia==5) goto _procuraEquipamento2;
	if (QUEST_bauCamelia==6) goto _abraBau;
	if (QUEST_bauCamelia==7) goto _devolveFaca;
	if (QUEST_bauCamelia==10) goto _abraBau;

	if (QUEST_bauCamelia==9 && gettimetick(2)>=QUEST_bauCameliaTime+60*60) goto _chave3;

	mes "[Enfermeira Camélia]";
	mes "\"Olá "+@meu_minha$+" jovem. O que posso fazer por você?\"";
	next;
	menu
		"Você poderia me curar?", _curar,
		"Nada. Obrigado.", -;
	close;

//= Inicio de conversa... fala como vc foi encontrado =================

_inicio:
	mes "[Enfermeira Camélia]";
	mes "\"Olá "+@meu_minha$+" jovem, que bom que acordou! Já estava ficando preocupada, pois já fazia mais de 24 horas que você estava em um sono profundo...\"";
	next;
	menu
		"Fazia?! E como eu vim parar aqui?", _comoPararAqui,
		"Ai... minha cabeça ainda doi.", _comRazao,
		"Você poderia me curar?", _curar;
	goto _explicacao;

_comoPararAqui:
	mes "[Enfermeira Camélia]";
	mes "\"Você não se lembra?!";
	goto _explicacao;

_comRazao:
	mes "[Enfermeira Camélia]";
	mes "\"Com razão!";
	goto _explicacao;

_explicacao:
	mes "Você foi encontrado, gravemente ferido, por pescadores da região e trazido para cá, pois precisava de cuidados médicos.\"";
	next;
	mes "[Enfermeira Camélia]";
	mes "\"Você é muito forte sabia?! E teve muita sorte de ter sobrevivido.\"";
	next;
	mes "[VOCÊ]";
	mes "\"Espere, eu não me lembro de onde eu vim. Qual é o meu nome?\"";
	next;
	mes "\"A unica coisa que eu me lembro é que eu estava caçando quando levei uma forte pancada na cabeça. Antes disto não me lembro de mais nada!\"";
	next;
	mes "[Enfermeira Camélia]";
	mes "\"Vamos fazer assim: enquanto você não se lembrar do seu nome verdadeiro eu vou te chamar de \""+ strcharinfo(0) +"\".\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "\"Gostei do nome! Você sabe se eu possuia alguma coisa comigo?\"";
	next;
	mes "[Enfermeira Camélia]";
	mes "\"Você foi trazido para este hospital apenas com a roupa do corpo e uma faca... mais nada. Provavelmente você foi saqueado, pois é pouco provável alguém ir caçar com roupas de algodão e uma faca! É loucura! Se é que você me entende.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "\"Entendo!\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "\"Mesmo assim, preciso encontrar mais alguma coisa que lembre o meu passado!\"";
	next;
	mes "[Enfermeira Camélia]";
	mes "\"Eu vou perguntar sobre isso aos pescadores que te trouxeram.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "\"Está certo! Enquanto isso vou dar uma volta para tentar lembrar de algo...\"";
	set QUEST_bauCamelia, 1;
	set QUEST_bauCameliaTime, gettimetick(2);
	close;

//= Ganha a chave para abrir o baú p/ pegar a Faca Afiada =============

_procuraEquipamento:
	mes "[Enfermeira Camélia]";
	mes "\"Olá "+@meu_minha$+" jovem, que bom que voltou!\"";
	next;
	menu
		"Você encontrou algo meu?", _calcular1,
		"Você poderia me curar novamente?", _curar,
		"Só passei para cumprimentar...", -;
	mes "[Enfermeira Camélia]";
	mes "\"Ok! Então, tchau!\"";
	close;

_calcular1:
	if (gettimetick(2) >= QUEST_bauCameliaTime + 3*60) goto _chave1;
	mes "[Enfermeira Camélia]";
	mes "\"Tenha calma, pois eu ainda irei falar com os pescadores daqui a "+ (((QUEST_bauCameliaTime+ 3*60)-gettimetick(2))/60+1) +" minuto(s).";
	mes "Eu prometo.\"";
	close;

_chave1:
	mes "[Enfermeira Camélia]";
	mes "\"Eu falei com os pescadores, mas eles disseram que não encontraram muita coisa no local. Só esta [Faca Afiada].\"";
	next;
	menu
		"Posso ver a faca?", _ganhaChave1,
		"A faca não me interessa.", -;
	close;

_ganhaChave1:
	mes "[Enfermeira Camélia]";
	mes "\"Claro, eu guardei ela em meu baú.";
	mes "Você poderia pegar esta chave e abrir meu baú, por favor?\"";
	next;
	menu
		"Sim, pode deixar que eu abro.", -;
	getinventorylist;
	if (@inventorylist_count==100 && countitem(3022)==0) goto _lotado;
	getitem 3022, 1;
	set QUEST_bauCamelia, 2;
	close;

//= Deixa ficar com a Faca Afiada. ====================================

_abraBau:
	mes "[Enfermeira Camélia]";
	mes "\"Abra o baú com a chave que te dei, por favor!\"";
	close;

_abriuBau:
	mes "[Enfermeira Camélia]";
	mes "\"Você encontrou a faca, "+@meu_minha$+" jovem?\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	menu
		"Sim, mas acho que ela não é minha.", _verdadeFaca,
		"Sim, é essa mesma!", _mentiraFaca;
	goto _verdadeFaca;

_verdadeFaca:
	mes "[Enfermeira Camélia]";
	mes "\"Não é! Vou comentar isso com os pescadores.\"";
	next;
	mes "[Enfermeira Camélia]";
	mes "\"Por enquanto fique com ela, já que não tem dono.\"";
	set QUEST_bauCamelia, 5;
	set QUEST_bauCameliaTime, gettimetick(2);
	goto _continuacao2;

_mentiraFaca:
	mes "[Enfermeira Camélia]";
	mes "\"Que bom que encontrou parte de seu equipamento.\"";
	set QUEST_bauCamelia, 4;
	goto _continuacao2;

//= Ganha a chave do baú para pegar a Adaga ===========================

_procuraEquipamento2:
	mes "[Enfermeira Camélia]";
	mes "\"Olá "+@meu_minha$+" jovem, que bom que voltou!\"";
	next;
	goto _continuacao2;

_continuacao2:
	menu
		"Você encontrou mais alguma coisa?", _calcular2,
		"Você poderia me curar?", _curar,
		"Te vejo depois...", -;
	close;

_calcular2:
	if(gettimetick(2) >= QUEST_bauCameliaTime + 10*60) goto _chave2;

	mes "[Enfermeira Camélia]";
	mes "\"Tenha calma, pois eu ainda irei falar com os pescadores daqui a "+ (((QUEST_bauCameliaTime+ 10*60)-gettimetick(2))/60+1) +" minutos.";
	mes "Volte mais tarde.\"";
	close;

_chave2:
	mes "[Enfermeira Camélia]";
	mes "\"Um pescador encontrou algo perto de onde você foi encontrado.\"";
	next;
	menu
		"Posso ver o que foi encontrado?", _ganhaChave2,
		"Depois eu passo aqui para ver...", -;
	mes "[Enfermeira Camélia]";
	mes "\"Ok! Então até logo.\"";
	close;

_ganhaChave2:
	mes "[Enfermeira Camélia]";
	mes "\"Sim, você pode.";
	mes "E aqui está a chave. Você já sabe o que fazer...\"";
	getinventorylist;
	if (@inventorylist_count==100 && countitem(3022)==0) goto _lotado;
	getitem 3022, 1;
	set QUEST_bauCamelia, 6;
	close;

//= Pede a Faca Afiada de volta. ======================================

_devolveFaca:
	mes "[Enfermeira Camélia]";
	mes "\"Então... você encontrou o que procurava no baú?\"";
	next;
	menu
		"Sim.. eu me lembro dessa Adaga.", -;
	mes "[Enfermeira Camélia]";
	mes "\"Por acaso você ainda está com a [Faca Afiada] que te dei?\"";
	next;
	menu
		"Sim. E quero devolver.", _devolver,
		"Não. Eu já a vendi.", _vendeu,
		"Você poderia me curar?", _curar;
	close;

_vendeu:
	mes "[Enfermeira Camélia]";
	mes "\"Desta vez você me decepcionou. Como pode vender algo que não era seu?";
	set QUEST_bauCamelia, 8;
	close;

_devolver:
	if (countitem(522)==0) goto _semFaca;

	mes "[Enfermeira Camélia]";
	mes "\"Você me surpreende! Nem precisei pedir que devolvesse aquela faca.";
	mes "Vou continuar a perguntar sobre seu equipamento. Tenha a certeza disso.\"";
	delitem 522, 1;
	set QUEST_bauCameliaTime, gettimetick(2);
	set QUEST_bauCamelia, 9;
	close;

_semFaca:
	mes "[Enfermeira Camélia]";
	mes "\"Eu acho que você perdeu a faca que te dei.\"";
	next;
	menu
		"Sim! Me perdoe. Eu não tive a intenção!", -;
	mes "[Enfermeira Camélia]";
	mes "\"Tudo bem!\"";
	close;

//= Ganha a chave do baú para pegar o Short de Algodão. ===============

_chave3:
	mes "[Enfermeira Camélia]";
	mes "\"Eu tenho duas notícias para você. Uma boa e uma não tão boa.\"";
	next;
	menu
		"Começe pela notícia boa.", -,
		"Começe pela notícia não tão boa.", -; 
	mes "[Enfermeira Camélia]";
	mes "\"Vou começar pela não tão boa:";
	mes "Os pescadores voltaram aqui e me avisaram que mais nada foi encontrado.";
	mes "Sem esperanças de encontrar o resto do seu equipamento.\"";
	next;
	menu
		"E a notícia boa.", -;
	mes "[Enfermeira Camélia]";
	mes "\"A notícia boa é que eles foram solidários e lhe mandaram um presente.";
	mes "Pegue a chave de meu baú e descubra o que tem lá.\"";
	getinventorylist;
	if (@inventorylist_count==100 && countitem(3022)==0) goto _lotado;
	getitem 3022, 1;
	set QUEST_bauCamelia, 10;
	set QUEST_bauCameliaTime, 0;
	close;

//= Cura e funções diversas. ==========================================

_curar:
	if (baselevel > 10) goto _temPagar;
	mes "[Enfermeira Camélia]";
	mes "\"Você realmente não me pacere bem.";
	mes "Mas essa agulhada vai resolver!\"";
	next;
	set @grito, rand(3);
	if(@grito==0) message strcharinfo(0), "Ai!";
	if(@grito==1) message strcharinfo(0), "Eu não gosto de injeção. Ui!";
	if(@grito==2) message strcharinfo(0), "Ai! Ai! Ai! Ai! Ai! ";
	if(@grito==3) message strcharinfo(0), "Vai dueeeeeeeeer.... O.o!";
	percentheal 100, 0;
	close;

_temPagar:
	mes "[Enfermeira Camélia]";
	mes "\"Sim, eu posso. Mas você terá que me ajudar também.";
	mes "Esta injeção é cara e exitem outras pessoas mais necessitadas.";
	mes "Mas eu poderia te curar por 500 GP.\"";
	next;
	menu
		"Tudo bem... eu pago.", _pagarOk,
		"Não mesmo!", -;
	close;

_pagarOk:
	if(zeny<500) goto _semGrana;
	set zeny, zeny - 500;
	mes "[Enfermeira Camélia]";
	mes "\"Tudo bem então. Vou aplicar a injeção.\"";
	next;
	set @grito, rand(4);
	if(@grito==0) message strcharinfo(0), "Ai!";
	if(@grito==1) message strcharinfo(0), "Eu não gosto de injeção. Ui!";
	if(@grito==2) message strcharinfo(0), "Ai! Ai! Ai! Ai! Ai! ";
	if(@grito==3) message strcharinfo(0), "Vai dueeeeeeeeer.... O.o!";
	percentheal 100, 0;
	close;

_lotado:
	mes "[Enfermeira Camélia]";
	mes "Você não tem espaço para minha chave. Volte mais tarde.";
	close;

_semGrana:
	mes "[Enfermeira Camélia]";
	mes "\"Você não tem dinheiro o suficiente para pagar pela injeção.\"";
	close;

}
