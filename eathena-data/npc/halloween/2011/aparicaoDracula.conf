/**
 * Testes com o Drácula.
 * @author Diogo_RBG - http://diogorbg.blogspot.com/
 * @date segunda, 03 de outubro de 2011
 */

005.gat,100,79,0	script	#dracula	0,{
	mes "[Drácula]";
	mes "\"Trago meus filhos e filhas para desfrutar do banquete que seus pescoços nos oferecem.";
	mes "Não há arma ou magia... nada poderá nos deter.\"";
	mes "";
	mes "* Halloween 2011, esperamos você aqui no mundo de MANA.";
	next;
	mes "[Drácula]";
	mes "\"Nesse halloween, as mais monstruosas criaturas, que em seus pesadelos viveram por anos, estão de volta para te aterrorizar e quem sabe, tirar um pedacinho seu!";
	mes "Mwhahahahahahahahaha!!!\"";
	mes "";
	mes "* Não falte, dias 24, 25, 26, 27, 28, 29, 30 e 31 de outubro. Estamos à sua espera.";
	close;

OnMinute01:
	if(getnpctimer(0)!=0) end;
	initnpctimer;
	end;

_subMorcegos:
	set $@n, 14 - mobcount("005.gat", "#dracula::OnKill");
	if($@n<1) return;
	areamonster "005.gat", 100-2, 79-2, 100+2, 79+2, "", 1348, $@n, "#dracula::OnKill";
	return;

OnKill:
	if(getnpctimer(0)>330100) end;
	set $@n, mobcount("005.gat", "#dracula::OnKill");
	if($@n>0) end;
	emotion 5;
	callsub _subMorcegos;
	end;

OnTimer100:
	killmonster "005.gat","#dracula::OnKill";
	announce "Boatos dizem que nuvens negras se aproximam da Capital de Bhramir... isto não parece ser um bom sinal.", 0;
	end;
OnTimer30100:
	announce "Todos devem esta alertas ao menor sinal de perigo. Afinal o Halloween está próximo.", 0;
	end;
OnTimer60100:
	announce "Os boatos se confirmam. Nuvens negras realmente estão indo em direção à Capital de Bhramir.", 0;
	end;
OnTimer90100:
	announce "Atenção bravos guerreiros do mundo de mana. Vamos combater esta ameaça que se aproxima da Capital de Bhramir.", 0;
	end;
OnTimer120100:
	announce "As nuvens negras se dispersaram sobre a Capital de Bhramir... algo está prestes a acontecer.", 0;
	end;
OnTimer150100:
	announce "A Capital de Bhramir acaba de ser invadida por morcegos negros. Isto não está certo!", 0;
	fakenpcname "#dracula", "", 365;
	callsub _subMorcegos;
	end;
OnTimer150600:
	fakenpcname "#dracula", "", 366;
	end;
OnTimer180100:
	announce "Conde Drácula acaba de surgir entre os morcegos! É o fim!", 0;
	fakenpcname "#dracula", "Drácula", 367;
	npctalk "Mwahahahahahah!!!";
	callsub _subMorcegos;
	end;
OnTimer240100:
	fakenpcname "#dracula", "Drácula", 368;
	npctalk "Mwahahahahahah!!!";
	callsub _subMorcegos;
	end;
OnTimer270100:
	fakenpcname "#dracula", "Drácula", 368;
	npctalk "Mwahahahahahah!!!";
	callsub _subMorcegos;
	end;
OnTimer300100:
	fakenpcname "#dracula", "Drácula", 368;
	npctalk "Mwahahahahahah!!!";
	callsub _subMorcegos;
	end;
OnTimer330100:
	fakenpcname "#dracula", "", 369;
	npctalk "Pobres vítimas. Estarão todos mortos antes mesmo do Halloween.";
	npctalk "Logo eu voltarei!";
	announce "Conde Drácula parece ter sido derrotado. Mas ele voltará.", 0;
	callsub _subMorcegos;
	end;
OnTimer331500:
	fakenpcname "#dracula", "", 0;
	end;
OnTimer360100:
	stopnpctimer;
	setnpctimer 0;
	announce "Halloween 2011... estejam preparados! 24 a 31 de outubro.", 0;
	end;

}
