// Criado por: Xtreem
// Revisado por: Sky
//http://forums.themanaworld.com.br/viewtopic.php?f=57&t=4244&p=38822#p38822

007.gat,80,87,0   script   Guilherme   156,{
   mes "[Guilherme]";
   mes "\"Eu descobri uma maneira de tirar a tintura das roupas usando pilhas de cinzas.\"";
   mes "\"Eu posso fazer isso para você, mas precisarei de 3 pilhas de cinzas e 5 mil GP.\"";
   next;

   if (countitem(701) < 3) goto L_no_ash;

   if (zeny < 5000) goto L_no_money;

   menu "Eu gostaria de descolorir uma roupa", -,
	   "Não, obrigado", L_close;

L_bleach_menu:
   mes "[Guilherme]";
   mes "\"O que você quer descolorir?\"";
   next;

   menu
	  "Camisa de Algodão", L_camisadealgodao,
	  "Suéter com decote em V", L_sueterDecoteV,
	  "Suéter de Gola", L_SueterGolaAlta,
	  "Calça de Algodão", L_shortdealgodao,
	  "Saia de Algodão", L_saiadealgodao,
	  "Mini saia", L_minisaia,
	  "Camiseta de algodão ", L_camiseta,
	  "Camiseta curta de algodão", L_camisetacurta,
	  "Tunica de Seda", L_tunicadeseda,
	  "Faixa tradicional", L_faixadealgodao,
	  "Chapéu do deserto", L_chapeudodeserto,
	  "Botas de algodão", L_botasdealgodao,
	  "Luvas de algodão", L_luvasdealgodao,
	  "Nao quero descolorir nada.", -;
   goto L_close;

L_camisadealgodao:
   set @normal, 1202;
   set @dyeBase, 2050;
   goto L_choose_color;

L_sueterDecoteV:
   set @normal, 624;
   set @dyeBase, 2060;
   goto L_choose_color;

L_SueterGolaAlta:
   set @normal, 564;
   set @dyeBase, 2070;
   goto L_choose_color;

L_shortdealgodao:
   set @normal, 586;
   set @dyeBase, 2110;
   goto L_choose_color;

L_saiadealgodao:
   set @normal, 632;
   set @dyeBase, 2100;
   goto L_choose_color;

L_minisaia:
   set @normal, 3048;
   set @dyeBase, 2170;
   goto L_choose_color;

L_camiseta:
   set @normal, 688;
   set @dyeBase, 2090;
   goto L_choose_color;

L_camisetacurta:
   set @normal, 689;
   set @dyeBase, 2120;
   goto L_choose_color;

L_tunicadeseda:
   set @normal, 720;
   set @dyeBase, 2080;
   goto L_choose_color;

L_faixadealgodao:
   set @normal, 724;
   set @dyeBase, 2140;
   goto L_choose_color;

L_chapeudodeserto:
   set @normal, 723;
   set @dyeBase, 2130;
   goto L_choose_color;

L_botasdealgodao:
   set @normal, 735;
   set @dyeBase, 2150;
   goto L_choose_color;

L_luvasdealgodao:
   set @normal, 741;
   set @dyeBase, 2160;
   goto L_choose_color;

L_choose_color:
   mes "[Guilherme]";
   mes "\"E a cor?\"";
   next;

   menu "Vermelho", -,
	   "Verde", -,
	   "Azul escuro", -,
	   "Amarelo", -,
	   "Azul Claro", -,
	   "Rosa", -,
	   "Preto", -,
	   "Laranja", -,
	   "Roxo", -,
	   "Verde Escuro", -;
   
   set @del, @dyeBase + @menu - 1;

   if (countitem(@del) == 0) goto L_no_item;

   if (countitem(701) < 3) goto L_no_ash;

   if (zeny < 5000) goto L_no_money;

   delitem @del, 1;
   getitem @normal, 1;
   delitem 701, 3;
   set zeny, zeny - 5000;

L_again:
   mes "[Guilherme]";
   mes "\"Quer descolorir mais alguma coisa?\"";
   next;

   menu "Sim", L_bleach_menu,
	   "Não, obrigado", L_close;

L_no_item:
   mes "[Guilherme]";
   mes "\"Você não tem nenhum desses. Desculpe.\"";
   next;

   goto L_again;

L_no_ash:
   mes "[Guilherme]";
   mes "\"Você não tem cinza suficiente.\"";
   mes "\"Eu preciso de 3 Pilhas de Cinza.\"";
   next;

   goto L_close;

L_no_money:
   mes "[Guilherme]";
   mes "\"Você não tem dinheiro suficiente.\"";
   mes "\"Eu preciso de 5 mil GP para custos adicionais.\"";
   next;

L_close:
   mes "\"Volte sempre.\"";
   close;
}
