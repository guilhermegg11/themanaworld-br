//
// @autor Diogo_RBG - http://diogorbg.blogspot.com
// @desc Joana adora as tortas de maçã de sua prima Cornélia.
// #quest <desc>, <getItens>, <delItens>, <extra>
// @quest 'Cesta de Maçãs', '1 cestaMacas', '', 'Esperar 1:00'
// @quest '+ Gratidão', '10 Apple', '', ''

//004.gat,48,101,0	script	time	163,{
//	set QUEST_tortaMacaTime, QUEST_tortaMacaTime-60;
//	mes "time avançou 60 seg.";
//	close;
//}

004.gat,51,101,0	script	Joana	163,{
	if (Sex == 0) set @meu_a$, "minha";
	if (Sex == 1) set @meu_a$, "meu";

	callfunc "getTime";
	if(QUEST_tortaMaca==2 && @time>=QUEST_tortaMacaTime+60) goto _pronto;
	if(QUEST_tortaMaca==2) goto _espere;
	if(QUEST_tortaMaca==3) goto _entregue;
	if(QUEST_tortaMaca==4) goto _tortaAssando;
	if(QUEST_tortaMaca==5) goto _tortaPronta;
	if(QUEST_tortaMaca==6) goto _ok;

	mes "[Joana]";
	mes "\"Olá "+@meu_a$+" jovem. Eu estou colhendo maçãs para que minha prima faça uma torta. Ela faz tortas deleciosas, sabia?\"";
	if (QUEST_tortaMaca==0) goto _fala1;
	if (QUEST_tortaMaca==1) goto _fala2;

_fala1:
	next;
	menu
		"Quem é sua prima?", _quem,
		"hum... até mais.", -;
	close;

_quem:
	mes "[Joana]";
	mes "\"Minha prima se chama Cornélia e mora em uma cabana na praia do porto.\"";
	next;
	menu
		"Obrigado pela informação. Talvez eu compre tortas dela.", -;
	close;

_fala2:
	next;
	menu
		"Acabei de falar com sua prima e ela está preocupada com sua demora.", -;
	mes "[Joana]";
	mes "\"É mesmo. Já faz muito tempo que saí para colher maças. Mas já estou quase acabando.";
	mes "Eu ficaria muito grata se você pudesse avisar a ela ou pudesse me esperar... só vou levar 1 minuto.\"";
	next;
	menu
		"Ok... vou avisar a ela.", -,
		"Ok... vou esperar.", -;
	callfunc "getTime";
	set QUEST_tortaMacaTime, @time;
	set QUEST_tortaMaca, 2;
	close;

_espere:
	mes "[Joana]";
	mes "\"Já estou quase acabando de colher as maçãs.";
	mes "Eu ficaria muito grata se você pudesse avisar a ela ou pudesse me esperar... só vou levar 1 minuto.\"";
	next;
	menu
		"Ok... vou avisar a ela.", -,
		"Ok... vou esperar.", -;
	close;

_pronto:
	mes "[Joana]";
	mes "\"Ponto... acabei de colher as maçãs. Ufa !";
	mes "Por gentileza, você poderia me fazer um favor?\"";
	next;
	mes "[Joana]";
	mes "\"Você poderia levar esta cesta de maçãs para minha prima Cornélia?\"";
	next;
	menu
		"Você prometeu as maçãs... você leva!", -,
		"Farei agora mesmo.", _levarCesta;
	close;

_levarCesta:
	mes "[Joana]";
	mes "\"Você é realmente muito gentil!\"";
	next;
	getinventorylist;
	if( (@inventorylist_count + (countitem(3027)==0) ) > 100 ) goto _cheio;

	mes "[Joana]";
	mes "\"Leve esta cesta para minha prima Cornélia e diga a ela que amanhã irei provar de sua deliciosa torta.\"";
	getitem 3027, 1; //Cesta de maçãs
	set QUEST_tortaMaca, 3;
	close;

_entregue:
	mes "[Joana]";
	mes "\"Por favor, entregue a cesta de maçãs que te dei para minha prima Cornélia.\"";
	close;

//=====================================================================

_tortaAssando:
	mes "[Joana]";
	mes "\"Entregou a cesta de maças para minha prima?\"";
	next;
	menu
		"Sim... inclusive ela já está preparando a torta.", -;
	close;

_tortaPronta:
	mes "[Joana]";
	mes "\"Entregou a cesta de maças para minha prima?\"";
	next;
	menu
		"Claro!", -,
		"Sim. E tenho uma fatia de torta para você.", _darTorta;
	getinventorylist;
	if( (@inventorylist_count + (countitem(535)==0) ) > 100 ) goto _cheio;

	mes "[Joana]";
	mes "\"Obrigada!";
	mes "Fique com estas maçãs... Um pequeno pagamento por ter me ajudado.\"";
	getitem 535, 10; //Maçãs
	set QUEST_tortaMaca, 6;
	close;

_darTorta:
	if (countitem(3028)<1) goto _semTorta;
	getinventorylist;
	if( (@inventorylist_count + (countitem(535)==0) ) > 100 ) goto _cheio;

	mes "[Joana]";
	mes "\"Eu adoro esta torta de maçã que minha prima faz. É deliciosa!";
	mes "Fique com estas maçãs... Um pequeno pagamento por ter me ajudado.\"";
	delitem 3028, 1;
	getitem 535, 20; //Maçãs
	set QUEST_tortaMaca, 6;
	close;

_semTorta:
	mes "[Joana]";
	mes "\"Onde está a torta? Não estou vendo!\"";
	next;
	menu
		"Me desculpe... me enganei!", -;
	close;

_cheio:
	mes "[Joana]";
	mes "\"Pretendia lhe dar algo, mas parece que sua sacola está cheia.";
	mes "Volte mais tarde.\"";
	close;

_ok:
	mes "[Joana]";
	mes "\"Obrigada por me ajudar!\"";
	close;

}
