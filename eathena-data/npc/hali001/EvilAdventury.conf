// Criado por: SKDB
// Revisado por: Sky


hali001.gat,103,92,0	script	#Aventureiro	300,7,7,{ 
//npctalk Reino+"←=→"+$PosseDeFroz;
if(Reino!=$PosseDeHalicarnazo) goto _Raiva;
if(BaseLevel < 40 || QUEST_AventureiroDemoniaco >= 2) end;
	emotion 13; // Emote de quest!!! XD 
end;

_Raiva:
	emotion 11; // Emote de Raiva pelo invasor!!! XD 
	heal -(MaxHp/10),0; // NPC Ataca o invasor tirando 10% de HP!!
end;

}

//###################################################################################

hali001.gat,103,93,0	script	Aventureiro	522,1,1, {
	set @Osso, 775;
	set @RetalhoDeCouro, 708;
	set @AdagaBoniClaide, 570;
	set @NomeDoReino$, "Halicarnazo";

	if(Reino!=$PosseDeHalicarnazo) goto _SugestaoAoInvasor;
	if(QUEST_AventureiroDemoniaco==0 && BaseLevel<40) goto L_semLevel;
	if(QUEST_AventureiroDemoniaco==1) goto L_Retorno;
	if(QUEST_AventureiroDemoniaco==2) goto L_Fim;
goto L_iniciaQuest;

_SugestaoAoInvasor:
	heal -(MaxHp/10),0;
	set @tmp,rand(4);
	if(@tmp == 0) npctalk "Saia daqui seu invasor de "+ @NomeDoReino$+"!";
	if(@tmp == 1) npctalk "Você não pode entrar em "+ @NomeDoReino$+"!";
	if(@tmp == 2) npctalk "Como você se atreve a tentar machucar as pessoas do Reino de "+ @NomeDoReino$+"?";
	if(@tmp == 3) npctalk "Eu te mostrarei do que o povo de "+ @NomeDoReino$+" é feito.";
end;

L_semLevel:
	mes "[Aventureiro Demoníaco]";
	mes "\"Quer ir ao inferno mais cedo? Cai fora.\"";
close;

L_iniciaQuest:
	mes "[Aventureiro Demoníaco]";
	mes "\"Bom dia aventureiro. Venho chegando do inferno há pouco trazendo uma arma de uma alma que capturei. No inferno essa arma era chamada de a arma que matou os mafiosos.\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"Uau, que interessante hein? T.T", -,
	"Wow! Será que poderia me mostrá-la?, ou me vendê-la?", L_Oferta;
	mes "";
	mes "[Aventureiro Demoníaco]";
	mes "\"Cai fora, Antes que eu te leve para minha proxima viagem até o inferno!\"";
close;

L_Oferta:
	mes "";
	mes "[Aventureiro Demoníaco]";
	mes "\"Claro, mas tudo nesta vida tem seu preço.\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"=l Aff! pra mostrar vai querer coisas, acha que nasci ontem? Fui!", -,
	"O que você quer?", L_DizNecessidades;
close;

L_DizNecessidades:
	mes "";
	mes "[Aventureiro Demoníaco]";
	mes "\"Vou precisar de 30 Ossos, 2 Retalho de Couro e 100K GP, pois minha viagem até o inferno custa caro.\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"Está de brincadeira né?", -,
	"Está caro! Mas estou precisando de uma arma nova, daqui a pouco te trago.", L_ok;
close;

L_ok:
	mes "";
	mes "[Aventureiro Demoníaco]";
	mes "\"Não demore pois preciso ir embora rápido, Hades não pode se dar conta que sai do inferno.\"";
	next;
	mes "["+strcharinfo(0)+"]";
	mes "\"Já vou buscar.\""; // ← Se só tem uma única opção usa-se "mes" ao inveś de "menu"
	set QUEST_AventureiroDemoniaco, 1;
close;

L_Retorno:
	mes "[Aventureiro Demoníaco]";
	mes "\"Trouxe tudo que te pedi humano?\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"Está tudo aqui", -,
	"Não, não sou o flash, guenta ai!", L_naotenho;
	if(zeny<100000 || countitem(@Osso)<30 || countitem(@RetalhoDeCouro)<2) goto L_naotem;
	getinventorylist;
	if (@inventorylist_count >=100) goto L_semlugar;
	delitem @Osso, 30;
	delitem @RetalhoDeCouro, 2;
	set zeny, zeny-100000;
	getitem @AdagaBoniClaide, 1;
	set QUEST_AventureiroDemoniaco, 2;
	mes "";
	mes "[Aventureiro Demoníaco]";
	mes "\"Aqui está sua faca.\"";
	next;
	mes "[Aventureiro Demoníaco]";
	mes "\"Faça bom proveito dela.\"";
close;

L_naotem:
	mes "[Aventureiro Demoníaco]";
	mes "\"Está tentando me enganar humano?\"";
close;

L_naotenho:
	mes "[Aventureiro Demoníaco]";
	mes "\"Não tenho muito tempo para te esperar, Me trega logos os 30 ossos 2 retalhos de couro e 100KGP.\"";
close;

L_semlugar:
	mes "[Aventureiro Demoníaco]";
	mes "\"Você é o que? Teu inventário está lotado vai lá depositar algo e volte logo meu tempo está acabando!\"";
close;

L_Fim:
	mes "[Aventureiro Demoníaco]";
	mes "\"Aleluia conseguiu, agora saia do meu caminho.\"";
close;

}
