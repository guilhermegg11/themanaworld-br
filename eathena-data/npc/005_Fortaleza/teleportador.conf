//
// Teleporte... o jeito fácil de se locomover no jogo.
//

// Mais falas
// mes "Mas isto não é nenhuma novidade. Dês de tempos remotos o homem viajou sendo guiado pelas estrelas.\"";

005.gat,122,74,0	script	Teleportadora	350,{

	if (@introTeleport==1) goto _menu;
	set @introTeleport, 1;

	mes "[Teleportadora Adhara]";
	mes "\"A ligação entre o nosso mundo e as estrelas é bem maior do que podemos imaginar.";
	mes "Eu acredito que os pontos deste mundo se ligam da mesma forma em que as estrelas se ligam no céu.\"";
	next;
	goto _menu;

_menu:
	mes "[Teleportadora Adhara]";
	mes "\"Guiada pelas estrelas eu poço te levar a muitos lugares... basta existir uma conexão entre eles.";
	mes "Para onde gostaria de ir?\"";
	menu
		//"Vila dos Pescadores (1 kGP)", _vilaPescadores,
		"Porto de Bhramir (3 kGP)", _portoBhramir,
		"Polo Norte (3 kGP)", _poloNorte,
		"Terras Desconhecidas (5 kGP)", _terrasDesconhecidas,
		"Para Locais de Batalha.", _menuBatalha,
		"Desisti de viajar.", -;
	close;

//----------------------------------------------------------------------

_vilaPescadores:
	callsub _pegarGrana, 1000;
	callsub _teleportar, "001", 70, 65;
	end;

_portoBhramir:
	callsub _pegarGrana, 3000;
	callsub _teleportar, "007", 105, 81;
	end;

_poloNorte:
	callsub _pegarGrana, 3000;
	callsub _teleportar, "010", 165, 85;
	end;

_terrasDesconhecidas:
	callsub _pegarGrana, 5000;
	callsub _teleportar, "org016", 63, 85;
	end;

//----------------------------------------------------------------------

_menuBatalha:
	mes "[Teleportadora Adhara]";
	mes "\"Também posso te levar direto para locais de batalha. Mas só para os locais que eu considerar seguro para você.";
	mes "Para onde gostaria de ir?\"";
	menu
		"Caverna do Eremita lvl 20 (3 kGP)", _cavernaEremita,
		"Caverna Luminosa lvl 30 (3 kGP)", _cavernaLuminosa,
		"Cavernas Alaguadas lvl 40 (3 kGP)", _cavernasAlagadas,
		"Mina da Areia lvl 50 (5 kGP)", _minaAreia,
		"Caverna Assombrada lvl 60 (5 kGP)", _cavernaAssombrada,
		"Caverna da Montanha lvl 70 (7 kGP)", _cavernaMontanha,
		"Pântano lvl 80 (7 kGP)", _pantano,
		"Cemitério lvl 90 (10 kGP)", _cemiterio,
		"Melhor não arriscar!", -;
	close;

//----------------------------------------------------------------------

_cavernaEremita:
	callsub _lvl, 20;
	callsub _pegarGrana, 3000;
	callsub _teleportar, "007-1", 95, 35;
	end;

_cavernaLuminosa:
	callsub _lvl, 30;
	callsub _pegarGrana, 3000;
	callsub _teleportar, "009-2", 36, 66;
	end;

_cavernasAlagadas:
	callsub _lvl, 40;
	callsub _pegarGrana, 3000;
	callsub _teleportar, "008-1", 61, 91;
	end;

_minaAreia:
	callsub _lvl, 50;
	callsub _pegarGrana, 5000;
	callsub _teleportar, "008-4", 36, 90;
	end;

_cavernaAssombrada:
	callsub _lvl, 60;
	callsub _pegarGrana, 5000;
	callsub _teleportar, "007-2", 160, 38;
	end;

_cavernaMontanha:
	callsub _lvl, 70;
	callsub _pegarGrana, 7000;
	callsub _teleportar, "009-1", 40, 88;
	end;

_pantano:
	callsub _lvl, 80;
	callsub _pegarGrana, 7000;
	callsub _teleportar, "org026", 36, 54;
	end;

_cemiterio:
	callsub _lvl, 90;
	callsub _pegarGrana, 10000;
	callsub _teleportar, "org027", 71, 42;
	end;

//----------------------------------------------------------------------

_teleportar:
	mes "[Teleportadora Adhara]";
	mes "\"Boa viagem\"";
	close2;
	warp getarg(0)+".gat", getarg(1), getarg(2);
	end;

_lvl:
	if (BaseLevel<getarg(0) ) goto _semLvl;
	return;

_semLvl:
	mes "[Teleportadora Adhara]";
	mes "\"Me desculpe, mas este não me parece um local seguro para você.\"";
	close;

_pegarGrana:
	if (zeny<getarg(0) ) goto _semGrana;
	set zeny, zeny-getarg(0);
	return;

_semGrana:
	mes "[Teleportadora Adhara]";
	mes "\"Me desculpe, mas você não tem dinheiro suficiente para esta viagem.\"";
	close;

}
