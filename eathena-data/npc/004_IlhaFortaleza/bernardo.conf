//
// Quest traduzida do org.
//

// Para testar o funcionamento do script.
//004.gat,86,54,0	script	debug	117,{
//	callfunc "debug";
//	set @tmp, 0;
//	set QUEST_MASK1, QUEST_MASK1 & ~MASK1_SOPA_VERMES; //< seta apenas flag MASK1_SOPA_VERMES como 0.
//	set QUEST_sopaVermes, 0;
//	close;
//}

004.gat,89,54,0	script	Bernardo	117,{
	set @vermeTorrado, 533;
	set @bolo, 513;
	set @gosmaVerme, 505;
	set @cerveja, 539;

	if (BaseLevel < 5) goto _semLvl;
	if (QUEST_MASK1 & MASK1_SOPA_VERMES) goto _feito2;
	if (QUEST_sopaVermes == 3) goto _emProgresso2;
	if (QUEST_sopaVermes == 2) goto _feito1;
	if (QUEST_sopaVermes == 1) goto _emProgresso;

	set @tmp, rand(2);
	if (@tmp == 0) goto _fala0;
	if (@tmp == 1) goto _fala1;

_semLvl:
	mes "[Bernardo]";
	mes "\"Humm... que fome. Preciso fazer algo para comer.\"";
	goto _close;

_fala0:
	mes "[Bernardo]";
	mes "\"Estas areias estão enfestadas de vermes O_o.";
	mes "Ei, eu preciso de um favor, você poderia me dar uma pequena ajuda?\"";
	goto _pergunta;

_fala1:
	mes "[Bernardo]";
	mes "\"O gosto de sopa de vermes é... inesquecível... simplesmente divino!";
	mes "Você me ajudaria a matar alguns?\"";
	goto _pergunta;

_pergunta:
	menu
		"Claro! Porque não.", _sim,
		"Agora não.", _close;
	goto _close;

_sim:
	set @tmp, rand(2);
	if(@tmp == 0) goto _req0;
	if(@tmp == 1) goto _req1;

_req0:
	mes "[Bernardo]";
	mes "\"Ótimo! Eu preciso de um [Verme Torrado] para a minha sopa.\"";
	next;
	goto _set;

_req1:
	mes "[Bernardo]";
	mes "\"Traga-me um [Verme Torrado]. Vou te dar alguma coisa, se você me trouxer.\"";
	next;
	goto _set;

_set:
	set QUEST_sopaVermes,1;
	mes "[Bernardo]";
	mes "\"Por favor, traga-o para mim!\"";
	goto _close;

_emProgresso:
	if (countitem(@vermeTorrado) < 1) goto _naoTrouxe;
	mes "[Bernardo]";
	mes "\"Oooh, perfeito! Isto é perfeito!";
	mes "Você me trouxe o meu [Verme Torrado]! Aqui, aceite estas fatias de bolo. São deliciosas.\"";
	getinventorylist;
	if (@inventorylist_count - (countitem(@vermeTorrado) == 1) > 99 - (countitem(@bolo) == 0) ) goto _cheio;
	delitem @vermeTorrado, 1;
	getexp 100, 0;
	getitem @bolo, 10;
	set QUEST_sopaVermes, 2;
	goto _close;

_emProgresso2:
	if (countitem(@gosmaVerme) < 3) goto _naoTrouxe1;
	mes "[Bernardo]";
	mes "\"Ótimo! Estas 3 gosmas servem. Agora poderei fazer minha deliciosa sopa cremosa de vermes!";
	mes "Você me trouxe as 3 gosmas! Aqui, tome um pouco de cerveja como prêmio.\"";
	getinventorylist;
	if ((@inventorylist_count - (countitem(@gosmaVerme) == 3) > 99  - (countitem(@cerveja) == 0) )) goto _cheio;
	delitem @gosmaVerme, 3;
	getexp 100, 0;
	getitem @cerveja, 3;
	set QUEST_MASK1, QUEST_MASK1 | MASK1_SOPA_VERMES; //< Seta flag MASK1_SOPA_VERMES como 1.
	set QUEST_sopaVermes, 0;
	goto _close;

_naoTrouxe:
	mes "[Bernardo]";
	mes "\"Por favor, se apresse e me traga um [Verme Torrado]. Eu estou ansiando por uma sopa de vermes!\"";
	goto _close;

_naoTrouxe1:
	mes "[Bernardo]";
	mes "\"Por favor, se apresse e me traga 3 [Gosma de Verme], para que eu possa terminar minha sopa!\"";
	goto _close;

_feito1:
	mes "[Bernardo]";
	mes "\"Muito obrigado!";
	mes "Mas... ainda estão faltando ingredientes para deixar a sopa cremosa.\"";
	next;
	mes "[Bernardo]";
	mes "\"Preciso de 3 [Gosma de Verme] para isso.";
	mes "Traga-as para mim, e eu lhe darei algo melhor que fatias de bolo.\"";
	set QUEST_sopaVermes, 3;
	goto _close;

_feito2:
	mes "[Bernardo]";
	mes "\"Eu não mencionei isso antes, mas eu também coloco cerveja em minha sopa. XD \"";
	next;
	mes "[Bernardo]";
	mes "\"Espero que goste de cerveja tanto como eu! Hahahah\"";
	goto _close;

_cheio:
	next;
	mes "[Bernardo]";
	mes "\"Você não tem espaço para a minha recompensa. Vou esperar até que você tenha espaço.\"";
	goto _close;

_close:
	set @tmp, 0;
	set @vermeTorrado, 0;
	set @bolo, 0;
	set @gosmaVerme, 0;
	set @cerveja, 0;
	close;

}
