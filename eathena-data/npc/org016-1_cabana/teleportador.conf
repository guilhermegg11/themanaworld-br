//
// Teleporte... Apenas para voltar.
//

org016-1.gat,26,27,0	script	Teleportadora	330,{

	if (@introTeleport==1) goto _menu;
	set @introTeleport, 1;

	mes "[Teleportadora Bellatrix]";
	mes "\"A ligação entre o nosso mundo e as estrelas é bem maior do que podemos imaginar.";
	mes "Eu acredito que os pontos deste mundo se ligam da mesma forma em que as estrelas se ligam no céu.\"";
	next;
	goto _menu;

_menu:
	mes "[Teleportadora Bellatrix]";
	mes "\"Guiada pelas estrelas eu poço te levar a muitos lugares... basta existir uma conexão entre eles.";
	mes "Para onde gostaria de ir?\"";
	menu
		"Vila dos Pescadores (5 kGP)", _vilaPescadores,
		"Porto de Bhramir (7 kGP)", _portoBhramir,
		"Centro de Bhramir (10 kGP)", _centroBhramir,
		"Para Locais de Batalha.", _menuBatalha,
		"Desisti de viajar.", -;
	close;

//----------------------------------------------------------------------

_vilaPescadores:
	callsub _pegarGrana, 5000;
	callsub _teleportar, "001", 80, 78;
	end;

_portoBhramir:
	callsub _pegarGrana, 7000;
	callsub _teleportar, "007", 100, 86;
	end;

_centroBhramir:
	callsub _pegarGrana, 10000;
	callsub _teleportar, "005", 127, 84;
	end;

//----------------------------------------------------------------------

_menuBatalha:
	mes "[Teleportadora Bellatrix]";
	mes "\"Também posso te levar direto para locais de batalha. Mas só para os locais que eu considerar seguro para você.";
	mes "Para onde gostaria de ir?\"";
	menu
		"Pântano lvl 80 (2 kGP)", _pantano,
		"Cemitério lvl 90 (5 kGP)", _cemiterio,
		"Melhor não arriscar!", -;
	close;

//----------------------------------------------------------------------

_pantano:
	callsub _lvl, 80;
	callsub _pegarGrana, 2000;
	callsub _teleportar, "org026", 36, 52;
	end;

_cemiterio:
	callsub _lvl, 90;
	callsub _pegarGrana, 5000;
	callsub _teleportar, "org027", 71, 42;
	end;

//----------------------------------------------------------------------

_teleportar:
	mes "[Teleportadora Bellatrix]";
	mes "\"Boa viagem\"";
	callsub _levitar;
	close2;
	callsub _levitar; //< Se o jogador demorar a fechar.
	specialeffect2 311;
	warp getarg(0)+".gat", getarg(1), getarg(2);
	specialeffect2 310;
	end;

_levitar:
	if(getnpctimer(0)!=0) return;
	initnpctimer;
	fakenpcname "Teleportadora", "Teleportadora", 331;
	return;

OnTimer2600:
	stopnpctimer;
	setnpctimer 0;
	fakenpcname "Teleportadora", "Teleportadora", 330;
	end;

_lvl:
	if (BaseLevel<getarg(0) ) goto _semLvl;
	return;

_semLvl:
	mes "[Teleportadora Bellatrix]";
	mes "\"Me desculpe, mas este não me parece um local seguro para você.\"";
	close;

_pegarGrana:
	if (zeny<getarg(0) ) goto _semGrana;
	set zeny, zeny-getarg(0);
	return;

_semGrana:
	mes "[Teleportadora Bellatrix]";
	mes "\"Me desculpe, mas você não tem dinheiro suficiente para esta viagem.\"";
	close;

}
