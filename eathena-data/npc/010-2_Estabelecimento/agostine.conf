//
// @author Shaili and QOAL
// @translate Diogo_RBG

//010-2.gat,28,21,0	script	debug	137,{
//	callfunc "debug";
//	menu
//		"set 0", -,
//		"set 2", -,
//		"set retalho 0", -,
//		"set retalho 4", -,
//		"sair", -;
//	if(@menu==1) set QUEST_agostine, 0;
//	if(@menu==2) set QUEST_agostine, 2;
//	if(@menu==3) set QUEST_AgostineRetalho, 0;
//	if(@menu==4) set QUEST_AgostineRetalho, 4;
//	close;
//}

010-2.gat,31,20,0	script	#Agostine	300,7,7,{ 
if(QUEST_agostine>=9) end;
	emotion 13; // Emote de quest!!! XD 
end;
}

//############################################################################

010-2.gat,31,21,0	script	Agostine	137,1,1,{
	set @retalhoPelucia, 3218;
	set @orelhasUrso, 3219;
	set @peloBranco, 611;

	setarray @meuAmigo$, "minha amiga", "meu amigo";
	// @en "my friend" "my friend"
	setarray @obrigado$, "obrigada", "obrigado";
	// @en "thanks" "thanks"
	setarray @charmoso$, "charmosa", "charmoso";
	// @en "glamourous" "glamourous"

	if (QUEST_AgostineRetalho>0 ) goto _inicioOrelhas;
	if (QUEST_agostine >= 2 && countitem(@retalhoPelucia)>=1 && (QUEST_MASK1 & MASK1_URSINHO) ) goto _inicioOrelhas;

	if (QUEST_agostine == 1) goto _estado_0_3;
	if (QUEST_agostine == 2) goto _estado_1;
	if (QUEST_agostine == 3) goto _inicioLuvas;
	if (QUEST_agostine == 4) goto _trouxePele1;
	if (QUEST_agostine == 5) goto _trouxePele1_success;
	if (QUEST_agostine == 6) goto _inicioBotas;
	if (QUEST_agostine == 7) goto _trouxePele2;
	if (QUEST_agostine == 8) goto _trazerBotas;
	if (QUEST_agostine >= 9) goto _obrigado;

	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Isto vai para cima, esta vai para a esquerda...\"";
	// @en "\"This goes up, this goes left...\""
	next;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Mmmm...?\"";
	next;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Oh, um cliente! Deixe-me apresentar! Meu nome é Agostine!";
	// @en "\"Oh, a customer! Let me introduce myself! My name is Agostine!"
	mes "Algumas pessoas dizem que eu sou o melhor alfaiate do mundo, mas eu acho que sou o melhor do universo!";
	// @en "Some people say that I am the best tailor in the world, but I think I am the best one in the universe!"
	mes "Então, o que posso fazer por você, " +@meuAmigo$[sex]+ "?\"";
	// @en "So, What can I do for you, " "?\""
	next;
	menu
		"Eu quero algo novo para o meu guarda-roupa!", _estado_0_1,
		// @en "I want something new for my wardrobe!"
		"Oh, nada, " +@obrigado$[sex]+ "!", -;
		// @en "Oh, nothing, " "!"
	close;

// Quest #0 - Beginning - Needed: Iron Potion

_estado_0_1:
	setarray @seuGarcon$, "sua garçonete", "seu garçom";
	// @en "your waiter" "your waiter"

	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Bem.. você sabe... trabalhar tão duro que me deixa tão cansado!";
	// @en "\"Well.. you know... working so hard makes me so tired!"
	mes "Você pode me trazer algo saboroso para beber, " +@meuAmigo$[sex]+ "?\"";
	// @en "Can you bring me something tasty to drink, " "?\""
	next;
	menu
		"Claro, que tipo de bebida você gosta?", _estado_0_2,
		// @en "Sure, what kind of drink you like?"
		"Eu não sou " +@seuGarcon$[sex]+ "!", -;
		// @en "I'm not " "!"
	close;

_estado_0_2:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Aqui não temos um bom bar. Deixe-me saborear bebidas diferentes.";
	// @en "\"Here we do not have a good bar. Let me taste different beverages.";
	mes "Quero algo novo para beber.\"";
	// @en "I want something new to drink.\"";
	next;
	menu
		"Eu vou lhe trazer bebidas diferentes para experimentar!", _estado_Accept,
		// @en "I'll bring you different drinks to try!"
		"Talvez um bom bar será aberto em breve, espere-o.", -;
		// @en "Maybe a good bar will open soon, wait for it."
	close;

_estado_Accept:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Muito obrigado.\"";
	// @en "\"Thanks so much.\""
	set QUEST_agostine, 1;
	close;

_estado_0_3:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Bem, o que você me trouxe?\"";
	// @en "\"Well, what have you brought me?\""
	next;
	menu
		"Uma [Bebida de Cactus].", _estado_0_4,
		// @en "A [Bebida de Cactus]."
		"Uma [Poção de Cactus].", _estado_0_5,
		// @en "A [Poção de Cactus]."
		"Um pouco de [Leite].", _estado_0_6,
		// @en "Some [Leite]."
		"Um copo de [Cerveja].", _estado_0_7,
		// @en "A pint of [Cerveja]."
		"Uma [Poção Energética].", _estado_0_9,
		// @en "A [Poção Energética]."
		"Uma [Poção de Concentração].", _estado_0_8,
		// @en "A [Poção de Concentração]."
		"Nada, no momento.", -;
		// @en "Nothing, at the moment."
	close;

_estado_0_4:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Humm, uma [Bebida de Cactus]?\"";
	// @en "\"So, a [Bebida de Cactus]?\""
	if (countitem(501) < 1) goto _estado_neg;
	next;
	delitem 501, 1;
	goto _estado_bad;
	close;

_estado_0_5:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Humm, uma [Poção de Cactus]?\"";
	// @en "\"So, a [Poção de Cactus]?\""
	if (countitem(502) < 1) goto _estado_neg;
	next;
	delitem 502, 1;
	goto _estado_bad;
	close;

_estado_0_6:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Humm, um pouco de [Leite] para mim?\"";
	// @en "\"So, some [Leite] for me?\""
	if (countitem(527) < 1) goto _estado_neg;
	delitem 527, 1;
	goto _estado_bad;
	close;

_estado_0_7:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Humm, uma [cerveja]?\"";
	// @en "\"So, a [cerveja]?\""
	if (countitem(539) < 1) goto _estado_neg;
	next;
	delitem 539, 1;
	goto _estado_bad;
	close;

_estado_0_8:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Humm, uma [Poção de Concentração]?\"";
	// @en "\"So, a [Poção de Concentração]?\"";
	if (countitem(568) < 1) goto _estado_neg;
	next;
	delitem 568, 1;
	goto _estado_bad;
	close;

_estado_0_9:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Humm, uma [Poção Energética]?\"";
	// @en "\"So, an [Poção Energética]?\""
	if (countitem(567) < 1) goto _estado_neg;
	next;
	delitem 567, 1;
	mes "Agostine parece satisfeito pela forma que ele bebe a poção.";
	// @en "Agostine looks pleased as he drinks the potion."
	next;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Oh! Isso é muito bom! Eu me sinto um novo homem, " +@meuAmigo$[sex]+ "!\"";
	// @en "\"Oh! This is damn good! I feel a new man, " "!\""
	next;
	set zeny, zeny + 500;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Esta é uma pequena recompensa por sua ajuda. Pegue 500 GP, " +@meuAmigo$[sex]+ "!\"";
	// @en "\"This is a little reward for your help. Take 500 GP, " "!\""
	mes "";
	mes "* Você recebeu 500 GP.";
	// @en "* You received 500 GP."
	set QUEST_agostine, 2;
	close;

_estado_neg:
	mes "";
	mes "\"Parece que você mentiu. Você não tem a bebida que você me disse.\"";
	// @en "\"Seems that you lied. You don't have the drink you told me.\""
	close;

_estado_bad:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Bem, eu não gostei muito. Traga-me algo mais.\"";
	// @en "\"Well, I don't like it so much. Bring me something else.\"";
	close;

// First Quest: edited version of Forest Bow Quest - Needed: about 30 White Furs and 15.000 GP

_estado_1:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Mmmm... Deixe-me ver... Este ano tons claros estão na moda!";
	// @en "\"Mmmm... Let me see... This year light colours are fashionable!"
	mes "Gostaria de um par de [Luvas de Inverno] branco como a neve?\"";
	// @en "Would you like a pair of snow-white [Luvas de Inverno]?\""
	next;
	menu
		"Não, " +@obrigado$[sex]+ ". Eu prefiro roupas escuras...", -,
		// @en "No, " ". I love dark clothes..."
		"Você realmente pode fazer isso por mim?", _inicioLuvas;
		// @en "You really can do this for me?"
	close;

_inicioLuvas:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Bem, [Luvas de Inverno] são tão elegantes por sua pele adorável!";
	// @en "\"Well, [Luvas de Inverno] are so fashionable for their lovable fur!"
	mes "Somente as melhores peles podem servir para estas luvas!";
	// @en "Only the best furs can be saw for these gloves!"
	mes "Mas, como você vê, eu sou um alfaiate, não um caçador! Então, por que você não me traz o melhor pelo macio que você puder encontrar?\"";
	// @en "But, as you see, I am a tailor, not a hunter! So, why don't you bring me the best fluffy's fur you can see?\""
	set QUEST_agostine, 3;
	next;
	menu
		"Você está louco! Eu não vou matar nenhum animal para isso!", -,
		// @en "You are crazy! I won't kill any animal for this!"
		"Claro, eu voltarei em breve!", _estado_3;
		// @en "Sure, I will be back soon!"
	close;

_estado_3:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Lembre-se, eu quero a melhor [Pele Branca]!\"";
	// @en "\"Remember, I want the best of [Pele Branca]!\""
	set QUEST_agostine, 4;
	close;

_trouxePele1:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Oh, " +@meuAmigo$[sex]+ "! Você já me trouxe alguma boa [Pele Branca]?\"";
	// @en "\"Oh, " "! Have you brought me some good [Pele Branca]?\""
	if (countitem(@peloBranco) < 1) close;
	next;
	menu
		"Aqui, dê uma olhada!", _trouxePele1_try,
		// @en "Here, take a look!"
		"Sim, mas eu preciso dela para outra coisa.", -;
		// @en "Yes, but I need it for something else."
	close;

_trouxePele1_try:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Deixe-me ver, " +@meuAmigo$[sex]+ "! Ela precisa ser perfeita para minhas luvas!\"";
	// @en "\"Let me see, " "! It needs to be perfect for my gloves!\"";
	mes "";
	mes "Agostine analisa o pedaço de pele.";
	// @en "Agostine examines the patch of fur.";
	next;
	delitem @peloBranco, 1;
	set @Temp1,rand(30);
	if (@Temp1 == 0) goto _trouxePele1_success;
	mes "Agostine joga fora a [Pele Branca].";
	// @en "Agostine throws away the [Pele Branca]."
	next;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Foi terrivelmente cortada! Você deveria prestar mais atenção quando você for matar Tufos!\"";
	// @en "\"It was awfully cut! You should pay more attention when you kill Tufos!\""
	if (countitem(@peloBranco) < 1) close;
	next;
	menu
		"Aqui, tenho outra!", _trouxePele1_try,
		// @en "Here I have another one!"
		"Desculpe, vou tomar cuidado...", -;
		// @en "Sorry, I will be careful..."
	close;

_trouxePele1_success:
	mes "Agostine retorna um sorriso.";
	// @en "Agostine turns into a smile."
	next;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"É perfeito! Perfeito! Bom trabalho, " +@meuAmigo$[sex]+ "! Vou preparar as luvas agora mesmo!\"";
	// @en "\"It's perfect! Perfect! Good job, " "! I will prepare your gloves right now!\""
	next;
	set QUEST_agostine, 5;
	goto _estado_5;

_estado_5:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Ehm, " +@meuAmigo$[sex]+ ", meu trabalho não é de graça. Eu preciso de 15.000 GP para o trabalho.\"";
	// @en "\"Ehm, " ", my work is not free. I need 15,000 GP for the work.\"";
	next;
	menu
		"Você não é um alfaiate, você é um ladrão!!", -,
		// @en "You are not a tailor, you are a thief!!"
		"Eu acho que é um preço razoável...", _estado_5_pay;
		// @en "I think it's a reasonable price..."
	close;

_estado_5_pay:
	if (zeny < 15000) goto _estado_5_nocash;
	getinventorylist;
	if (@inventorylist_count == 100) goto _semEspaco;
	set zeny, zeny - 15000;
	getitem 563, 1;
	set QUEST_agostine, 6;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Aqui estão elas. Você vai ter as mãos mais elegantes do mundo!\"";
	// @en "\"Here they are. You will have the more fashionable hands in the world!\""
	close;

_estado_5_nocash:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Parece que você está sem dinheiro. Volte quando você tiver o dinheiro.\"";
	// @en "\"Seems like you are out of cash. Come back when you have the money.\""
	close;

_inicioBotas:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Oh, eu lembro de você! Suas luvas são tão elegantes, " +@meuAmigo$[sex]+ "!";
	// @en "\"Oh, I remember you! Your gloves are sooo fashionable, " "!"
	mes "Bem, eu tenho uma boa notícia para você. Nesta edição do 'TMW-BR Fashion' eu vi um bonito acessório, um par de [Botas de Inverno], " +@meuAmigo$[sex]+ ".";
	// @en "Well, I have a good news for you. In this issue of 'TMW-BR Fashion' I saw a pretty accessory, a pair of [Botas de Inverno], " "."
	mes "Eu posso fazer seu próprio par, se você quiser!\"";
	// @en "I can make your own pair, if you want!\""
	next;
	menu
		"É uma ótima idéia!", _estado_10,
		// @en "It's a great idea!"
		"Já estou bastante " +@charmoso$[sex]+ ", " +@obrigado$[sex]+ ".", -;
		// @en "I'm " " enough, " "."
	close;

// Start of Second Quest: Furry Boots - Needed: About 30 White Furs, 1 pair of Boots and 15.000 GP

_estado_10:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Perfeito! Para satisfazer o seu pedido, eu preciso de outra perfeita [Pele Branca] e um par de [Botas], como aquelas que você pode encontrar nas minas.";
	// @en "\"Perfect! For satisfying your request I need another perfect [Pele Branca] and a pair of [Botas], like those you can find in the mines."
	mes "Elas vão ficar ótimas em você, " +@meuAmigo$[sex]+ "!\"";
	// @en "They will make you lovely, " "!\""
	set QUEST_agostine, 7;
	close;

_trouxePele2:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Então, " +@meuAmigo$[sex]+ ", me trouxe uma correta [Pele Branca]?\"";
	// @en "\"So, " ", have you brought me the right [Pele Branca]?\""
	if (countitem(@peloBranco) < 1) close;
	next;
	menu
		"Claro, eu sou um caçador de Tufos", _trouxePele2_try,
		// @en "Sure, I'm a fluffy hunter!"
		"Ainda não, desculpe.", -;
		// @en "Not yet, sorry."
	close;

_trouxePele2_try:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Eu prefiro ver o pedaço de pele primeiro.\"";
	// @en "\"I'd rather see the patch of fur, first.\""
	mes "";
	mes "Agostine analisa o pedaço de pele.";
	// @en "Agostine examines the patch of fur."
	next;
	delitem @peloBranco, 1;
	set @Temp2,rand(30);
	if (@Temp2 == 0) goto _trouxePele2_success;
	mes "Agostine rasga a [Pele Branca].";
	// @en "Agostine rips the [Pele Branca]."
	next;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Essa pele era terrível. Eu não vou trabalhar com ela!\"";
	// @en "\"That fur was terrible. I won't work with it!\""
	if (countitem(@peloBranco) < 1) close;
	next;
	menu
		"Bem, talvez esta seja a melhor!", _trouxePele2_try,
		// @en "Well, maybe this is better!"
		"Vou caçar outros Tufos...", -;
		// @en "I will hunt other fluffies..."
	close;

_trouxePele2_success:
	mes "Agostine retorna um sorriso.";
	// @en "Agostine turns into a smile.";
	next;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Esta é boa o suficiente, " +meuAmigo$[sex]+ ". Bom trabalho.\"";
	// @en "\"This one is good enough, " ". Good job.\""
	delitem @peloBranco, 1;
	set QUEST_agostine, 8;
	goto _trazerBotas;

_trazerBotas:
	next;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Eu preciso também de um par de [Botas] para o trabalho.";
	// @en "\"I need also a pair of [Botas] for the work."
	mes "E o meu pagamento, obviamente. 15.000 GP mais precisamente.\"";
	// @en "And my payment, obviously. 15,000 GP is right.\""
	next;
	menu
		"Claro, aqui está tudo.", _trazerBotas_pay,
		// @en "Sure, here it is all."
		"Eu perdi alguma coisa, já volto!", -;
		// @en "I missed something, I will be back soon!
	close;

_trazerBotas_pay:
	if (zeny < 15000) goto _trazerBotas_missing;
	if (countitem(528) < 1) goto _trazerBotas_missing;
	// No inventory check needed, as boots are removed, opening a slot
	set zeny, zeny - 15000;
	delitem 528, 1;
	getitem 655, 1;
	set QUEST_agostine, 9;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Aproveite suas novas botas, " +meuAmigo$[sex]+ "!\"";
	// @en "\"Enjoy your new boots, " "!\""
	close;

_trazerBotas_missing:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Parece que você esqueceu algo. Verifique o seu inventário e seus bolsos!\"";
	// @en "\"Seems you have forgotten something. Check your inventory and your pockets!\""
	close;

_obrigado:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Você está tão " +@charmoso$[sex]+ ", " +@meuAmigo$[sex]+ "!";
	// @en "\"You are so " ", " "!"
	mes "Eu tenho feito um excelente trabalho em sua roupa!\"";
	// @en "I have done a very good job on your clothes!\""
	close;

_semEspaco:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Você não tem lugar para colocá-los. Volte quando tiver.\"";
	// @en "\"You don't have anywhere to put them. Come back when you do.\""
	close;

//= QUEST RETALHO DE PELÚCIA ===========================================

_inicioOrelhas:
	if( QUEST_AgostineRetalho==1 ) goto _verRetalho;
	if( QUEST_AgostineRetalho==2 ) goto _grandeIdeia;
	if( QUEST_AgostineRetalho==3 ) goto _trouxePeles3;
	if( QUEST_AgostineRetalho==4 ) goto _confeccaoOrelhas;

	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Hoje estou com vontade de fazer algo diferente... algo novo, inusitado e nada convencional.";
	mes "Mas não sei ainda o que fazer...\"";
	next;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Talvez você poça me ajudar! Eu preciso que você escolha um tecido para meu novo trabalho. Mas não escolha qualquer um... tem que ser algo inusitado.\"";
	next;
	mes "Agostine coloca uma venda nos olhos e impacientemente o espera com sua escolha.";
	next;
	mes "Ele não deu nenhuma pista sobre o novo trabalho que ele pretende fazer... ele deixou você decidir.";
	next;
	goto _menuTecido;

_menuTecido:
	mes "Qual tecido você escolhe para o novo trabalho de Agostine?";
	menu
		"Tecido de Algodão.", _tecidoAlgodao,
		"Tecido de Seda.", _tecidoSeda,
		"Tecido de Pelúcia (do ursinho)", _tecidoPelucia,
		"nenhum", -;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Você acaba de perder a chance de me ver criando uma obra prima.\"";
	close;

_tecidoAlgodao:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Algodão?! Algodão é simples de mais. Não acredito que demorou todo este tempo para me dar esta sugestão!\"";
	next;
	mes "Agostine parece não ter gostado de sua escolha... melhor pensar em outra coisa.";
	next;
	goto _menuTecido;

_tecidoSeda:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Já estou acostumado a trabalhar em tecidos de seda... eu quero algo incomum!\"";
	next;
	mes "Agostine parece não ter gostado de sua escolha... melhor pensar em outra coisa.";
	next;
	goto _menuTecido;

_tecidoPelucia:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Pelúcia! É perfeito! Mas onde foi que você viu este tecido?\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "> \"Eu trouxe um retalho de pelúcia comigo!\"";
	set QUEST_AgostineRetalho, 1;
	next;
	goto _verRetalho;

_verRetalho:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Deixe-me ver seu retalho de pelúcia.\"";
	menu
		"Claro... aqui está.", _verRetalho2,
		"Melhor deixar para depois.", -;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"É uma pena. Eu poderia fazer um belo trabalho com ele.\"";
	close;

_verRetalho2:
	if(countitem(@retalhoPelucia)<1) goto _semRetalho;
	mes "Agostine examina seu retalho de pelúcia e logo chega a uma conclusão.";
	next;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"É exatamente o tipo de tecido que procurava. Incomum... inusitado.";
	mes "O problema é que além de ser apenas um pequeno retalho ele está cheio de pequenas imperfeições. Não posso aproveitar muita coisa dele.\"";
	next;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Posso saber porque guarda este retalho?\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "> \"Este foi um presente de um amiguinho peludo! Foi presente de um ursinho.\"";
	next;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Sério?! Que incrível! Nunca vi ninguém fazer amizade com um urso.\"";
	next;
	mes "Agostine se perde em seu pensamento por um instante e volta cheio de ideias.";
	set QUEST_AgostineRetalho, 2;
	next;
	goto _grandeIdeia;

_grandeIdeia:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Eu acabei de ter uma grande ideia sobre o que fazer com este retalho de pelúcia, mas vou precisar que me traga alguns materiais para o trabalho.";
	mes "E não se preocupe... você com certeza irá gostar de meu trabalho!\"";
	next;
	menu
		"O que devo trazer?", _trazerPeles,
		"Não estou interessado.", -;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Você não está duvidando do meu trabalho! Está?.\"";
	close;

_trazerPeles:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Se você me trouxer 10 Peles Branca e 10.000 GP pelo serviço eu poderei trabalhar em algo muito especial.\"";
	next;
	menu
		"Me parece justo.", _precoJusto,
		"Você está me explorando.", -;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Só estou cobrando o preço dos materiais que irei utilizar, mas se você não quiser tudo bem.\"";
	close;

_precoJusto:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Esplendido! Estarei esperando que me traga os materiais e o retalho de pelúcia é claro!\"";
	set QUEST_AgostineRetalho, 3;
	close;

_trouxePeles3:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Você me trouxe as 10 Peles Branca e os 10.000 GP para o trabalho?\"";
	next;
	menu
		"Tenho tudo... Vamos iniciar o trabalho.", _iniciarTrabalho,
		"Não trouxe as 10 Peles Branca!", -,
		"Esqueci o retalho de pelúcia!", -,
		"Não tenho todo o dinheiro!", -;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Assim que você me trouxer 10 Peles Branca e 10.000 GP poderei iniciar o meu trabalho.\"";
	close;

_iniciarTrabalho:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Muito bem. Coloque tudo em cima de meu balcão.\"";
	next;
	if (countitem(@retalhoPelucia) < 1) goto _semRetalho;
	if (countitem(@peloBranco) < 10) goto _semPeles;
	if (Zeny < 10000) goto _semGrana;
	mes "Você coloca todos os materiais sobre o balcão e Agostine começa a trabalhar no mesmo instante.";
	delitem @retalhoPelucia, 1;
	delitem @peloBranco, 10;
	set Zeny, Zeny - 10000;
	set QUEST_AgostineRetalho, 4;
	next;
	goto _confeccaoOrelhas;

_confeccaoOrelhas:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Fique atento em meu trabalho. Você irá se surpreender!\"";
	next;
	mes "Agostine faz uns rabiscos num papel grosso e dele recorta alguns moldes. Logo depois posiciona os moldes cuidadosamente sobre o retalho de pelúcia em locais que não estão danificados e os fixa com alguns alfinetes e em seguida, com uma tesoura enorme, começa a cortar o seu retalho de pelúcia.";
	next;
	mes "A peça começa a ganhar forma assim que Agostine começa a costurar, mas ainda não dá pra saber o que é. Luvas, botas, meias... poderia ser qualquer coisa!";
	next;
	mes "Você fica ainda mais surpreso quando Agostine começa a encher a peça com pelo de tufo cortado!";
	next;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"O segredo de deixar bem fofinho é enchendo apenas com o pelo mais macio, mesmo que não se aproveite muito de cada Pelo Branco.\"";
	next;
	mes "Depois de enchê-las de pelo de tufo, Agostine termina de costurar as peças e às prende em uma Haste.";
	next;
	getinventorylist;
	if( @inventorylist_count >= 100) goto _cheio;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Está pronto! Veja que belas orelhas de urso!\"";
	mes "";
	mes "Ao dizer isso Agostine lhe entrega o resultado de seu trabalho.";
	getitem @orelhasUrso, 1;
	set QUEST_AgostineRetalho, 0; //< Limpa a variável e permite refazer a quest várias vezes.
	next;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Imaginei que fazer orelhas de urso daquele retalho de pelúcia seria o melhor a ser feito. Até mesmo pelo laço de afeto que aquele retalho ligava você ao seu amiguinho urso.";
	mes "Espero que tenha gostado do meu trabalho! Dei o meu melhor.\"";
	next;
	menu
		"Adorei o seu trabalho!", -,
		"Ficou ótimo, abrigado!", -,
		"Lindinhas essas orelhinhas de urso!", -;
	mes "[Agostine o Lendário Alfaiate]";
	mes "\":P Obrigado!\"";
	close;

_semRetalho:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Você não trouxe nenhum retalho de pelúcia... o que fez com ele?\"";
	close;

_semPeles:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Você não trouxe as 10 Peles Branca. Preciso delas para iniciar o trabalho.\"";
	close;

_semGrana:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Você não tem 10.000 GP para pagar meus materiais. Preciso do dinheiro para iniciar o trabalho.\"";
	close;

_cheio:
	mes "[Agostine o Lendário Alfaiate]";
	mes "\"Infelizmente você não tem espaço para carregar a peça que criei para você! Volte mais tarde.\"";
	close;

}
