/**
 * Quest da Poção para Queimaduras.
 * Quest destinada aos leveis iniciais, sem vínculo com nenhuma historia
 * do jogo, apenas para passar o tempo e ganhar experiência.
 *
 * @date quarta, 11 de janeiro de 2012
 * @author King
 */

//001.gat,33,99,0	script	debug	349,{
//	callfunc "debug";
//	mes "QUEST_pocaoQueimadura: "+QUEST_pocaoQueimadura;
//	menu
//		"set QUEST_pocaoQueimadura, 0", -,
//		"set _falarComFlora, 1", -,
//		"set _checarPocao1, 2", -,
//		"set _checarPocao1, 3", -,
//		"set _checarPocao2, 4", -,
//		"set _licao, 5", -,
//		"Sair", _close;
//	set QUEST_MASK1, QUEST_MASK1 & ~MASK1_POCAO_QUEIMADURA; //< seta apenas MASK1_POCAO_QUEIMADURA como 0.
//	set QUEST_pocaoQueimadura, @menu-1;
//	close;
//
//_close:
//	close;
//}

001.gat,36,99,0	script	Júlia        	349,{

	if (BaseLevel<18) goto _falas;

	if (QUEST_MASK1 & MASK1_POCAO_QUEIMADURA) goto _obrigada;
	if (QUEST_pocaoQueimadura==0) goto _inicio;
	if (QUEST_pocaoQueimadura==1) goto _falarComFlora;
	if (QUEST_pocaoQueimadura<=3) goto _checarPocao1;
	if (QUEST_pocaoQueimadura==4) goto _checarPocao2;
	if (QUEST_pocaoQueimadura==5) goto _licao;
	goto _falas;

_falas:
	mes "[Júlia]";
	mes "\"Adoro curtir uma praia.";
	mes "A areia fininha, o vento que refresca e o mar que embala meu descanso indo pra lá e pra cá.\"";
	close;

_inicio:
	mes "["+strcharinfo(0)+"]";
	mes "\"Olá...\"";
	next;
	mes "[Júlia]";
	mes "\"Ai ai ai, olá...\"";
	next;
	menu
		"Algo está doendo?", -,
		"(Ai ai ai?)", -;
	mes "[Júlia]";
	mes "\"Meu corpo todo está doendo... Ui ui ui...\"";
	next;
	menu
		"Oque aconteceu com você?", _resposta1,
		"Espero que melhore...", -;
	close;

_resposta1:
	mes "[Júlia]";
	mes "\"Ai ui... É que eu adoro a praia e por isso passo muito tempo aqui...\"";
	next;
	mes "["+strcharinfo(0)+"]";
	mes "\"Muito... Quanto tempo passa aqui?\"";
	next;
	mes "[Júlia]";
	mes "\"Hum... Passo quase meu tempo todo aqui em frente ao mar, me bronzeando pra ficar cada vez mais linda...\"";
	next;
	mes "["+strcharinfo(0)+"]";
	mes "\"Acho que ouvi falar que isso não faz muito bem ao corpo...\"";
	next;
	mes "[Júlia]";
	mes "\"Deve ser isso mesmo... Estou toda doída...\"";
	next;
	menu
		"È uma pena... Até mais...", -,
		"Eu poderia te ajudar?", _ajudar1;
	close;

_ajudar1:
	mes "[Júlia]";
	mes "\"Ahh, pode sim... Estou toda queimada, você poderia ir até a Flora e pegar uma poção para queimaduras com ela...\"";
	next;
	mes "["+strcharinfo(0)+"]";
	mes "\"Hum, onde a Flora está?\"";
	next;
	mes "[Júlia]";
	mes "\"É fácil encontrá-la. Procure-a nas montanhas.";
	mes "Ela costuma colher ervas medicinais na encostas destas montanhas para fazer suas poções.\"";
	next;
	mes "[Júlia]";
	mes "\"Ao norte, entre pela passagem da Gruta Escura e alagada e siga até chegar à floresta que fica na parte alta das montanhas.\"";
	next;
	menu
		"Agora não posso fazer isso, deixe pra mais tarde...", -,
		"Tudo bem, volto logo...", _ajudar2;
	close;

_ajudar2:
	set QUEST_pocaoQueimadura, 1;
	mes "[Júlia]";
	mes "\"Fico muito agradecida, ai ai ui...\"";
	close;

_falarComFlora:
	mes "[Júlia]";
	mes "\"Conseguiu a poção?\"";
	next;
	mes "["+strcharinfo(0)+"]";
	mes "\"Eu nem consegui encontrar a Flora.\"";
	next;
	mes "[Júlia]";
	mes "\"É fácil encontrá-la. Procure-a nas montanhas.";
	mes "Ela costuma colher ervas medicinais na encostas destas montanhas para fazer suas poções.\"";
	next;
	mes "[Júlia]";
	mes "\"Ao norte, entre pela passagem da Gruta Escura e alagada e siga até chegar à floresta que fica na parte alta das montanhas.\"";
	next;
	mes "[Júlia]";
	mes "\"Por favor não demore, ai ai ui...\"";
	close;

_checarPocao1:
	mes "[Júlia]";
	mes "\"Encontrou a Flora e troxe minha poção?\"";
	next;
	mes "["+strcharinfo(0)+"]";
	mes "\"Eu encontrei a Flora. Mas a sua poção ainda não está pronta.";
	mes "Melhor esperar na sombra.\"";
	next;
	mes "[Júlia]";
	mes "\"É o que vou fazer.";
	mes "Como está doendo... Ui ui ui...\"";
	close;

_checarPocao2:
	mes "[Júlia]";
	mes "\"Conseguiu a poção?\"";
	next;
	mes "["+strcharinfo(0)+"]";
	mes "\"Tive que pegar todos os ingredientes pra fazer... Um  trabalhão, mas tá aqui...\"";
	next;
	goto _pegarItens;

_pegarItens:
	set @pocaoQueimadura, 3404;
	if (countitem(@pocaoQueimadura)<1) goto _insuficiente1;

	mes "[Júlia]";
	mes "\"Muito obrigada, agora sim vou melhorar...\"";
	delitem @pocaoQueimadura, 1;
	set QUEST_pocaoQueimadura, 5;
	next;
	goto _licao;

_insuficiente1:
	mes "[Júlia]";
	mes "\"Onde está? Você perdeu a poção?\"";
	next;
	mes "["+strcharinfo(0)+"]";
	mes "\"Não pode ser! Eu estava com ela agora pouco!\"";
	close;

_licao:
	mes "["+strcharinfo(0)+"]";
	mes "\"E então, aprendeu alguma lição com isso?\"";
	next;
	mes "[Júlia]";
	mes "\"Sim... é muito bom ficar na praia, se bronzear e tal, mas temos que ter limites... Não é recomendado ficar exposto ao sol das 10:00 às 16:00.";
	mes "Só fora desse horário é saudável aproveitar...\"";
	next;
	mes "["+strcharinfo(0)+"]";
	mes "\"Isso mesmo, agora está bem, vou indo...\"";
	next;
	mes "[Júlia]";
	mes "\"Jovem, tenho um presente de agradecimento pra você...\"";
	next;
	menu
		"Não precisa...", -,
		"É mesmo?... O que é?! O que é?!", -;
	//------------------------------------------------------------------
	getinventorylist;
	if( (@inventorylist_count + 1) >= 100) goto _cheio;
	mes "[Júlia]";
	mes "\"Pegue aqui esta boia para poder aproveitar a praia comigo...\"";
	set @tmp, rand(127);
	if (@tmp<127) set @item, 3399; // 1
	if (@tmp<63) set @item, 3400;  // 2
	if (@tmp<31) set @item, 3401;  // 3
	if (@tmp<15) set @item, 3398;  // rinoceronte
	if (@tmp<7) set @item, 3395;   // patinho
	if (@tmp<3) set @item, 3397;   // jiboia
	if (@tmp<1) set @item, 3396;   // elefantinho
	getitem @item, 1;
	set QUEST_pocaoQueimadura, 0;
	set QUEST_MASK1, QUEST_MASK1 | MASK1_POCAO_QUEIMADURA;
	//------------------------------------------------------------------
	next;
	mes "["+strcharinfo(0)+"]";
	mes "\"Ahh sim... Muito obrigado. Vou aproveitar com consciência, só no horário certo...\"";
	next;
	mes "[Júlia]";
	mes "\"Até mais... E muito obrigado...\"";
	close;

_cheio:
	mes "[Júlia]";
	mes "\"Que pena, você não tem espaço para meu presente.";
	mes "Estarei esperando você voltar com mais espaço.\"";
	close;

_obrigada:
	mes "[Júlia]";
	mes "\"Agora sim! Aquela poção para queimaduras é realmente milagrosa.";
	mes "Me sinto ótima agora. Obrigada!\"";
	close;

}
