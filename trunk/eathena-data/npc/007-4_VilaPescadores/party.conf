//////////////////////////////////////////////////////////////////////////
// LUNOVOX: O nome original do NPC era "Tathin" que toquei por "Tomas".
//          10GP? 50GP? Esse preços realmente serão usados? =/
//////////////////////////////////////////////////////////////////////////

007-4.gat,63,57,0	script	Tomas	107,{

	set @PARTY_SKILL, 3; // <---- Para que ser esta linha?

	mes "[Tomas]";
 //@en "\"Hello what Can I do for you?\""
	mes "\"Olá! O que posso fazer por você?\"";
	next;

	if (getskilllv(@PARTY_SKILL) == 2 ) goto _JaTemParty;
	if (BaseLevel >= 15 && getskilllv(@PARTY_SKILL) == 1) goto _PedePartyCriar;
	if (getskilllv(@PARTY_SKILL) == 1) goto _JaTemParty;
	if (BaseLevel >= 10) goto _PedePartyParticipar;

_JaTemParty:
	mes "["+ strcharinfo(0) +"]";
	menu
//@en "Nothing, I guess."
		"Nada, eu acho.", -;
	close;

_PedePartyParticipar:
	mes "["+ strcharinfo(0) +"]";
	menu
//@en "I'd like to get a party permit."
		"Eu gostaria de obter uma licença de Grupo(Party).", _PrecoPartyParticipar,
//@en "Nothing, I guess."
		"Nada, eu acho.", -;
	close;

_PedePartyCriar:
	mes "["+ strcharinfo(0) +"]";
	menu
//@en "I'd like to get a party creator permit."
		"Eu gostaria de ter autorização para criar um Grupo(Party).", _PrecoPartyCriar,
//@en "Nothing, I guess."
		"Nada, eu acho.", -;
	close;

_PrecoPartyParticipar:
	mes "[Tomas]";
 //@en "\"A permit to join parties costs 10 GP.\""
	mes "\"A autorização para aderir a um Grupo(Party) custa 10 GP.\"";
	next;

	mes "["+ strcharinfo(0) +"]";
	menu
//@en "OK, I accept!"
		"OK, Eu aceito!", _GanhaPartyParticipar,
//@en "No, thank you!"
		"Não, obrigado!", -;
	close;

_GanhaPartyParticipar:
	if (zeny < 10) goto _SemGrana;
	set zeny, zeny - 10;
	setskill @PARTY_SKILL, 1;

	mes "[Tomas]";
 //@en "=] \"Here's your permit. You'll need to be invited by an existing member of a party to join it.\""
	mes "=] \"Aqui está a sua autorização. Para participar você precisará ser convidado por um membro de um Grupo(Party) já existente.\"";
	next;

	mes "[Tomas]";
 //@en "=þ \"Remember, parties cannot share experience if any members are more than 10 levels apart.\""
	mes "=þ \"Lembre-se, os participantes dos Grupos(Party) não podem partilhar experiência, se tiverem diferença superior a 10 níveis.\"";
	close;

_PrecoPartyCriar:
	mes "[Tomas]";
 //@en "\"A permit to create parties costs 50 GP.\""
	mes "\"A autorização para criar um Grupo(Party) custa 50 GP.\"";
	next;

	mes "["+ strcharinfo(0) +"]";
	menu
//@en "OK, I accept!"
		"OK, Eu aceito!", _GanhaPartyCriar,
//@en "No, thank you!"
		"Não, obrigado!", -;
	close;

_GanhaPartyCriar:
	if (zeny < 50) goto _SemGrana;
	set zeny, zeny - 50;
	setskill @PARTY_SKILL, 2;

	mes "[Tomas]";
 //@en "=] \"Here's your permit. You can create parties with the 'new' or 'create' commands on the Party tab in the client. Parties need to have unique names.\""
	mes "=] \"Aqui está a sua autorização. Você pode criar um Grupo com o comando '/new NomeExclusivoDoGrupo' ou '/create NomeExclusivoDoGrupo' se digita-los na aba de Grupo(ou Party) da caixa de dialogo. Os Grupos devem possuir nomes exclusivos, que não foram usados por nenhum outro Grupo(Party).\"";
	close;

_SemGrana:
	mes "[Tomas]";
 //@en "=[ \"Sorry, you don't have enough GP.\""
	mes "=[ \"Desculpe, você não tem grana suficiente.\"";
	close;
}

