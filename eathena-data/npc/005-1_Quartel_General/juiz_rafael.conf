///////////////////////////////////////////////////////////////////////////////////////////////////////
// NOME: Juiz Miguel
// ETMOLOGIA: Miguel é o nome do anjo da guerra!
// SEXO: Masculino
// SPRITE: Adefinir (Temporariamente 157)
// PROGRAMADOR: Indigovox<rui.gravata@hotmail.com>
// CRIAÇÃO: Quarta, 2009-01-17
// FUNÇÃO: Receber $$$ aposta de quem entrar no ringue. E devolver montante ao unico que permanecer.
// STATUS: Completo
// RESET: Ainda não há reset! (Talvés Necessite)
///////////////////////////////////////////////////////////////////////////////////////////////////////

005-1.gat,28,27,0	script	Juiz Miguel	157,{
	callfunc "getTime";
	set @MaxLutadores, 6;
	set @Retorno_Mapa$, "005-1.gat";
	set @Retorno_X, 31;
	set @Retorno_Y, 25;
	set @MaxPermanencia, 60*60*2; // 2 horas
//------------------------------------------------------------------------------------------------------------------
if($JuizRafael_Config==1) goto _configurado;
	set $JuizRafael_QuandoIniciado, 0;
	set $JuizRafael_Lutadores, 0;
	set $JuizRafael_ValorApostado, 0;
	set $JuizRafael_ValorAcumulado, 0;
	cleararray $JuizRafael_NomeDoLutador$,"",@MaxLutadores;
	set $JuizRafael_Config, 1;
_configurado:
//------------------------------------------------------------------------------------------------------------------
_SeJogadorEntrou:
	set @I, 0;
	set @SeDentro, 0;
	//mes "----- _SeJogadorEntrou: ------";
	//mes "----- $JuizRafael_Lutadores("+ $JuizRafael_Lutadores +") ------";
	//mes "----- $JuizRafael_ValorApostado("+ $JuizRafael_ValorApostado +") ------";
	//mes "----- $JuizRafael_ValorAcumulado("+ $JuizRafael_ValorAcumulado +") ------";
_loop_buscar_lutadores:
	set @I, @I+1;
	//mes "----- " + @I + "º: " + $JuizRafael_NomeDoLutador$[@I] + " ------";

	if($JuizRafael_NomeDoLutador$[@I] == strcharinfo(0)) goto _estadentro;
	if(@I >= @MaxLutadores) goto _estafora;
goto _loop_buscar_lutadores;

_estadentro:
//	mes "----- _estadentro (" + @I + "º): " + $JuizRafael_NomeDoLutador$[@I] + " ------";
	set @SeDentro, @I;
_estafora:

//------------------------------------------------------------------------------------------------------------------

//mes "----- _aprensentacao: JuizRafael_Apresentado(" + JuizRafael_Apresentado + ") ------";
if(JuizRafael_Apresentado==1) goto _SeEntrar;
_aprensentacao:
	mes "\"Olá! Me chamo Juiz Miguel!\"";
	next;
_reaprensentacao:
	mes "[Juiz Miguel]";
	mes "=) \"Eu sou o Juiz deste ringue.";
	mes "O pessoal cria comigo a suas apostas em multiplos de 1kGP. " +
	"Depois deve esperar dentro do ringue até que alguem aceite aposta. " + 
	"Outros desafinate podem entrar no meio da luta. " + 
	"Cada um vai perdendo durante a luta. " + 
	"E no final devolvo o dinheiro apostado por todos para o unico que ficar vivo dentro do ringue. " +
	"\"";
	next;

	mes "[Juiz Miguel]";
	mes ">=| \"Mas ja vou avisando que é crime desconectar dentro do ringue. " +
	"Quem desconectar dentro do ringue perderá todo o dinheiro que apostou " +
	"e ainda poderá ser punido severamente por um General Magico por tentar bloquear e roubar jogadores! " +
	"\"";

	set JuizRafael_Apresentado, 1;
next;

//------------------------------------------------------------------------------------------------------------------
_SeEntrar:
	//mes "----- _SeEntrar: $JuizRafael_Lutadores(" + $JuizRafael_Lutadores + "), @SeDentro(" + @SeDentro + ") ------";
	if($JuizRafael_Lutadores==0) goto _Questao_0;
	
	if($JuizRafael_Lutadores>=1 && @time > ($JuizRafael_QuandoIniciado+@MaxPermanencia)) goto _SeDeslogouDentro;
	if($JuizRafael_Lutadores==1 && @SeDentro==0) goto _Questao_1a;
	if($JuizRafael_Lutadores==1 && @SeDentro>=1) goto _Questao_1b;
	if($JuizRafael_Lutadores>=2 && @SeDentro==0) goto _Questao_1a; 
	if($JuizRafael_Lutadores>=2 && @SeDentro>=1) goto _Questao_2b;
close;
//------------------------------------------------------------------------------------------------------------------
_SeDeslogouDentro:
	if(countitem(725)<=0) goto _AvisoAoGM;
	mes "[Juiz Miguel]";
	mes "\"Existem "+$JuizRafael_Lutadores+" que delogou aqui dentro. " + 
	"Isso é uma tática maliciosa para roubar outros jogadores!";
	if(Sex==0) mes "O que o Senhor General quer fazer?\"";
	if(Sex==1) mes "O que o Senhora General quer fazer?\"";
	next;

	mes "["+strcharinfo(0)+"]";
	menu
		">=[ \"Quero saber o nome de quem delogou ai dentro!\"", _EntregarNome,
		">=[ \"Vou guardar o dinheiro das apostas!\"", _ResetRing,
		"=/ \"Deixa quieto!\"", -;
close;
//------------------------------------------------------------------------------------------------------------------
_AvisoAoGM:
	mes "[Juiz Miguel]";
	mes "\"Existem "+$JuizRafael_Lutadores+" que delogou aqui dentro. " + 
	"Isso é uma tática maliciosa para roubar outros jogadores. " +
	"Por favor "+strcharinfo(0)+", chame a algum GM para vir falar comigo sobre isso!\"";
close;
//------------------------------------------------------------------------------------------------------------------
_EntregarNome:
	set @I, 0;
	mes "[Juiz Miguel]";
	mes "\"O nome dos Lutadores são\:";
_loop_buscar_desconectados:
	set @I, @I+1;
	if($JuizRafael_NomeDoLutador$[@I] != "") mes " * " + $JuizRafael_NomeDoLutador$[@I];
	if(@I >= @MaxLutadores) goto _FimListaDesconectados;
goto _loop_buscar_desconectados;
_FimListaDesconectados:
	mes "\"";
	next;
goto _SeDeslogouDentro;
//------------------------------------------------------------------------------------------------------------------
_ResetRing:
	set Zeny, Zeny + $JuizRafael_ValorAcumulado;
	npctalk strcharinfo(0) + " regatou o valor de "+ $JuizRafael_ValorAcumulado +"GP!";
	set $JuizRafael_QuandoIniciado, 0;
	set $JuizRafael_Lutadores, 0;
	set $JuizRafael_ValorApostado, 0;
	set $JuizRafael_ValorAcumulado, 0;
	cleararray $JuizRafael_NomeDoLutador$,"",@MaxLutadores;
	set $JuizRafael_Config, 1;
goto _Questao_0;
//------------------------------------------------------------------------------------------------------------------
_Questao_0:
	mes "[Juiz Miguel]";
	mes "\"O que quer fazer, " + strcharinfo(0) + "?\"";
	next;

	mes "["+strcharinfo(0)+"]";
	menu
		"=S \"O que você faz mesmo?\"", _reaprensentacao,
		"=] \"Quero criar um desafio!\"", _CriarAposta,
		"=/ \"Deixa quieto!\"", -;

	mes "[Juiz Miguel]";
	mes "\"Ok! Até mais!\"";
close;
//------------------------------------------------------------------------------------------------------------------
_Questao_1a:
	mes "[Juiz Miguel]";
	mes "\"Ja existe " +  $JuizRafael_Lutadores + " lutadores no ringe. " +
	"O Valor do premio para quem ficar por ultimo é de " +  $JuizRafael_ValorAcumulado + "GP. " +
	"Para entrar você tem que aposta mais os " +  $JuizRafael_ValorApostado + "GP de desafio.\"";
	next;

	mes "[Juiz Miguel]";
	mes "\"Vai aceitar o desafio?\"";
	next;

	mes "["+strcharinfo(0)+"]";
	menu
		"=) \"Ok! Eu pago o desafio!\"", _SeCobre,
		"=X \"Deixa quieto!\"", -;

	mes "[Juiz Miguel]";
	mes "\"Ok! Até mais!\"";
close;
//------------------------------------------------------------------------------------------------------------------
_Questao_1b:
	mes "[Juiz Miguel]";
	mes "=( \"Ninguem está aceitando o seu desafio. " + 
	"O premio está acumulado em " +  $JuizRafael_ValorAcumulado + "GP!";
	mes "Vai esperar mais um pouco por algum desafiante?\"";

	next;

	mes "["+strcharinfo(0)+"]";
	menu
		"=[ \"Bah! Vou pegar o acumulado e sair!!\"", _DesistirDeEsperar,
		"XD \"Sim! Mas vou esperar mais um pouco!\"", -;

close;
//------------------------------------------------------------------------------------------------------------------
_Questao_2b:
	mes "["+strcharinfo(0)+"]";
	menu
		"=D \"Buaaaa! Desisto da Luta!\"", _DesistoDaLuta,
		"=] \"Vou lutar mais um pouco!\"", -;
close;
//------------------------------------------------------------------------------------------------------------------
_CriarAposta:
	mes "[Juiz Miguel]";
	mes "\"Para criar um desafio você precisa fazer uma aposta.";
	mes "Quanto kGPs você deseja apostar?\"";
	next;

	mes "["+strcharinfo(0)+"]";
	input @QualAposta;
	if(@QualAposta <= 0) goto _ReexplicaApostaParaIniciar;
	if(@QualAposta * 1000 > Zeny) goto _SemGranaParaIniciar;

	set Zeny, Zeny - (@QualAposta * 1000);
	set $JuizRafael_Lutadores, 1;
	set $JuizRafael_QuandoIniciado, @time;
	set $JuizRafael_ValorApostado, (@QualAposta * 1000);
	set $JuizRafael_ValorAcumulado, (@QualAposta * 1000);
	set $JuizRafael_NomeDoLutador$[$JuizRafael_Lutadores], strcharinfo(0);

	npctalk strcharinfo(0) + " criou o desafio de "+ (@QualAposta * 1000) +"GP!";
	warp "005-1.gat",31,28;
	savepoint "005-1.gat",30,74;

	mes "[Juiz Miguel]";
	mes "=) \"Ok! Agora espere no ringue alguem aceitar sua aposta!\"";
close;
//------------------------------------------------------------------------------------------------------------------
_SeCobre:
	//mes "----- _SeCobre: $JuizRafael_ValorApostado(" + $JuizRafael_ValorApostado + ") ------";

	if($JuizRafael_ValorApostado <= 0) goto _ReexplicaApostaParaCobrir;
	if($JuizRafael_ValorApostado > Zeny) goto _SemGranaParaCobrir;

	set $JuizRafael_Lutadores, $JuizRafael_Lutadores + 1;
	set Zeny, Zeny - $JuizRafael_ValorApostado;
	set $JuizRafael_ValorAcumulado, $JuizRafael_ValorAcumulado + $JuizRafael_ValorApostado;
	set $JuizRafael_NomeDoLutador$[$JuizRafael_Lutadores],strcharinfo(0);
	warp "005-1.gat",31,28;
	savepoint "005-1.gat",30,74;
	pvpon "005-1.gat";
	npctalk strcharinfo(0)+" aceitou a aposta e entrou no ringue!";
close;
//------------------------------------------------------------------------------------------------------------------
_ReexplicaApostaParaIniciar:
	mes "[Juiz Miguel]";
	mes "=) \"Para entrar é obrigatório fazer uma aposta de desafio a outros cidadãos!\"";
	next;
goto _Questao_0;
//------------------------------------------------------------------------------------------------------------------
_ReexplicaApostaParaCobrir:
	mes "[Juiz Miguel]";
	mes "=) \"Para entrar é obrigatório cobrir a aposta de desafio de " + $JuizRafael_ValorApostado + "GP!\"";
	next;
goto _Questao_1a;
//------------------------------------------------------------------------------------------------------------------
_SemGranaParaIniciar:
	mes "[Juiz Miguel]";
	mes "=/ \"Infelizmente, você não tem grana suficiente para criar este desafio de " + (@QualAposta * 1000) + "GP!\"";
	next;
goto _Questao_0;
//------------------------------------------------------------------------------------------------------------------
_SemGranaParaCobrir:
	mes "[Juiz Miguel]";
	mes "=/ \"Infelizmente, você não tem grana suficiente para cobrir a aposta de " + $JuizRafael_ValorApostado + "GP!\"";
close;

//------------------------------------------------------------------------------------------------------------------
_DesistirDeEsperar:
	npctalk strcharinfo(0)+" pegou o montante de "+ $JuizRafael_ValorAcumulado +"GP e saiu do ringue!";
	cleararray $JuizRafael_NomeDoLutador$,"",@MaxLutadores;
	set $JuizRafael_Lutadores, 0;
	set $JuizRafael_ValorApostado, 0;
	set Zeny, Zeny + $JuizRafael_ValorAcumulado;
	set $JuizRafael_ValorAcumulado, 0;
	pvpoff @Retorno_Mapa$;
	warp @Retorno_Mapa$,@Retorno_X,@Retorno_Y;
close;
//------------------------------------------------------------------------------------------------------------------
_DesistoDaLuta:
	set @I, 0;
_loop_buscar_lutadores2:
	set @I, @I+1;
	if($JuizRafael_NomeDoLutador$[@I] == strcharinfo(0)) goto _DesistenteSelecionado;
	if(@I > $JuizRafael_Lutadores) close;
goto _loop_buscar_lutadores2;

_DesistenteSelecionado:
	set $JuizRafael_NomeDoLutador$[@I], $JuizRafael_NomeDoLutador$[@I+1];
	set @I,@I+1;
if(@I <= $JuizRafael_Lutadores) goto _DesistenteSelecionado;

	npctalk strcharinfo(0)+" desistiu do desafio e perdeu "+ $JuizRafael_ValorApostado +"GP!";
	set $JuizRafael_Lutadores, $JuizRafael_Lutadores-1;
	if($JuizRafael_Lutadores<=1) pvpoff "005-1.gat";
	warp @Retorno_Mapa$,@Retorno_X,@Retorno_Y;
close;
//------------------------------------------------------------------------------------------------------------------
}
