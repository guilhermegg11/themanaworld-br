//
// @script Cornélia
// @autor Diogo_RBG - http://diogorbg.blogspot.com
// @desc Uma bondosa e atenciosa dona de casa que irá lhe ajudar de bom grado.
// #quest <desc>, <getItens>, <delItens>, <extra>
// @quest 'Tortas de Maçã', '8 tortaMaca', '1 cestaMacas', 'Esperar 5:00'
// @quest 'Rolo de Macarrão', '1 roloMacarrao, 180 exp', '', 'Matar 15 Vermes'

//007-4.gat,18,27,0	script	debug	140,{
//	menu
//		"0 - zerar", -,
//		"1 - procurar", -,
//		"2 - esperar maçãs", -,
//		"3 - levar cesta", -,
//		"4 - esperar torta", -,
//		"5 - levar torta", -,
//		"6 - fim", -;
//	set QUEST_praia, @menu-1;
//	if (@menu==1) set QUEST_contamobs, 0;
//	callfunc "getTime";
//	set QUEST_praiaTime, @time;
//	mes "resetado para "+(@menu-1);
//	close;
//}


// Proposta de modificações em variáveis:
// QUEST_TortaMaca == QUEST_praia - Economia de 1 variável nos NPCs joana e cornelia

007-4.gat,21,27,0	script	Cornélia	140,{
	if (Sex == 0) set @meu_a$, "minha";
	if (Sex == 1) set @meu_a$, "meu";
	if(QUEST_praia==1) goto _encontre;
	if(QUEST_praia==2) goto _colhendo;
	if(QUEST_praia==3) goto _entregarCesta;
	callfunc "getTime";
	if(QUEST_praia==4 && @time>=QUEST_praiaTime+5*60) goto _tortaPronta;
	if(QUEST_praia==4) goto _tortaAssando;
	if(QUEST_praia==5 || QUEST_praia==6) goto _obrigada;
	if(QUEST_praia==7) goto _contagemVermes;
	if(QUEST_praia>=8 && QUEST_praia<=12) goto _esperapresente;
	if(QUEST_praia>=13) goto _ganhoupresente;

	mes "[Cornélia]";
	mes "\"Olá "+@meu_a$+" jovem. Você poderia me ajudar?\"";
	next;
	menu
		"Mas é claro que sim.", _sim,
		"Outra hora.", -;
	close;

_sim:
	mes "[Cornélia]";
	mes "\"Estou muito preocupada com minha prima Joana. Ela saiu para colher umas maçãs e ainda não voltou.\"";
	next;
	menu
		"Porque ela foi colher maçãs?", _pqMacas,
		"Ela deve voltar logo.", -;
	close;

_pqMacas:
	mes "[Cornélia]";
	mes "\"É que eu precisava de algumas maças para fazer uma deliciosa torta de maças e eu não tinha nenhuma!\"";
	next;
	menu
		"E onde você acha que ela foi colher maçãs?", _onde,
		"Ela deve voltar logo.", -;
	close;

_onde:
	mes "[Cornélia]";
	mes "\"Ela costumava colher maçãs próxima a casa de Jack o lenhador, pois era lá que costumávamos catar maçãs quando éramos crianças.";
	mes "Mas acredito que ela deva estar colhendo maçãs em seu próprio quintal.\"";
	next;
	menu
		"E onde fica a casa dela?", -;
	mes "[Cornélia]";
	mes "\"Ela mora em uma casa à beira mar na praia ao norte.";
	mes "Ela possui um lindo quital gramado e coberto de flores... não será difícil encontrá-la!\"";
	next;
	menu
		"Não se preocupe. Irei encontrá-la e dizer que você está preocupada com ela.", -;
	mes "[Cornélia]";
	mes "\"Muito obrigada!\"";
	set QUEST_praia, 1;
	close;

_encontre:
	mes "[Cornélia]";
	mes "\"Por favor encontre minha prima Joana. Ela deve estar colhendo maçãs no quintal de sua casa.\"";
	next;
	menu
		"Não se preocupe. Irei encontrá-la.", -;
	mes "[Cornélia]";
	mes "\"Por favor não demore!\"";
	close;

//=====================================================================

_colhendo:
	mes "[Cornélia]";
	mes "\"Encontrou minha prima?\"";
	next;
	menu
		//"Sim... ela está colhendo maçãs próximo a casa de Jack o lenhador.", -;
		"Sim... ela está colhendo maçãs no quintal de sua casa.", -;
	mes "[Cornélia]";
	mes "\"Estou aliviada... muito obrigada!\"";
	goto _questVermes;

//=====================================================================

_entregarCesta:
	mes "[Cornélia]";
	mes "\"Encontrou minha prima?\"";
	next;
	menu
		"Sim... e aqui estão as maçãs que ela colheu para você.", -;
	mes "[Cornélia]";
	mes "\"Estou aliviada... muito obrigada!\"";
	next;
	mes "[Cornélia]";
	mes "\"Gostaria de provar a minha deleciosa toda de maçã?\"";
	next;
	menu
		"Sim. Deve ser deliciosa mesmo, pois todos falam dela.", -;
	if (countitem(3027)<1) goto _semCesta;
	mes "[Cornélia]";
	mes "\"Irei prepará-la agora mesmo!";
	mes "Só vai levar 5 minutos.\"";
	delitem 3027, 1;
	callfunc "getTime";
	set QUEST_praiaTime, @time;
	set QUEST_praia, 4;
	close;

_semCesta:
	mes "[Cornélia]";
	mes "\"Mas primeiro eu preciso que você me traga a cesta de maçãs que minha prima mandou para mim.";
	mes "Você disse que estava com a cesta, mas ela não está aí.\"";
	close;

_tortaAssando:
	mes "[Cornélia]";
	mes "\"A torta ainda está assando... por favor espere alguns minutos.\"";
	close;

_tortaPronta:
	getinventorylist;
	if( (@inventorylist_count + (countitem(3028)==0) ) > 100 ) goto _cheio;
	set QUEST_praiaTime, 0;
	mes "[Cornélia]";
	mes "\"A torta de maçã está pronta!";
	mes "Fique com estas 7 fatias. A outra vou guardar para minha prima.\"";
	getitem 3028, 7;
	set QUEST_praia, 5;
	next;
	menu
		"Sua prima avisou que nao viria hoje.", -;
	mes "[Cornélia]";
	mes "\"Neste Caso vou precisar novamente se sua ajuda. Leve esta outra fatia para ela, por favor.\"";
	getitem 3028, 1;
	close;

_cheio:
	mes "[Cornélia]";
	mes "\"Pretendia lhe dar algo, mas parece que seu inventário está cheio.";
	mes "Volte mais tarde.\"";
	close;

_obrigada:
	mes "[Cornélia]";
	mes "\"Obrigada por me ajudar com a torta. Eu agora só espero que o Pablo goste... Pois este é o meu presente de aniversário de casamento para ele...\"";
	next;
	mes "[Cornélia]";
	mes "\"Espero que este ano ele capriche no meu presente, pois ano passado fiquei com a impressão de que ele só se lembrou da data em cima da hora...\"";
	goto _questVermes;

_esperapresente:
	mes "[Cornélia]";
	mes "\"Não vejo a hora de ver o presente que o Pablo comprou para mim!\"";
	close;

_ganhoupresente:
	mes "[Cornélia]";
	mes "\"Nossa... o meu marido Pablo é tão romântico e dedicado... o presente dele foi uma rosa branca, um simbolo de amor eterno. \"";
	close;

_close:
	close;

//=====================================================================

_questVermes:
	if (QUEST_contamobs==0) goto _roloMacarrao;
	if (QUEST_contamobs>=2) goto _contagemVermes;
	close;

_roloMacarrao:
	next;
	mes "[Cornélia]";
	mes "\"Meu marido Pablo me disse que você é guerreiro! É verdade?\"";
	next;
	menu
		"Sim. Eu luto contra todo tipo de monstro.", -,
		"Não quero falar sobre isso.", _close;
	mes "[Cornélia]";
	mes "\"Nossa! Você realmente é um guerreiro destemido!";
	mes "Eu gostaria muito de te ajudar em sua jornada, mas não tenho muito a oferecer.\"";
	next;
	menu
		"Eu não recusaria ajuda dada de bom grado.", -;
	getinventorylist;
	if( (@inventorylist_count + (countitem(3029)==0) ) > 100 ) goto _cheio;

	mes "[Cornélia]";
	mes "\"Neste caso fique com este Rolo de Macarrão. Não é exatamente uma arma, mas servirá para matar alguns Vermes.\"";
	getitem 3029, 1;
	set QUEST_praia, 7;
	next;
	menu
		"Matar Vermes?", -;
	mes "[Cornélia]";
	mes "\"Sim. Isto servirá para ajudar em sua evolução.";
	mes "Mate 15 Vermes com o Rolo de Macarrão que te dei e verá como você se tornará mais forte.";
	close;

_contagemVermes:
	next;
	mes "[Cornélia]";
	mes "\"E então! Já matou os vermes que te falei?\"";
	next;
	menu
		"Já matei "+(QUEST_contamobs)+" Vermes.", -;
	if (QUEST_contamobs>=15) goto _completou;
	mes "[Cornélia]";
	mes "\"Essa quantia é insuficiente. Você precisa matar pelo menos 15 Vermes se quiser se tornar mais forte.\"";
	close;

_completou:
	mes "[Cornélia]";
	mes "\"Parabéns... você matou uma boa quantia de Vermes e deve ter ganhado uma boa experiência por isso.";
	mes "";
	mes "* Você ganhou 180 pontos de experiência.";
	set BaseExp, BaseExp+180;
	set QUEST_contamobs, 0;
	set QUEST_praia, 8;
	close;

//OnTimer5000:
//	set @tmp, rand(3);
//	if(@tmp==0) npctalk "##6La! La! ##3Ri! ##6La!";
//	if(@tmp==1) npctalk "##6La! La! La! ##0- ##6La! ##3Ri! Ri! ##1Ra!";
//	if(@tmp==2) npctalk "##3Ri! Ri! Ri! ##1Ra!";
//	setnpctimer 0;
//OnInit:
//	initnpctimer;
}
