//######################################################################################################
//##   QUEST DO CAVALEIRO AMALDIÇOADO PARA O HALLOWEEN (NPC Bruxa)
//######################################################################################################
//## QUESTARTS:
//##    * Selena<santos.alinnee@gmail.com>
//##    * Lunovox<rui.gravata@hotmail.com>
//## CRIAÇÃO: 22/10/2011
//## DESCRIÇÃO:
//##    * O jogador tem que insistir até convencer a bruxa a trocar alguns itens pelo Poção de
//##      Despetrificação que será entregue ao Cavaleiro Amaldiçoado.
//## IDs DE ITENS DEPENDENTES:
//##    * Poção Despetrificação
//##    * Asas de Morcego Vampiro
//##    * Asas de Morcego Negro
//##    * Petalas de Flor Carnívora
//##    * Garrafas de Água
//##    * Par de Olhos
//##    * Monte de Palhas de Espantalho
//##    * Alma de Jack Maligno.
//##    * Lanterna Jack
//##    * Espada Petrificada (Necessária na Quest, mas não usada neste NPC)
//## CÓDIGO DEPENDENTE:
//##    * Falta por is ID de itens na configuração deste script.
//##    * A quest inteira possui 2 NPC:
//##         + Cavaleiro Amaldiçoado
//##         + Bruxa Amaldiçoadora
//######################################################################################################

//013-2.gat,43,28,0	script	debug	377,{
//	callfunc "debug";
//	mes "HLW2011_cavAmaldicoado: " + HLW2011_cavAmaldicoado;
//	menu
//		"Set HLW2011_cavAmaldicoado 0", -,
//		"Set HLW2011_cavAmaldicoado 1", -,
//		"Set HLW2011_cavAmaldicoado 2", -,
//		"Set HLW2011_cavAmaldicoado 3", -,
//		"Set HLW2011_cavAmaldicoado 4", -,
//		"Set HLW2011_cavAmaldicoado 5", -,
//		"Set HLW2011_cavAmaldicoado 6", -,
//		"Set HLW2011_cavAmaldicoado 7", -,
//		"sair", -;
//	if(@menu<0 || @menu>8) close;
//	set HLW2011_cavAmaldicoado, @menu-1;
//	close;
//}

013-2.gat,46,28,0	script	Morgana	377,{

	//Seta detalhes de fala
	setarray @Guerreirx$, "Guerreira", "Guerreiro";
	setarray @perdidx$, "perdida", "perdido";

	//Seta IDs dos Itens
	set @asasMorcegoVampiro, 3340;
	set @asasMorcegoNegro, 3339;
	set @petalaFlorCarnivora, 3338;
	set @garrafaAgua, 541;
	set @ParOlhos, 3354;
	set @montePalha, 3351;
	set @almaJackMaligno, 3337;
	set @lanternaJackO, 1991;
	set @pocaoDespetrificacao, 3355;

	//Redirecionamento de Cenas
	if(HLW2011_cavAmaldicoado==1) goto _procuraMorgana;
	if(HLW2011_cavAmaldicoado==2) goto _insistimento1;
	if(HLW2011_cavAmaldicoado==3) goto _insistimento2;
	if(HLW2011_cavAmaldicoado==4) goto _voltaCheckIngredientes;
	if(HLW2011_cavAmaldicoado>=5) goto _conclusao;
	goto _inicio;

//-------------------------------------------------------------------------------------------------------
_itemCheck:
	set @check, 0;
	if(countitem(@asasMorcegoVampiro)<30) return;
	if(countitem(@asasMorcegoNegro)<20) return;
	if(countitem(@petalaFlorCarnivora)<7) return;
	if(countitem(@garrafaAgua)<6) return;
	if(countitem(@ParOlhos)<5) return;
	if(countitem(@montePalha)<5) return;
	if(countitem(@almaJackMaligno)<3) return;
	if(countitem(@lanternaJackO)<2) return;
	set @check, 1;
	return;
//-------------------------------------------------------------------------------------------------------
_inicio:
	mes "[Morgana]";
	mes "(^_^) \"Quer uma maçã?\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "(T.T) \"Eu agradeço, mas estou sem fome.\"";
close;
//-------------------------------------------------------------------------------------------------------
_procuraMorgana:
	mes "[Mulher]";
	mes "Você bateu na porta e ninguem atendeu. Mesmo assim, você entra na casa da bruxa. A casa não parece ser de uma bruxa. Os moveis são novinhos, todos bem cuidados, tudo combinando na decoração, parecendo mais um comodo da casa de realezas. Uma mulher está a ler distraidamente e é muito formosa. Quando se dá conta de sua presença lhe abre um sorriso tão bondoso e diz:";
	mes "(^_^) \"Olá "+@Guerreirx$[sex]+", está "+@perdidx$[sex]+"? Talvez eu possa lhe servir alguma coisa?\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	menu
		"Oh, sim, eu gostaria.",_aceitaBebida,
		"Não obrigado, estou bem.", _recusaBebida;
goto _recusaBebida;
//-------------------------------------------------------------------------------------------------------
_aceitaBebida:
	mes "[Mulher]";
	mes "A moça lhe oferece uma caneca com um liquido negro esfumaçante e diz:";
	mes "(^_^) \"É um chá de ervas. Muito gostoso.\"";
	next;
	mes "[Mulher]";
	mes "Ela bebe do seu copo para encorajar.";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "Depois de beber o 'chá', alguma coisa parece mudar, a casa está velha, moveis quebrados e a bela mulher se tornou assustadora. Mas não há mais tempo para reparar pois você está morto.";
	percentheal -100, 0;
close;
//-------------------------------------------------------------------------------------------------------
_recusaBebida:
	mes "["+ strcharinfo(0) +"]";
	mes "(T.T) \"Procuro uma mulher chamada Morgana, é você?\"";
	next;
	mes "[Mulher]";
	mes "A mulher o estuda por um momento. Abre um sorriso meio sinistro e diz:";
	mes "(¬_¬) \"Sim, me chamo Morgana. O que precisa de mim?\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "Você sente sua garganta secar, enquando se pergunta qual será a reação dela quando você falar.";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "Você se arma de coragem e diz:";
	mes "(-.-) \"Vim em nome de um amigo. Um cavaleiro amaldiçoado que vive na floresta. Conhece?\"";
	next;
	mes "[MORGANA]";
	mes "Ela não demonstra reação nenhuma e lhe responde:";
	mes "(T_T) \"Infelizmente não. Pode ir embora agora.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	menu
		"Tudo bem, obrigado!",-,
		"Tem certeza? Um certo homem que vira pedra de dia. Não se lembra dele mesmo?",_insistimento1;
close;
//-------------------------------------------------------------------------------------------------------
_insistimento1:
	set HLW2011_cavAmaldicoado,2;
	mes "[MORGANA]";
	mes "A feição da bela mulher começa a mudar, ela parece estar ganhando anos. A mulher parece uma velha agora.";
	mes "(T_T) \"Está bem está bem, o que você quer? Quer ser amaldiçoado também?\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "(x.x) \"É que já faz tantos anos que vocês brigaram. Você poderia tirar o feitiço dele.\"";
	next;
	mes "[MORGANA]";
	mes "Ela faz uma cara triste e  diz:";
	mes "(-_-) \"Tem razão, faz tantos anos, eu nem me lembro mais o porque.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "(*-*) \"Então, irá tirar o feitiço?\"";
	next;
	mes "[MORGANA]";
	mes "A bruxa se aproxima, seus olhos ficam vermelhos, ela mostra dentes afiados, e grita:";
	mes "(¬.¬) \"NÃOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO!\"";
	next;
	mes "[MORGANA]";
	mes "Ela tem um bafo tão horrível que causa ardor nas suas narinas. Depois senta-se novamente e se transforma na mulher bela de novo.";
	next;
	mes "["+ strcharinfo(0) +"]";
	menu
		"Então, vou embora.",-,
		"Mas Dona Morgana, pense bem. Ele está a tanto tempo lá. Seja o que for que a fez transformá-lo, ele já aprendeu a lição.",_insistimento2;
close;
//-------------------------------------------------------------------------------------------------------
_insistimento2:
	set HLW2011_cavAmaldicoado,3;
	mes "[MORGANA]";
	mes "Por acaso eu pareço uma fada? ";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "Você a estuda. Se aproxima. Ela está bonita agora, mas a minutos atrás não estava, e diz:";
	mes "(¬.¬) \"Na verdade... Não!\"";
	next;
	mes "[MORGANA]";
	mes "Ela vem um pouco mais perto, te encarando, e responde:";
	mes "(T_T) \"Sabe porque? É porque sou uma BRUXA, ahahahahahahahha.\"";
	next;
	mes "[MORGANA]";
	mes "Ela fica em pé e continua:";
	mes "(T_T) \"Bruxas não ajudam, Bruxas não fazem o bem, e Bruxas não se importam com nada. Agora suma, antes que eu lhe lance uma maldição pior.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "Você pensa por uns instantes e retruca:";
	mes "(x.X) \"Talvez possamos fazer um acordo. Posso consertar sua casa ou comprar algo que precise. Fazer entregas. Ai você desfaz o feitiço.\"";
	next;
	mes "[MORGANA]";
	mes "A bruxa caminha em sua direção, pronta a expulsá-lo usando magia, mas então pensa por um instante, sua sobrancelha levanta um pouco e ela lhe lança um sorriso encantador dizendo:";
	mes "(^-^) \"Posso lhe dar uma das minhas poções, para desfazer o encanto. Mas, deve me trazer algumas coisas. Já sou velha.\"";
	next;
	mes "[MORGANA]";
	mes "Ela desfaz a aparência de moça e fica velha de novo, e continua dizendo:";
	mes "(^-^) \"já não posso mais caminhar tanto. Me traga essas coisas.  Ela lhe entrega uma lista:\"";
	next;
	mes "[LISTA DE INGREDIENTES]";
	mes "* 30 Asas de morcego vampiro.";
	mes "* 20 Asas de morcego Negro.";
	mes "* 07 Pétalas de Flor Carnívora.";
	mes "* 06 Garrafas d'água.";
	mes "* 05 Pares de Olhos.";
	mes "* 05 Montes de Palha.";
	mes "* 03 Almas de Jack Maligno.";
	mes "* 02 Lanternas do Jack O.";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "(x.X) \"Tudo isso?\"";
	next;
	mes "[MORGANA]";
	mes "(^-^) \"Sim! Tudo isso.\"";
	next;
	mes "[MORGANA]";
	mes "(T-T) \"E ande logo, se não vou transformá-lo em sapo.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "(x.X) \"Sim! Sim! Já estou indo.\"";
	set HLW2011_cavAmaldicoado,4;
close;
//-------------------------------------------------------------------------------------------------------
_voltaCheckIngredientes:
	callsub _itemCheck;
	if(@check==1) goto _voltaComIngredientes;

	mes "[MORGANA]";
	mes "(^-^) \"Conseguiu o que eu pedi?\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "Você olha desesperançado para lista.";
	next;
	mes "[LISTA DE INGREDIENTES]";
	mes "* 30 Asas de morcego vampiro.";
	mes "* 20 Asas de morcego Negro.";
	mes "* 07 Pétalas de Flor Carnívora.";
	mes "* 06 Garrafas d'água.";
	mes "* 05 Pares de Olhos.";
	mes "* 05 Montes de Palha.";
	mes "* 03 Almas de Jack Maligno.";
	mes "* 02 Lanternas do Jack O.";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "E responde sem ânimo:";
	mes "(-_-) \"Ainda não.\"";
	next;
	mes "[MORGANA]";
	mes "(T-T) \"E ande logo, se não vou transformá-lo em sapo.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "(x.X) \"Sim! Sim! Já estou indo.\"";
close;
//-------------------------------------------------------------------------------------------------------
_voltaComIngredientes:
	mes "["+ strcharinfo(0) +"]";
	mes "(^-^) \"Trouxe tudo o que você pediu.\"";
	next;
	mes "[MORGANA]";
	mes "(T-T) \"Já não era sem tempo.\"";
	next;
	mes "[MORGANA]";
	mes "Ela pega o pacote e confere. Tira um olho e come e depois mais um, no terceiro ela volta sua atenção para você e diz:";
	mes "(^-^) \"Saboroso.\"";
	next;
	mes "[MORGANA]";
	mes "Ela mexe no bolso em seu manto, pega um frasco pequeno , e diz:";
	mes "(T.T) \"É meu ultimo frasco. Se perder ou quebrar não terá mais.\"";
	next;
	callsub _itemCheck;
	if(@check==0) goto _tentouEnganar;
	delitem @asasMorcegoVampiro, 30;
	delitem @asasMorcegoNegro, 20;
	delitem @petalaFlorCarnivora, 7;
	delitem @garrafaAgua, 6;
	delitem @ParOlhos, 5;
	delitem @montePalha, 5;
	delitem @almaJackMaligno, 3;
	delitem @lanternaJackO, 2;
	getitem @pocaoDespetrificacao, 1;
	set HLW2011_cavAmaldicoado, 5;

	mes "[MORGANA]";
	mes "Ela lhe dá o frasco, e diz:";
	mes "(T.T) \"Se eu voltar a ver você ou aquele cavaleiro de meia tigela, eu os transformarei em minhocas, entendeu?\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "(O.o) \"Si...sim senhora.\"";
close;
//-------------------------------------------------------------------------------------------------------
_tentouEnganar:
	mes "[MORGANA]";
	mes "(T-T) \"Insolente! Tentando enganar uma BRUXA!";
	mes "Isso irá lhe ensinar boas maneiras... ahahahahahahahha.\"";
	percentheal -100, 0;
close;
//-------------------------------------------------------------------------------------------------------
_conclusao:
	mes "[MORGANA]";
	mes "(T-T) \"Você aqui denovo?\"";
	next;
	mes "[MORGANA]";
	mes "(T-T) \"Vou contar até 3. Se vc não sumir irei te transformar numa vassoura de vaso sanitário.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "(O.o)";
	next;
	mes "[MORGANA]";
	mes "(T-T) \"1...\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "(O.o) \"Si...sim senhora. Já tô indo!\"";
close;
//-------------------------------------------------------------------------------------------------------

}
