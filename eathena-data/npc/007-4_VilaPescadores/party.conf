
007-4.gat,63,26,0	script	Tathin	107,{
	set @SkillGrupo, 3; // ID da skill relacionada aos grupos(party)
	set @PrecoParaParticipar, 500; //Original → 10GP
	set @PrecoParaCriar, 5000; //Original → 50GP

	mes "[Tathin]";
	mes "\"Olá! Sou o gerente de Grupo(Party).\"";
	next;
	mes "[Tathin]";
	mes "\"O que posso fazer por você?\"";
	next;

	if(BaseLevel>=10 && getskilllv(@SkillGrupo)==0) goto _PedePartyParticipar; // ← Se tiver 1 é pq ja pode participar de uma party!
	if(BaseLevel>=15 && getskilllv(@SkillGrupo)==1) goto _PedePartyCriar; // ← Se tiver 2 é pq ja pode criar uma party!
goto _Dispensa;

_PedePartyParticipar:
	mes "["+ strcharinfo(0) +"]";
	menu
	"Eu gostaria de obter uma licença de Grupo(Party).", _PrecoPartyParticipar,
	"Nada, eu acho.", -;
close;

_PrecoPartyParticipar:
	mes "";
	mes "[Tathin]";
	mes "\"A autorização para aderir a um Grupo(Party) custa "+@PrecoParaParticipar+"GP.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	menu
	"OK, Eu aceito!", _GanhaPartyParticipar,
	"Não, obrigado!", -;
close;

_GanhaPartyParticipar:
	if (zeny < @PrecoParaParticipar) goto _SemGrana;
	set zeny, zeny - @PrecoParaParticipar;
	setskill @SkillGrupo, 1;
	mes "";
	mes "[Tathin]";
	mes "=] \"Aqui está a sua autorização. Para participar você precisará ser convidado por um membro de um Grupo(Party) já existente.\"";
	next;
	mes "[Tathin]";
	mes "=þ \"Lembre-se, os participantes dos Grupos(Party) não podem partilhar experiência, se tiverem diferença superior a 10 níveis.\"";
close;

_PedePartyCriar:
	mes "["+ strcharinfo(0) +"]";
	menu
	"Eu gostaria de ter autorização para criar um Grupo(Party).", _PrecoPartyCriar,
	"Nada, eu acho.", -;
close;

_PrecoPartyCriar:
	mes "";
	mes "[Tathin]";
	mes "\"A autorização para criar um Grupo(Party) custa "+@PrecoParaCriar+"GP.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	menu
	"OK, Eu aceito!", _GanhaPartyCriar,
	"Não, obrigado!", -;
close;

_GanhaPartyCriar:
	if (zeny < @PrecoParaCriar) goto _SemGrana;
	set zeny, zeny - @PrecoParaCriar;
	setskill @SkillGrupo, 2;
	mes "";
	mes "[Tathin]";
	mes "=] \"Aqui está a sua autorização. Você pode criar um Grupo com o comando '/new NomeExclusivoDoGrupo' ou '/create NomeExclusivoDoGrupo' se digita-los na aba de Grupo(ou Party) da caixa de dialogo. Os Grupos devem possuir nomes exclusivos, que não foram usados por nenhum outro Grupo(Party).\"";
close;

_SemGrana:
	mes "";
	mes "[Tathin]";
	mes "=[ \"Desculpe, você não tem grana suficiente.\"";
close;

_Dispensa:
	mes "["+ strcharinfo(0) +"]";
	mes "=( \"Nada, eu acho.\"";
	next;
	mes "[Tathin]";
	mes "=) \"Ok! Se desejar ajuda digite o comando:\"";
	mes " → /help";
close;

}

