// Traduzido e adaptado por alastrim

007.gat,103,87,0	script	Selim	128,{
	setarray @todas_cores$, "vermelha", "verde", "azul escura", "amarela", "azul clara", "rosa", "preta", "laranja", "roxa", "verde escura";
	setarray @todas_cores_cap$, "Vermelha", "Verde", "Azul Escura", "Amarela", "Azul Clara", "Rosa", "Preta", "Laranja", "Roxa", "Verde Escura";
	if (Sex==0) set @genre$, "vinda";
	if (Sex==1) set @genre$, "vindo";
	if (QUEST_tinta <= 0) goto L_begin;
	if (QUEST_tinta > 0 || (QUEST_MASK1 & MASK1_SABETINTA)) goto L_begin2;

L_begin:
	mes "[Selim]";
	mes "\"Olá, gostaria de pintar alguma roupa?\"";
	next;
	menu
		"Sim, claro. Mas onde consigo tintura de roupa?", L_explain_dye,
		"Hoje não.",-;
	close;

L_begin2:
	mes "[Selim]";
	mes "\"Bem " +@genre$+ " de volta! Quer pintar alguma roupa?\"";
	next;
	menu
		"Sim, claro.", L_clothes_choice,
		"Hoje não.",-;
	close;


L_clothes_choice:
	menu
		"Camisa de Algodão", L_cottonshirt,
		"Suéter com decote em V", L_vneck,
		"Suéter de Gola Alta", L_tneck,
		"Short de algodão", L_shorts,
		"Camiseta ", L_tanktop,
		"Camiseta Curta", L_tanktop_short,
		"Túnica de Seda", L_robe,
		"Faixa Tradicional", L_headband,
		"Chapéu do Deserto", L_desert_hat,
		"Botas de Algodão", L_cottonboots,
		"Luvas de Algodão", L_cottongloves,
		"Hoje não, obrigado.", -;
	close;

L_cottonshirt:
	set @del, 1202;
	set @new, 2050;
	set @name$, "Camisa de Algodão";
	goto L_picked_item;

L_vneck:
	set @del, 624;
	set @new, 2060;
	set @name$, "Suéter com decote em V";
	goto L_picked_item;

L_tneck:
	set @del, 564;
	set @new, 2070;
	set @name$, "Suéter de Gola Alta";
	goto L_picked_item;

L_shorts:
	set @del, 586;
	set @new, 2110;
	set @name$, "Short de Algodão";
	goto L_picked_item;

L_skirt:
	set @del, 632;
	set @new, 2100;
	set @name$, "Saia de Algodão";
	goto L_picked_item;

L_miniskirt:
	set @del, 771;
	set @new, 2170;
	set @name$, "Mini saia";
	goto L_picked_item;

L_tanktop:
	set @del, 688;
	set @new, 2090;
	set @name$, "Camiseta";
	goto L_picked_item;

L_tanktop_short:
	set @del, 689;
	set @new, 2120;
	set @name$, "Camiseta Curta";
	goto L_picked_item;

L_robe:
	set @del, 720;
	set @new, 2080;
	set @name$, "Túnica de Seda";
	goto L_picked_item;

L_headband:
	set @del, 724;
	set @new, 2140;
	set @name$, "Faixa Tradicional";
	goto L_picked_item;

L_desert_hat:
	set @del, 723;
	set @new, 2130;
	set @name$, "Chapéu do Deserto";
	goto L_picked_item;

L_cottonboots:
	set @del, 735;
	set @new, 2150;
	set @name$, "Bota de Algodão";
	goto L_picked_item;

L_cottongloves:
	set @del, 741;
	set @new, 2160;
	set @name$, "Luva de Algodão";
	goto L_picked_item;

L_rabbitears:
	set @del, 1255;
	set @new, 2190;
	set @name$, "Orelha de Lebre";
	goto L_picked_item;

L_picked_item:
	if (countitem(@del) == 0) goto L_havenone;
L_colour:
	mes "[Selim]";
	mes "\"Excelente! Agora só falta escolher a cor.\"";
	next;
	menu
		@todas_cores_cap$[0], -,
		@todas_cores_cap$[1], -,
		@todas_cores_cap$[2], -,
		@todas_cores_cap$[3], -,
		@todas_cores_cap$[4], -,
		@todas_cores_cap$[5], -,
		@todas_cores_cap$[6], -,
		@todas_cores_cap$[7], -,
		@todas_cores_cap$[8], -,
		@todas_cores_cap$[9], -;
	set @cor, @menu - 1;
	set @vial, 690 + @cor;
	if (countitem(@vial) == 0) goto L_no_dye;
	goto L_finish;

L_no_dye:
	mes "[Selim]";
	mes "\"Hmmm... " + @name$ + "? Eu até poderia pintar esta peça de roupa, mas primeiro você tem que me trazer uma tinta " + @todas_cores$[@cor] + " .\"";
	next;
	menu
		"Me desculpe, eu queria uma outra cor.", L_colour,
		"Na verdade eu queria pintar um item diferente.", L_clothes_choice,
		"Onde mesmo eu posso conseguir tinta?", L_explain_dye,
		"Deixa pra lá.", -;
	close;

L_explain_dye:
	mes "[Selim]";
	mes "\"Bem... Tintura de roupa é bem cara. E como eu não cobro nada para pintar, eu não posso te oferecer tintas de graça.";
	mes "Mas muitos alquimistas sabem criar tintas. Talvez você encontre algum por aí.\"";
	if (QUEST_tinta == 0) set QUEST_tinta, 1;
	close;

L_finish:
	if (countitem(@del) == 0) goto L_havenone;
	delitem @vial, 1;
	delitem @del, 1;
	getitem @new + @cor, 1;
	mes "[Selim]";
	mes "\"Aqui está! " + @name$ + " " + @todas_cores$[@cor] + "... agora é só usar!\"";
	close;

L_havenone:
	mes "[Selim]";
	mes "\"Você não tem " + @name$ + " sem cor? Que pena.";
	next;
	mes "Você gostaria de pintar alguma outra coisa?\"";
	next;
	goto L_clothes_choice;
}
