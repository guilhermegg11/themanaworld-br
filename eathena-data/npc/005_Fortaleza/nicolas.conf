//#################################################################################
//#
//# Nicholas the Smith.
//# Participates in iron ore smithery and the following quests:
//# - Together with Pachua: Iron Shield quest (repeatable)
//#    (10 iron ores, 2 infantry helmets, 20000 GP, leather patch) -> iron shield
//# - Primary participant in the Setzer quest (monster oil/golden stinger subquests)
//#
//# Variables used:  @QUEST_Forestbow_state, nibble 3 (setzer quest)
//#                  @QUEST_Forestbow_state, nibble 4 (iron shield quest)
//#
//#################################################################################

005.gat,108,66,0	script	Nicolas	135,{

	set @IRON_ORE, 640;
	set @INFANTRY, 638;
	set @LEATHER_PATCH, 708;
	set @SHORT_SWORD, 536;
	set @MONSTER_OIL, 707;
	set @SETZER, 571;
	set @STEEL_SHIELD, 601;

	set @SETZER_XP, 100000;
	set @SHIELD_XP, 20000;

	set @SHIELD_INITIAL, 0;
	set @SHIELD_KNOWS_PATCH, 1;	// knows that a leather patch is needed
	set @SHIELD_COMPLETED, 2;	// shield completed once

	set @SETZER_INITIAL, 0;
	set @SETZER_KNOWS_OIL, 1;
	set @SETZER_KNOWS_STINGER, 2;
	set @SETZER_FLAG_MADE_OIL, 4;
	set @SETZER_FLAG_MADE_SETZER, 8;

	set @Q_SETZER_MASK, NIBBLE_3_MASK;
	set @Q_SETZER_SHIFT, NIBBLE_3_SHIFT;

	set @Q_SHIELD_MASK, NIBBLE_4_MASK;
	set @Q_SHIELD_SHIFT, NIBBLE_4_SHIFT;

	set @Q_SETZER_status, (QUEST_Forestbow_state & @Q_SETZER_MASK) >> @Q_SETZER_SHIFT;
	set @Q_SHIELD_status, (QUEST_Forestbow_state & @Q_SHIELD_MASK) >> @Q_SHIELD_SHIFT;

	mes "[Nicolas o Ferreiro]";
	mes "\"Olá, como vai?";
	mes "Eu sou um ferreiro especialista.";
	mes "Se você me trouxer um pouco de Minério de Ferro, posso fabricar para você um valioso escudo ou um resistente capacete.\"";
	next;
	menu
		"Eu tenho Minério de Ferro!", L_Check,
		"Onde eu posso encontrar esse Minério de Ferro?", L_Info,
		"Agora não, mas obrigado de qualquer forma.", L_Pass;

L_Check:
	mes "[Nicolas o Ferreiro]";
	mes "\"Deixa eu olhar o que você tem aí...\"";
	next;
	if(countitem(@IRON_ORE)<5) goto L_NoItem;
	if(countitem(@IRON_ORE)<10) goto L_StageA;
	if(countitem(@IRON_ORE)<15) goto L_StageB;
	goto L_StageC;

L_Info:
	mes "[Nicolas o Ferreiro]";
	mes "\"As minas costumavam ter bastante Minério de ferro.";
	mes "Mas agora é um pouco complicado de se achar, pois os limos amarelos pegaram praticamente todo o minério disponível.\"";
	close;

L_Pass:
	mes "[Nicolas o Ferreiro]";
	mes "\"Ah, ok.";
	mes "Volte sempre que quiser.\"";
	close;

L_StageA:
	mes "[Nicolas o Ferreiro]";
	mes "\"Isso é suficiente para eu fabricar para você o famoso Elmo do Cavaleiro Alado.";
	mes "Mas lhe custará uns 10,000 GP e 5 pedaços de Minério de Ferro.\"";
	next;
	goto L_main_menu;

L_StageB:
	mes "[Nicolas o Ferreiro]";
	mes "\"Ahh, com todo esse Minério de Ferro, eu posso lhe fazer um ou dois capacetes, por apenas 10,000 GP, ou então, lhe faço um escudo, por 20,000 GP.\"";
	mes "";
	mes "\"O que você escolhe?\"";
	next;
	goto L_main_menu;

L_StageC:
	mes "[Nicolas o Ferreiro]";
	mes "\"Excelente, isso é suficiente para fazer três tipos diferentes de capacetes. Meu trabalho sai por 10,000GP.";
	mes "Também posso lhe fazer um escudo, mas isso vai custar em torno de 20,000 GP.\"";
	mes "";
	mes "\"Qual capacete ou escudo você vai querer?\"";
	next;
	goto L_main_menu;

L_main_menu:
	set @CHOICE_KNIGHT, 1;
	set @CHOICE_CRUSADE, 2;
	set @CHOICE_WARLORD, 3;
	set @CHOICE_SHIELD, 4;
	set @CHOICE_SETZER, 5;

	setarray @choice$, "", "", "", "", "", "";
	set @choices_nr, 0;
	setarray @choice_idx, 0, 0, 0, 0, 0;

	set @choice$[@choices_nr], "Não quero negociar no momento...";
	set @choices_nr, 1 + @choices_nr;

	if (countitem(@SHORT_SWORD) < 1) goto L_main_menu_post_setzer;
	set @choice$[@choices_nr], "Você pode fazer a minha espada curta ficar melhor?";
	set @choice_idx[@choices_nr], @CHOICE_SETZER;
	set @choices_nr, 1 + @choices_nr;

L_main_menu_post_setzer:
	if (countitem(@IRON_ORE)<5) goto L_no_more_helmets;
	set @choice$[@choices_nr], "Elmo do Cavaleiro Alado. (5 Minérios de Ferro)";
	set @choice_idx[@choices_nr], @CHOICE_KNIGHT;
	set @choices_nr, 1 + @choices_nr;

	if (countitem(@IRON_ORE)<10) goto L_no_more_helmets;

	set @choice$[@choices_nr], "Elmo das Cruzadas. (10 Minérios de Ferro)";
	set @choice_idx[@choices_nr], @CHOICE_CRUSADE;
	set @choices_nr, 1 + @choices_nr;

	set @choice$[@choices_nr], "Escudo de Aço. (10 Minérios de Ferro)";
	set @choice_idx[@choices_nr], @CHOICE_SHIELD;
	set @choices_nr, 1 + @choices_nr;

	if (countitem(@IRON_ORE)<15) goto L_no_more_helmets;

	set @choice$[@choices_nr], "Elmo do Lorde da Guerra. (15 Minérios de Ferro)";
	set @choice_idx[@choices_nr], @CHOICE_WARLORD;
	set @choices_nr, 1 + @choices_nr;

L_no_more_helmets:

	menu
		@choice$[0], -,
		@choice$[1], -,
		@choice$[2], -,
		@choice$[3], -,
		@choice$[4], -,
		@choice$[5], -;

	set @menu, @menu - 1;

	if (@menu >= @choices_nr)
	close;

	set @choice, @choice_idx[@menu];

	if (@choice == @CHOICE_KNIGHT) goto L_YesKnight;
	if (@choice == @CHOICE_CRUSADE) goto L_YesCrusade;
	if (@choice == @CHOICE_WARLORD) goto L_YesWarlord;
	if (@choice == @CHOICE_SHIELD) goto L_YesShield;
	if (@choice == @CHOICE_SETZER) goto L_SetzerQuest;
	close;

L_NoItem:
	mes "[Nicolas o Ferreiro]";
	mes "\"Parece que você não tem Minério de Ferro suficiente para eu trabalhar.";
	mes "Volte quando você tiver maior quantidade, Obrigado.\"";
	close;

L_ComeBack:
	mes "[Nicolas o Ferreiro]";
	mes "\"Volte sempre que quiser.\"";
	close;

L_NoMoney:
	mes "[Nicolas o Ferreiro]";
	mes "\"Que pena, parece que você não tem dinheiro suficiente.\"";
	close;

L_YesKnight:
	if (zeny < 10000) goto L_NoMoney;
	getinventorylist;
	if (@inventorylist_count == 100) goto L_TooMany;
	set zeny, zeny - 10000;
	delitem @IRON_ORE, 5;
	getitem 637, 1;
	goto L_Done;

L_YesCrusade:
	if (zeny < 10000) goto L_NoMoney;
	getinventorylist;
	if (@inventorylist_count == 100) goto L_TooMany;
	set zeny, zeny-10000;
	delitem @IRON_ORE, 10;
	getitem 639, 1;
	goto L_Done;

L_YesWarlord:
	if (zeny < 10000) goto L_NoMoney;
	getinventorylist;
	if (@inventorylist_count == 100) goto L_TooMany;
	set zeny, zeny-10000;
	delitem @IRON_ORE, 15;
	getitem 636, 1;
	goto L_Done;

L_YesShield:
	mes "[Nicolas o Ferreiro]";
	mes "Nicolas examina o seu Minério de Ferro.";
	mes "\"Não... Esse ferro é muito quebradiço. Para algo tão grande quanto um escudo eu preciso fundir este material a uma peça macia de ferro. Posso ver se você tem alguma coisa que sirva a esta fusão?\"";
	next;
	menu
		"Claro, olhe meu inventário!", -,
		"Não.", L_Pass;
	if (countitem(@INFANTRY) == 1) goto L_MoreInfantry;
	if (countitem(@INFANTRY) == 0) goto L_NoInfantry;

	mes "[Nicolas o Ferreiro]";
	mes "Nicolas retira dois de seus Capacetes da Infantaria.";
	mes "\"Eu poderia usar esses... sim, com eles vai funcionar. Tudo que preciso agora é de um Retalho de couro para a empunhadura e 20,000 GP.\"";

	if (@Q_SHIELD_status < @SHIELD_KNOWS_PATCH)
	set @Q_SHIELD_status, @SHIELD_KNOWS_PATCH;
	callsub L_Update_Var;

	next;
	menu
		"Aqui está.", -,
		"Onde posso encontrar um retalho de couro?", L_WhereLeather,
		"Nem pensar!", L_Pass;

	if (zeny < 20000) goto L_ShieldNoZeny;
	if (countitem(@INFANTRY) < 2) goto L_ShieldNoInfantry;
	if (countitem(@IRON_ORE) < 10) goto L_ShieldNoOre;
	if (countitem(@LEATHER_PATCH) < 1) goto L_ShieldNoLeatherPatch;
	// No inventory check needed, as infantry helms are removed, opening two slots

	mes "[Nicolas o Ferreiro]";
	mes "\"Parece que você tem tudo que eu preciso aí!\"";
	mes "Você observa Nicolas derreter o ferro e despejar em um molde de escudo. Então ele corta o Retalho de couro e o adiciona em duas empunhaduras.";
	mes "\"Aqui está o seu escudo!\"";

	delitem @INFANTRY, 1;
	delitem @INFANTRY, 1;
	delitem @LEATHER_PATCH, 1;
	delitem @IRON_ORE, 10;
	set zeny, zeny - 20000;

	if (@Q_SHIELD_status < @SHIELD_COMPLETED)
	getexp @SHIELD_XP, 0;
	if (@Q_SHIELD_status < @SHIELD_COMPLETED)
	mes "[Você ganhou " + @SHIELD_XP + " pontos de experiência]";

	set @Q_SHIELD_status, @SHIELD_COMPLETED; // get XP only once
	callsub L_Update_Var;

	getitem @STEEL_SHIELD, 1;
	close;

L_ShieldNoZeny:
	mes "[Nicolas o Ferreiro]";
	mes "\"Me desculpe, mas eu preciso lhe cobrar 20,000 GP, pois esse trabalho é de alta qualidade!\"";
	close;

L_ShieldNoInfantry:
	mes "[Nicolas o Ferreiro]";
	mes "\"Isso é estranho... Eu poderia jurar que você tinha dois Capacetes da Infantaria bem aqui. Volte se você tiver mais desses!\"";
	close;

L_ShieldNoOre:
	mes "[Nicolas o Ferreiro]";
	mes "\"Que engraçado... Não coloquei os seus Minérios de Ferro bem aqui em cima da mesa? Bem, eu vou precisar deles para fazer o escudo.\"";
	close;

L_ShieldNoLeatherPatch:
	mes "[Nicolas o Ferreiro]";
	mes "\"Você não tem um Retalho de couro adequado. Me desculpe, mas um escudo sem uma empunhadura de couro é terrível de ser usado.\"";
	close;

L_MoreInfantry:
	mes "[Nicolas o Ferreiro]";
	mes "Nicolas pega seu Capacete de Infantaria e o analisa.";
	mes "\"Sim, isso é perfeito! Se você me trouxer mais um desses eu posso fazer o escudo.\"";
	close;

L_NoInfantry:
	mes "[Nicolas o Ferreiro]";
	mes "Nicolas balança a cabeça.";
	mes "\"Não, nenhum de seus itens é adequado. Tente me trazer coisas de metal que sejam grandes.\"";
	close;

L_WhereLeather:
	mes "[Nicolas o Ferreiro]";
	mes "\"Hmm. Você deveria procurar alguém que possa trabalhar com algum tipo de couro. Os rumores dizem que há um índio que pode, ele vive além das cavernas cheias de serpentes, mas eu não pude confirmar o boato, pois o caminho até lá é muito perigoso.\"";
	close;


L_SetzerQuest:
	mes "[Nicolas o Ferreiro]";
	if (@Q_SETZER_status & @SETZER_FLAG_MADE_SETZER)
	mes "\"Outra espada? Claro que sim.\"";
	mes "Nicolas examina a sua espada e então lhe fala.";
	mes "\"Essa é realmente de qualidade.  Eu tenho certeza que posso fazer algo bem especial com ela, com 5 nacos de Minério de Ferro mais uns 50,000 GP. Mas eu também vou precisar de uma Poção de Óleo de Monstro.\"";
	if (@Q_SETZER_status < @SETZER_KNOWS_OIL)
	set @Q_SETZER_status, @SETZER_KNOWS_OIL;
	callsub L_Update_Var;
	next;
	menu
		"Aqui está.", -,
		"Óleo de Monstro, que diabos é isso?", L_ExplainMonsterOil,
		"QUANTO? Tá maluco é?.", L_Pass;

	if (zeny < 50000) goto L_SetzerNoZeny;
	if (countitem(@IRON_ORE) < 5) goto L_SetzerNoOre;
	if (countitem(@MONSTER_OIL) < 1) goto L_SetzerNoMonsterOil;
	if (countitem(@SHORT_SWORD) < 1) goto L_SetzerNoSword;
	// No inventory check needed, as the short sword is removed, opening a slot

	mes "[Nicolas o Ferreiro]";
	mes "Nicolas pega os itens, aquece sua espada e bate nela com um martelo bem pesado. Depois de várias batidas, ele finalmente despeja o Óleo de Monstro na espada, a aquece novamente e balanceia a forja em um pote de água.";

	set zeny, zeny - 50000;
	delitem @IRON_ORE, 5;
	delitem @MONSTER_OIL, 1;
	delitem @SHORT_SWORD, 1;

	getitem @SETZER, 1;
	if (!(@Q_SETZER_status & @SETZER_FLAG_MADE_SETZER))
	mes "[Você ganhou " + @SETZER_XP + " pontos de experiência]";
	if (!(@Q_SETZER_status & @SETZER_FLAG_MADE_SETZER))
	getexp @SETZER_XP, 1;
	set @Q_SETZER_status, @Q_SETZER_status | @SETZER_FLAG_MADE_SETZER;
	callsub L_Update_Var;
	next;

	mes "[Nicolas o Ferreiro]";
	mes "Nicolas lhe entrega a espada. Ela parece ser muito leve.";
	mes "\"Eu fiz ela ficar mais leve e mais rápida. Ela também deve cortar melhor agora.  Esse tipo de espada foi nomeada como Setzer, em homenagem ao ferreiro que a inventou.. Trata-se de uma arma muito versátil, letal como uma espada e leve como uma faca.\"";
	next;
	mes "[Nicolas o Ferreiro]";
	mes "\"Estou bem feliz com esse trabalho, tenha cuidado com os gumes, pois são muito afiados.\"";
	close;

L_ExplainMonsterOil:
	mes "[Nicolas o Ferreiro]";
	mes "\"Óleo de Monstro é um tipo especial de óleo usado para fortalecer camadas finas de aço. Infelizmente, é quase impossível de se conseguir hoje em dia... Se você tiver muita sorte, talvez um alquimista por aí saiba como conseguir um.\"";
	close;

L_SetzerNoZeny:
	mes "[Nicolas o Ferreiro]";
	mes "\"Me desculpe, mas você terá que pagar os 50,000 GP, pois essa arma que pretendo fazer é muito difícil de ser feita.\"";
	close;

L_SetzerNoOre:
	mes "[Nicolas o Ferreiro]";
	mes "\"Sem os 5 nacos de Minério de Ferro eu não serei capaz de melhorar a sua espada.\"";
	close;

L_SetzerNoMonsterOil:
	mes "[Nicolas o Ferreiro]";
	mes "\"Eu sei que o Óleo de Monstro está praticamente impossível de se conseguir hoje em dia, mas se você pedir ajuda a algum alquimista, acho que conseguirá.\"";
	close;

L_SetzerNoSword:
	mes "[Nicolas o Ferreiro]";
	mes "\"Precisarei de sua antiga espada curta para usar como base da arma, por favor me traga ela aqui.\"";
	close;

L_end:
	close;

L_Done:
	mes "[Nicolas o Ferreiro]";
	mes "\"Aqui está!\"";
	mes "";
	mes "\"Volte sempre que quiser.\"";
	close;

L_Update_Var:
	set QUEST_Forestbow_state,
		(QUEST_Forestbow_state & ~(@Q_SHIELD_MASK | Q_SETZER_MASK)
		| (@Q_SHIELD_status << @Q_SHIELD_SHIFT)
		| (@Q_SETZER_status << @Q_SETZER_SHIFT));
	return;

L_TooMany:
	mes "[Nicolas o Ferreiro]";
	mes "\"Você não tem lugar para isso, volte sempre que quiser.\"";
	close;
}
