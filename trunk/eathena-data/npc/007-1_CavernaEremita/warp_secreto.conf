/**
 * Warp condicional que trata toda a lógica do Warp Secreto.
 *
 * @date sexta, 14 de janeiro de 2011
 * @author Diogo_RBG - http://diogorbg.blogspot.com/
 */

007-1.gat,53,21,0	script	#warpSecreto	0,0,0{
	if(QUEST_MASK1 & MASK1_WARP_SECRETO) goto _warp; //< Quest Warp Secreto concluída. Estado 6.

	if(QUEST_warpSecreto2==1 || QUEST_warpSecreto2==2) goto _quaseAcha;
	if(QUEST_warpSecreto2==3) goto _apenasWarp;
	if(QUEST_warpSecreto2>=4) goto _warp; //< Estados 4 e 5.

	set QUEST_warpSecreto, 0; //< Apaga a variável antiga.

	set @tmp, rand(4);
	if(@tmp==0) warp "007-1.gat", 64, 37;
	if(@tmp==1) warp "007-1.gat", 79, 58;
	if(@tmp==2) warp "007-1.gat", 95, 40;
	if(@tmp==3) warp "007-1.gat", 97, 56;

	if(countitem(3006)>0) goto _comMapa;

	set @tmp, rand(4);
	if(@tmp==0) message strcharinfo(0), "Acho que me perdi!";
	if(@tmp==1) message strcharinfo(0), "Preciso de um mapa!";
	if(@tmp==2) message strcharinfo(0), "Estas cavernas são muito confusas!";
	if(@tmp==3) message strcharinfo(0), "Preciso de ajuda para encontrar a passagem.";
	end;

_comMapa:
	set @tmp, rand(2);
	if(@tmp==0) message strcharinfo(0), "Este mapa não ajuda em nada!";
	if(@tmp==1) message strcharinfo(0), "Não consigo entender este mapa.";
	end;

_quaseAcha:
	warp "008-6.gat", 127, 77;
	set @warpSecreto, -1;
	set QUEST_warpSecreto2, 2;
	end;

_apenasWarp:
	warp "008-6.gat", 127, 77;
	end;

_warp:
	warp "008-6.gat", 127, 77;
	set @warpSecreto, 1;
	close;

}
