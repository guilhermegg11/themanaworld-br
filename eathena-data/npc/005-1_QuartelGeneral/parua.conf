// Parua's fight - Original from Kage
// Adapted to the brazilian server and translated to portuguese by alastrim
// Adaptado do original desenvolvido por kage


005-1.gat,38,46,0	script	Parua	307,{
	set @dentro, 0;
	if isin("005-1.gat", 28, 46, 53, 65), set @dentro, 1;
	set @INDIVIDUO$, strcharinfo(0);
	if (($@ARENA_STATUS_IND == 1) && (@INDIVIDUO$ == $@LUTADOR$)) goto L_inddentro;
	if (($@ARENA_STATUS_IND == 1) && (@INDIVIDUO$ != $@LUTADOR$)) goto L_indfora;
	if (($@ARENA_STATUS_GRUPO == 2) && (@dentro == 1)) goto L_grupodentro;
	if (($@ARENA_STATUS_GRUPO == 2) && (@dentro == 0)) goto L_grupofora;
	if (($@ARENA_STATUS_GRUPO == 1) && (@dentro == 1)) goto L_grupodentro2;
	if (($@ARENA_STATUS_GRUPO == 1) && (@dentro == 0) && (PAGOU_GRUPO == 1)) goto L_perguntagrupo2;
	if (($@ARENA_STATUS_GRUPO == 1) && (@dentro == 0)) goto L_perguntagrupo;

L_menu:
	mes "[Parua]";
	mes "\"Olá "+ strcharinfo(0) +"!\"";
	next;
	mes "[Parua]";
	mes "\"Você gostaria de saber se é forte o suficiente enfrentando os desafios de minha arena? Desta vez o desafio está um pouco diferente... Se eu fosse você aproveitaria a oportunidade.\"";

	mes "["+ strcharinfo(0) +"]";
	menu
		"=S Não, Agradeço!", L_fim,
		"=] Sim, Por favor!", L_comeca,
		"=| Qual é o maior Record?", L_record;

L_comeca:
	if (getgmlevel(0) >= 20 && (@gmtemp != 1)) goto L_avisogm;
	mes "[Parua]";
	mes "\"Muito bem! Mas para aceitar meu desafio, você precisará me pagar uma quantia em dinheiro...\"";

	mes "["+ strcharinfo(0) +"]";
	menu
		"=) OK! Mas quanto preciso pagar?", -,
		"=[ Me desculpe, mas estou sem dinheiro no momento.", L_fim;

	if BaseLevel <= 10,  set @preco, 1000;
	if (BaseLevel > 10) && (BaseLevel <= 20), set @preco, 3000;
	if (BaseLevel > 20) && (BaseLevel <= 30), set @preco, 5000;
	if (BaseLevel > 30) && (BaseLevel <= 40), set @preco, 9000;
	if (BaseLevel > 40) && (BaseLevel <= 50), set @preco, 15000;
	if (BaseLevel > 50) && (BaseLevel <= 60), set @preco, 20000;
	if (BaseLevel > 60) && (BaseLevel <= 70), set @preco, 30000;
	if (BaseLevel > 70) && (BaseLevel <= 80), set @preco, 40000;
	if (BaseLevel > 80) && (BaseLevel <= 90), set @preco, 50000;
	if (BaseLevel > 90) && (BaseLevel <= 255), set @preco, 60000;

	mes "[Parua]";
	mes "\"Se você for lutar individualmente, o preço é somente " + @preco/1000 + " KGP. Mas se quiser uma disputa em grupo o preço é o total de 5KGP para cada componente do grupo. \"";
	next;
	menu
		"=) OK! Quero lutar na modalidade individual.", L_individual,
		"=) Não vou enfrentar essa arena sem ajuda. Quero a disputa em grupo.", L_grupo,
		"=[ Me desculpe, mas realmente não tenho como pagar.", L_fim;

L_perguntagrupo:
	getpartymember($@GRUPO_ID);
	mes "[Parua]";
	mes "\"Você gostaria de participar do desafio iniciado pelo grupo " +$@GRUPO$+ "?\"";
	menu
		"Eu não, sai pra lá...", -,
		"Sim, pretendo ajudar meu grupo!", -;
	if (@menu == 2) goto L_sepertencegrupo;
	close;

L_perguntagrupo2:
	mes "[Parua]";
	mes "\"Você gostaria de voltar para a arena?\"";
	menu
		"Ainda não...", -,
		"Sim!", -;
	if (@menu == 2)	warp "005-1.gat", 41, 55;
	close;


L_sepertencegrupo:
	if (getcharid(1) != $@GRUPO_ID) goto L_naopertence;
	goto L_desafiogrupo2;

L_naopertence:
	mes "[Parua]";
	mes "\"Você não faz parte do grupo " +$@GRUPO$+ "!\"";
	close;

L_sai:
	warp "005-1.gat", 40, 42;
	set @dentro, 0;
	close;

L_individual:
	mes "[Parua]";
	mes "\"Ótima escolha! A arena é muito simples. Eu vou sumonando os monstros e você tem que matá-los. Só isso!\"";
	mes "\"Se você não conseguir se manter de pé, ou se sair por aquele portão, o desafio acaba! Ah, e para não prejudicar as outras pessoas que querem participar, todos os desafios tem um limite de 45 minutos. Ou seja, você deve fazer o seu melhor neste tempo!\"";
	mes "\"E então, ainda está com vontade de lutar?\"";
	menu
		"=) Claro! Pode começar.", L_sozinho,
		"=[ Acho que preciso resolver algumas coisas antes!", L_fim;


L_grupo:
	mes "[Parua]";
	mes "\"Excelente... Quero ver essa arena ferver! Mas antes vou te explicar algumas coisas.\"";
	mes "\"A arena é muito simples. Eu vou sumonando os monstros e seu grupo têm que lutar contra eles. Só isso!\"";
	mes "\"Ah, e para não prejudicar as outras pessoas que querem participar, todos os desafios tem um limite de 45 minutos. Ou seja, seu grupo deve fazer o melhor possível neste tempo!\"";
	mes "\"Outra coisa. Vou sumonar os monstros daqui a 5 minutos. Assim você terá tempo de reunir seu grupo.\"";
	mes "\"E quem estiver do lado de fora depois dos 5 minutos não vai poder entrar, ok?\"";
	mes "\"E então, gostou da idéia?\"";
	menu
		"=) Claro! Mas quanto é mesmo que tenho que pagar?", L_desafiogrupo,
		"=[ Isso não me parece uma boa idéia!", L_fim;

L_sozinho:
	if (zeny < @preco) goto L_semgrana;
	warp "005-1.gat", 41, 55;
	if (getareausers("005-1.gat", 28, 46, 53, 65) > 1) goto L_naosozinho;
	set zeny, zeny - @preco;
	npctalk "Vamos ver como você se sai sozinho!";
	set $@LUTADOR$, @INDIVIDUO$;
	set $@ARENA_STATUS_IND, 1;
	set $@ARENA_LEVEL_IND, 1;
	set $@MORTES, PC_DIE_COUNTER;
	set $@ID_PARUA, getcharid(3);

	startnpctimer;	
	goto L_fim;

L_desafiogrupo:
	set $@GRUPO_ID, getcharid(1);
	if ($@GRUPO_ID == 0) goto L_naogrupo;
	set $@GRUPO$, getpartyname($@GRUPO_ID);
	mes "[Parua]";
	mes "\"Apenas 5kGP para cada pessoa do seu grupo que quiser participar. Cada um paga a sua parte.\"";
	menu
		"=) OK! ", -,
		"=[ Está muito caro. Vou desistir.", -;
	if (@menu == 2) goto _L_desistegrupo;
	if (zeny < 5000) goto L_semgrana;
	set zeny, zeny - 5000;
	mes "\"Tudo certo. Vou começar a contar os 5 minutos a partir de agora!\"";
	set $@ARENA_STATUS_GRUPO, 1;
	set $@INICIO_REUNIAO_GRUPO, gettimetick(2);
	set PAGOU_GRUPO, 1;
	close2;
	warp "005-1.gat", 41, 55;
	startnpctimer;
	end;	

L_desistegrupo:
	mes "[Parua]";
	mes "\"Ok, tudo bem!\"";
	set $@GRUPO_ID, 0;
	set $@GRUPO$, 0;
	close;

L_desafiogrupo2:
	mes "[Parua]";
	mes "\"Ótimo. Vai lhe custar apenas 5kGP.\"";
	menu
		"=) Ok! ", -,
		"=[ Está muito caro. Vou desistir.", L_fim;
	if (zeny < 5000) goto L_semgrana;
	set zeny, zeny - 5000;
	close2;
	warp "005-1.gat", 41, 55;
	end;

L_comecagrupo:
	if (getareausers("005-1.gat", 28, 46, 53, 65) <= 0) goto L_limpa;
	npctalk "Que a batalha começe!";
	set $@ARENA_STATUS_GRUPO, 2;
	set $@ARENA_LEVEL_GRUPO, 1;
	set $@ARENA_CONTA_JOGADORES, getareausers("005-1.gat", 28, 46, 53, 65);
	set $@INICIO_REUNIAO_GRUPO, 0;
	set PAGOU_GRUPO, 0;
	startnpctimer;
	goto L_fim;

L_naosozinho:
	mes "[Parua]";
	mes ">=[ \"Eu estou vendo que tem mais gente aqui... Porque não pede pra eles sairem? Com educação, claro... =)\"";
	goto L_fim;

L_naogrupo:
	mes "[Parua]";
	mes ">=[ \"Hmmm... Você não faz parte de um grupo. Por favor, volte quando fizer parte de um grupo!\"";
	goto L_fim;

L_semgrana:
	mes "[Parua]";
	mes "=S \"Parece que você não tem dinheiro suficiente.\"";
	goto L_fim;

L_record:
	npctalk "Records anteriores: Grupo:" + $ARENA_RECORD_GRUPO + " - Lvl: " + $RECORD_GRUPO$ + " --- Individual: " +$ARENA_RECORD_IND+ " - Lvl " +$RECORD_IND$;
	if($ARENA_RECORD_IND2==0 && $ARENA_RECORD_GRUPO2==0) npctalk "Não existe ninguem suficientemente corajoso neste mundo para aceitar meu novo desafio!";
	if($ARENA_RECORD_GRUPO2>0) npctalk "O record da arena pertence ao clan/grupo " + $RECORD_GRUPO2$ + ", que conseguiu a façanha de ser o primeiro a chegar ao nível " + $ARENA_RECORD_GRUPO2 + ".";
	if($ARENA_RECORD_IND2>0) npctalk "O record individual da arena pertence a " + $RECORD_IND2$ + ", que foi o primeiro a chegar ao nível " + $ARENA_RECORD_IND2 + ".";
	close;

L_reunegrupo:
	if (gettimetick(2) >= $@INICIO_REUNIAO_GRUPO + 300) goto L_comecagrupo;
	end;

OnTimer5000:
	setnpctimer 0;
	if ($@ARENA_STATUS_IND == 1) goto L_logica;
	if ($@ARENA_STATUS_GRUPO == 1) goto L_reunegrupo;
	if ($@ARENA_STATUS_GRUPO == 2) goto L_logicagrupo;

L_logica:
	attachrid($@ID_PARUA);
	if (PC_DIE_COUNTER > $@MORTES) goto L_limpa;
	if (getareausers("005-1.gat", 28, 46, 53, 65) <= 0) goto L_limpa;
	set $@ARENA_ROUND_TEMPO, $@ARENA_ROUND_TEMPO + 5; // Avança 5 segundos
	if ($@ARENA_ROUND_TEMPO >= 120) goto L_proximoround;
	if (mobcount("005-1.gat", "Parua::onPetDeath") <= 0) goto L_proximoround;
	end;

L_logicagrupo:
	set $@ARENA_CONTA_JOGADORES, getareausers("005-1.gat", 28, 46, 53, 65);
	if ($@ARENA_CONTA_JOGADORES <= 0) goto L_limpa;
	set $@ARENA_ROUND_TEMPO, $@ARENA_ROUND_TEMPO + 5; // Avança 5 seconds
	if ($@ARENA_ROUND_TEMPO >= 2700) goto L_limpa;
	if (mobcount("005-1.gat", "Parua::onPetDeath") <= 0) goto L_proximoroundgrupo;
	end;

L_proximoround:
	set $@ARENA_ROUND_TEMPO, 0;
	set $@ARENA_LEVEL_IND, $@ARENA_LEVEL_IND + 1;
	set $@ARENA_POINTS, $@ARENA_LEVEL_IND;
	goto L_return;

L_proximoroundgrupo:
	set $@ARENA_LEVEL_GRUPO, $@ARENA_LEVEL_GRUPO + 1 + ($@ARENA_CONTA_JOGADORES/2) + ($@ARENA_LEVEL_GRUPO / 40);
	set $@ARENA_POINTS, $@ARENA_LEVEL_GRUPO;
	goto L_return;

L_return:
	if ($@ARENA_STATUS_IND == 1) set $@ARENA_LEVEL, $@ARENA_LEVEL_IND;
	if ($@ARENA_STATUS_GRUPO == 2) set $@ARENA_LEVEL, $@ARENA_LEVEL_GRUPO;

	npctalk "Nova rodada começa! O nível desta rodada é " + $@ARENA_LEVEL;

	set $@MOB_1_SUMMON, 0;
	set $@MOB_2_SUMMON, 0;
	set $@MOB_3_SUMMON, 0;
	set $@MOB_4_SUMMON, 0;
	set $@MOB_5_SUMMON, 0;
	set $@MOB_6_SUMMON, 0;
	set $@MOB_7_SUMMON, 0;
	set $@MOB_8_SUMMON, 0;
	goto L_SUMMON;

L_SUMMON:
	if ($@ARENA_POINTS >= 128 && $@MOB_1_SUMMON < 3) goto L_MOB1;
	if ($@ARENA_POINTS >= 96 && $@MOB_2_SUMMON < 5) goto L_MOB2;
	if ($@ARENA_POINTS >= 64 && $@MOB_3_SUMMON < 6) goto L_MOB3;
	if ($@ARENA_POINTS >= 32 && $@MOB_4_SUMMON < 8) goto L_MOB4;
	if ($@ARENA_POINTS >= 16 && $@MOB_5_SUMMON < 12) goto L_MOB5;
	if ($@ARENA_POINTS >= 8 && $@MOB_6_SUMMON < 15) goto L_MOB6;
	if ($@ARENA_POINTS >= 4 && $@MOB_7_SUMMON < 18) goto L_MOB7;
	if ($@ARENA_POINTS >= 1 && $@MOB_8_SUMMON < 20) goto L_MOB8;
	end;


//JackO
L_MOB1:
	set $@MOB_1_SUMMON, $@MOB_1_SUMMON + 1;
	set $@ARENA_POINTS, $@ARENA_POINTS - 128;
	areamonster "005-1.gat", 29, 47, 52, 64, "", 1022, 1, "Parua::onPetDeath";
	goto L_SUMMON;

//Guerreiro Caido
L_MOB2:
	set $@MOB_2_SUMMON, $@MOB_2_SUMMON + 1;
	set $@ARENA_POINTS, $@ARENA_POINTS - 96;
	areamonster "005-1.gat", 29, 47, 52, 64, "", 1045, 1, "Parua::onPetDeath";
	goto L_SUMMON;

//Esqueleto Guerreiro
L_MOB3:
	set $@MOB_3_SUMMON, $@MOB_3_SUMMON + 1;
	set $@ARENA_POINTS, $@ARENA_POINTS - 64;
	areamonster "005-1.gat", 29, 47, 52, 64, "", 1043, 1, "Parua::onPetDeath";
	goto L_SUMMON;


//Esqueleto Pirata
L_MOB4:
	set $@MOB_4_SUMMON, $@MOB_4_SUMMON + 1;
	set $@ARENA_POINTS, $@ARENA_POINTS - 32;
	areamonster "005-1.gat", 29, 47, 52, 64, "", 1305, 1, "Parua::onPetDeath";
	goto L_SUMMON;

//Cobra da Montanha
L_MOB5:
	set $@MOB_5_SUMMON, $@MOB_5_SUMMON + 1;
	set $@ARENA_POINTS, $@ARENA_POINTS - 16;
	areamonster "005-1.gat", 29, 47, 52, 64, "", 1026, 1, "Parua::onPetDeath";
	goto L_SUMMON;

//Esqueleto Arqueiro
L_MOB6:
	set $@MOB_6_SUMMON, $@MOB_6_SUMMON + 1;
	set $@ARENA_POINTS, $@ARENA_POINTS - 8;
	areamonster "005-1.gat", 29, 47, 52, 64, "", 1304, 1, "Parua::onPetDeath";
	goto L_SUMMON;

//Cobra
L_MOB7:
	set $@MOB_7_SUMMON, $@MOB_7_SUMMON + 1;
	set $@ARENA_POINTS, $@ARENA_POINTS - 2;
	areamonster "005-1.gat", 29, 47, 52, 64, "", 1010, 1, "Parua::onPetDeath";
	goto L_SUMMON;

//Limo Vermelho
L_MOB8:
	set $@MOB_8_SUMMON, $@MOB_8_SUMMON + 1;
	set $@ARENA_POINTS, $@ARENA_POINTS - 1;
	areamonster "005-1.gat", 29, 47, 52, 64, "", 1008, 1, "Parua::onPetDeath";
	goto L_SUMMON;

onPetDeath:
	end;

onInit:
	initnpctimer;
	stopnpctimer;
	end;

L_limpa:
	npctalk "Fim de Jogo.";
	mapannounce "005-1.gat", "Parua: A arena já está livre para novos desafiantes!", 0;
	if ($@ARENA_LEVEL_IND > $ARENA_RECORD_IND2) goto L_novorecordindividual;
	if ($@ARENA_LEVEL_GRUPO > $ARENA_RECORD_GRUPO2) goto L_novorecordgrupo;
	set PAGOU_GRUPO, 0;
	set $@INICIO_REUNIAO_GRUPO, 0;
	set $@ARENA_STATUS_IND, 0;
	set $@ARENA_STATUS_GRUPO, 0;
	set $@ARENA_CONTA_JOGADORES, 0;
	set $@ARENA_LEVEL_IND, 1;
	set $@ARENA_LEVEL_GRUPO, 1;
	set $@ARENA_ROUND_TEMPO, 0;
	set $@MORTES, 0;
	set $@ID_PARUA, 0;

	killmonster "005-1.gat", "Parua::onPetDeath";
	setnpctimer 0;
	stopnpctimer;
	close2;
	end;

L_novorecordindividual:
	set $RECORD_IND2$, $@LUTADOR$;
	set $ARENA_RECORD_IND2, $@ARENA_LEVEL_IND;
	set $@ARENA_STATUS_IND, 0;
	set $@ARENA_LEVEL_IND, 1;
	set $@ARENA_ROUND_TEMPO, 0;
	set $@ARENA_CONTA_JOGADORES, 0;
	set $@MORTES, 0;
	set $@ID_PARUA, 0;
	killmonster "005-1.gat", "Parua::onPetDeath";
	stopnpctimer;
	setnpctimer 0;
	end;

L_novorecordgrupo:
	set $@INICIO_REUNIAO_GRUPO, 0;
	set PAGOU_GRUPO, 0;
	set $RECORD_GRUPO2$, $@GRUPO$;
	set $ARENA_RECORD_GRUPO2, $@ARENA_LEVEL_GRUPO;
	set $@ARENA_STATUS_GRUPO, 0;
	set $@ARENA_LEVEL_GRUPO, 1;
	set $@ARENA_ROUND_TEMPO, 0;
	set $@ARENA_CONTA_JOGADORES, 0;
	killmonster "005-1.gat", "Parua::onPetDeath";
	stopnpctimer;
	setnpctimer 0;
	end;

L_indfora:
	mes "[Parua]";
	mes "\"Olá, está gostando do desempenho de \"" + $@LUTADOR$ + "\"?\"";
	menu "Sim! " + $@LUTADOR$ + " é muito forte!", L_fim,
	     "Não! Não vejo a hora de chegar a minha vez!", L_fim,	
	     "Na verdade, eu queria saber mais sobre os recordes da arena.", L_record;

L_inddentro:
	npctalk "Preste atenção na luta " + $@LUTADOR$ + " !";

L_grupofora:
	mes "[Parua]";
	mes "\"Olá, está gostando do desempenho do pessoal do \"" + $@GRUPO$ + "\"?\"";
	menu "Sim! Eles são os melhores!", L_fim,
	     "Não! Não vejo a hora de chegar a minha vez!", L_fim,	
	     "Na verdade, eu queria saber mais sobre os recordes da arena.", L_record;

L_grupodentro:
	mes "Preste atenção na luta " + strcharinfo(0) + " ! Seu grupo depende de você!";
	close;

L_grupodentro2:
	mes "Você ainda tem " + (($@INICIO_REUNIAO_GRUPO + 300)-gettimetick(2)) + " segundos para reunir seu grupo!";
	menu
		"Deixe-me sair para reunir meu grupo", -,
		"Quero que isso começe agora!", -,
		"Vou aguardar mais um pouco. Tem gente chegando!", -;
	if (@menu == 1) warp "005-1.gat", 40, 42;
	if (@menu == 2) goto L_comecagrupo;
	close;

L_avisogm:
	set @gmtemp, 1;
	mes "[Parua]";
	mes "=S \"Oi. Eu já vi você usando uns truques estranhos por aí... Só espero que você não prejudique meu ranking com seus poderes... Isso causaria uma grande revolta nos meus clientes.\"";
	next;
	goto L_comeca;

L_fim:
	close;
	end;
	
}
