//
005-1.gat,40,27,0	script	Boram	156,{

	set @taxa, 20000;
	set @bolsa, 1000;

	if (getgmlevel(0) >= 20 && $@BORAM_STATUS != 0) goto L_menugm;
	if ($@BORAM_STATUS != 0) goto L_aguardar;

L_inicio:
	mes "[Boram]";
	mes "\"Olá. Meu nome é Boram. Eu sou um ex domador de feras. Há alguns anos eu larguei minha profissão para abrir o meu próprio negócio. O que eu faço aqui é bem simples. Por uma taxa de 20,000 GP eu coloco um monstro à sua disposição. Você deve derrotar este monstro o mais rápido possível. No final da luta eu registro o seu tempo e comparo com o recorde.\"";
	next;
	mes "\"Se você perder a luta ou se o seu tempo for pior que o recorde, você não ganha nada e eu adiciono 1,000 GP à bolsa de premiação. Se o tempo for melhor, você recebe a sua taxa de 20,000 GP de volta e ganha todo o valor acumulado na bolsa de premiação.\"";
	next;
	mes "\"Ou seja, se você quebrar um recorde que outras 100 pessoas tentaram sem sucesso, seu prêmio será de 100,000 GP! Legal, não é?\"";
	menu
		"Sim! Quero participar agora!", L_escolhe_monstro,
		"Eh, interessante, mas não estou interessado.", -;
	close;

L_escolhe_monstro:
	mes "[Boram]";
	mes "\"OK, basta escolher o nível que você quer disputar. Mas eu recomendo checar os recordes primeiro.\"";
	menu
		"Nível 1 - Mouboo", -,
		"Nível 2 - Caveira Envenenada", -,
		"Nível 3 - Jack O", -,
		"Quero ver os valores das bolsas de premiação.", L_bolsa,
		"Quero ver os recordes.", L_recordes,
		"Quero desistir.", L_fim;
	if (Zeny < @taxa) goto L_sem_grana;
	set Zeny, Zeny - @taxa;
	set $@NIVEL, @menu;
	set $@NOME$, strcharinfo(0);
	set $@BORAM_MORTES, PC_DIE_COUNTER;
	set $@BORAM_ID, getcharid(3);
	if ($@NIVEL == 1) set @mob_id, 1028;
	if ($@NIVEL == 2) set @mob_id, 1024;
	if ($@NIVEL == 3) set @mob_id, 1022;
	warp "005-1.gat", 44, 32;
	monster "005-1.gat",44,32,"",@mob_id,1,"Boram::OnMobDeath";
	set $@INICIO, gettimetick(2);
	set $@BORAM_STATUS, 1;
	npctalk "Começou a contagem do tempo!";
	startnpctimer;
	close;

L_bolsa:
	mes "Bolsa de premiação para o Nível 1 - Mouboo: " + $BOLSAS[1] + " GP";
	mes "";
	mes "Bolsa de premiação para o Nível 2 - Caveira Envenenada: " + $BOLSAS[2] + " GP";
	mes "";
	mes "Bolsa de premiação para o Nível 3 - JackO: " + $BOLSAS[3] + " GP";
	next;
	goto L_escolhe_monstro;

L_recordes:
	mes "Recorde do Nível 1 - Mouboo: " + $RECORDS[1] + " segundos. Recordista: " + $RECORDISTA$[1] + ".";
	mes "";
	mes "Recorde do Nível 2 - Caveira Envenenada: " + $RECORDS[2] + " segundos. Recordista: " + $RECORDISTA$[2] + ".";
	mes "";
	mes "Recorde do Nível 3 - JackO: " + $RECORDS[3] + " segundos. Recordista: " + $RECORDISTA$[3] + ".";
	next;
	goto L_escolhe_monstro;

OnMobDeath:
	if ($@BORAM_STATUS != 1) goto L_limpar;
	set $@FIM, gettimetick(2);
	npctalk "Desempenho de " + $@NOME$ + ":";
	npctalk "Nível escolhido: " + $@NIVEL + " -- Tempo/" + $@NOME$ + ": " + ($@FIM - $@INICIO) + " segundos -- Tempo/Recorde: " + $RECORDS[$@NIVEL] + " segundos.";
	if ($RECORDS[$@NIVEL] <= 0 || $@FIM - $@INICIO < $RECORDS[$@NIVEL]) goto L_novo_record;
	set $BOLSAS[$@NIVEL], $BOLSAS[$@NIVEL] + @bolsa;
	npctalk "Valor atual da bolsa de premiação: " + $BOLSAS[$@NIVEL] + " GP.";
	goto L_limpar;

L_novo_record:
	npctalk "Parabéns " + $@NOME$ + "! Você estabeleceu o novo record do nível " + $@NIVEL + " e ganhou o dinheiro depositado na bolsa de premiação do respectivo nível, cujo valor é de " + $BOLSAS[$@NIVEL] + " GP!";
	attachrid($@BORAM_ID);
	set Zeny, Zeny + @taxa + $BOLSAS[$@NIVEL];
	set $RECORDS[$@NIVEL], $@FIM - $@INICIO;
	set $RECORDISTA$[$@NIVEL], $@NOME$;
	set $BOLSAS[$@NIVEL], 0;
	goto L_limpar;

L_limpar:
	set $@BORAM_STATUS, 0;
	killmonster "005-1.gat", "Boram::OnMobDeath";
	attachrid($@BORAM_ID);
	warp "005-1.gat", 38, 32;
	set $@NIVEL, 0;
	set $@FIM, 0;
	set $@INICIO, 0;
	set $@NOME$, "";
	set $@BORAM_MORTES, 0;
	set $@BORAM_ID, 0;
	setnpctimer 0;
	stopnpctimer;
	end;

onInit:
	initnpctimer;
	stopnpctimer;
	end;

OnTimer5000:
	setnpctimer 0;
	attachrid($@BORAM_ID);
	if (PC_DIE_COUNTER > $@BORAM_MORTES) goto L_limpar;
	end;

L_sem_grana:
	mes "[Boram]";
	mes "\"Ops, parece que você não tem dinheiro para pagar pelos meus serviços...\"";
	close;

L_aguardar:
	mes "[Boram]";
	mes "\"Aguarde um momento, há uma luta em andamento...\"";
	close;

L_menugm:
	mes "\"Há uma luta em andamento. Se o jogador estiver abusando das condições do script, fazendo os outros esperar, etc, utilize a opção abaixo. Use esta opção com responsabilidade!\"";
	menu
		"Retirar o jogador.", -,
		"Sair.", -;
	if (@menu == 1) goto L_limpar;
	close;

L_fim:
	close;
}
