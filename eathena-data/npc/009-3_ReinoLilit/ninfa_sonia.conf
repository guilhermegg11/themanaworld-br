//--------------- Clicar no NPC Pescador Gismo ----------------------------
// PROGRAMADOR: Indigovox<rui.gravata@hotmail.com>
// NPC: Ninfa Sônia
// Conjunto: NPC [Nº3/5] necessário para concluir a sequancia de quest do Colar Horcrux.
// Pede: 50x couros de cobra da Montanha[ID641] + 100x Casulos de Bicho-da-Seda[ID718] + 50KGP
// Pré-requisitos: Leva uma hora para costurar.
// Prêmio: Costura uma Couraça de Pele de Cobra da Montanha(ID3026).
// CONFIGURAÇÕES: O [ID3026] de item é falso, pois o verdadeiro item do prẽmio ainda não existe.
//-------------------------------------------------------------------------

//009-3.gat,138,48,0	script	debug	316,{
//	mes "[Sex:"+ Sex +"] [Necro:"+ SeCacadorDeNecromantes +"]";
//	mes "[Cap:"+ NinfaSonia_capitulo +"] [Temp:"+ NinfaSonia_tempo +"] [$:"+ zeny +"]";
//	menu
//		"Cap 0", -,
//		"Cap 1", -,
//		"Cap 2", -,
//		"Cap 3", -,
//		"Cap 4", -,
//		"setTime", _setTime,
//		"setCacadorDeNecromantes", _setNecro,
//		"fechar.", -;
//
//	if (@menu>=1 && @menu<=5)
//		set NinfaSonia_capitulo, @menu-1;
//	close;
//
//_setTime:
//	callfunc "getTime";
//	set NinfaSonia_tempo, @time;
//	close;
//
//_setNecro:
//	set SeCacadorDeNecromantes, 1;
//	close;
//}

009-3.gat,141,48,0	script	Sonia	316,{
	if(Sex==1) set @SexForasteirx$, "forasteiro";
	if(Sex!=1) set @SexForasteirx$, "forasteira";

	if(SeCacadorDeNecromantes!=1) goto _comprimenta;
	if(NinfaSonia_capitulo==1) goto _retorno;
	if(NinfaSonia_capitulo==2) goto _buscar;

	if(NinfaSonia_capitulo==3 && zeny<50000)  goto _sem_grana_novamente;
	if(NinfaSonia_capitulo==3 && zeny>=50000) goto _comgrana;

	if(NinfaSonia_capitulo==4) goto _comprimenta_conhecendo;
	goto _inicio;

//-------------------------------------------------------------------------
_comprimenta:
	mes "[Ninfa Sônia]";
	mes "\"Olá "+ @SexForasteirx$ +"!\"";
close;
//-------------------------------------------------------------------------
_comprimenta_conhecendo:
	mes "[Ninfa Sônia]";
	mes "\"Olá "+ strcharinfo(0) +"!\"";
	next;
	mes "[Ninfa Sônia]";
	mes "\"Quer fazer outra Couraça de Pele de Cobra?\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	menu
		"=D Sim! Preciso de outra.", _recomeco,
		"¬¬ Não me interessa no momento.", - ;
	mes "[Ninfa Sônia]";
	mes "\"Ok! Então, até próxima!\"";
	close;
//-------------------------------------------------------------------------

_recomeco:
	set NinfaSonia_capitulo, 1;
	goto _selecao;
close;

//-------------------------------------------------------------------------
_inicio:
	mes "[Ninfa Sônia]";
	mes "\"Olá "+ @SexForasteirx$ +"!";
	mes "Posso te ajudar de alguma forma?\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "\"Meu nome não é \""+ @SexForasteirx$ +"\"! Eu me chamo \""+ strcharinfo(0) +"\"!\"";
	next;
	mes "[Ninfa Sônia]";
	mes "\"Ok, "+ strcharinfo(0) +"!";
	mes "Menu nome é Sônia.";
	mes "Você quer que eu te faça alguma roupa?";
	mes "Sei fazer ótimas roupas de couro.\"";
	next;
	menu
		"Se for igual a sua pode esquecer!", -,
		"Pode me fazer um [Couraça de Pele de Cobra]?", _queroCouraca,
		"Roupas de couro não me interessam.", -;
	close;

_queroCouraca:
	mes "[Ninfa Sônia]";
	mes "\"Sei sim! Me traga esta listinha de itens com 25.000GP, que eu te darei a melhor couraça que você já viu na face de Mana.\"";
	next;
	mes "[LISTA PARA A COURAÇA]";
	mes "* 100 Casulos de Bicho-da-Seda";
	mes "* 050 Couros de Cobra da Montanha";
	mes "[PREÇO: 50KGP]";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "O.o Caramba! Tudo isso?";
	next;
	mes "[Ninfa Sônia]";
	mes "\"Item raros são feitos de materiais difíceis de se conseguir.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "Ok Sônia! Então vou buscar esses itens...";
	set NinfaSonia_capitulo, 1;
close;
//-------------------------------------------------------------------------
_retorno:
	mes "[Ninfa Sônia]";
	mes "\"Olá "+ strcharinfo(0) +"!\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "Olá Sônia!";
	next;
_selecao:
	mes "["+ strcharinfo(0) +"]";
	menu
		"=S Me mostra novamente a lista de itens.", _lista,
		"=D Eu já consegui todos os itens.", _checaitens,
		"¬¬ Mas Deixe para depois, então!", -;
close;
//-------------------------------------------------------------------------
_lista:
	mes "[LISTA PARA A COURAÇA]";
	mes "+ 100 Casulos de Bicho-da-Seda";
	mes "+ 050 Couros de Cobra da Montanha";
	mes "[PREÇO: 50KGP]";
	next;
goto _selecao;
//-------------------------------------------------------------------------
_checaitens:
//     641=couros de cobra da Montanha    ||    718=Casulos de Bicho-da-Seda
	if (countitem(718)<100) goto _sem_casulo;
	if (countitem(641)<50) goto _sem_couro;
	mes "[Ninfa Sônia]";
	mes "\"Deixe eu ver!\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "Tome!";
	next;
	mes "[Ninfa Sônia]";
	mes "\"É realmente você tem tudo o que precisa.\"";
	next;
	mes "[Ninfa Sônia]";
	mes "\"Vai querer deixar comigo para fazer sua Couraça?\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	menu
		"=D Deixo sim!", _deixo,
		"=S Não, agora não!", -;

	mes "[Ninfa Sônia]";
	mes "\"Ok! Então até mais!\"";
close;

//-------------------------------------------------------------------------
_deixo:
	if (countitem(718)<100) goto _sem_casulo; //- garantia q o jogador não retirou os itens
	if (countitem(641)<50) goto _sem_couro;   //- garantia q o jogador não retirou os itens
	set NinfaSonia_tempo, gettimetick(2);
	set NinfaSonia_capitulo, 2;
	delitem 641, 50;
	delitem 718, 100;
	// 641=couros de cobra da Montanha
	// 718=Casulos de Bicho-da-Seda
	mes "[Ninfa Sônia]";
	mes "\"Ok! Volte daqui a uma hora!\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "O.o Daqui a uma hora ainda?";
	next;
	mes "[Ninfa Sônia]";
	mes "\"Sim! Ou vc quer que eu faça de qualquer jeito e te entregue?\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "Não! Pode deixar eu espero.";
	mes "Atá mais!";
close;
//-------------------------------------------------------------------------
_buscar:
	//mes @time +" - "+ NinfaSonia_tempo +" = "+ (@time-NinfaSonia_tempo);
	//mes @time +" - "+ NinfaSonia_tempo +" = "+ (@time-NinfaSonia_tempo);
	if (gettimetick(2) > NinfaSonia_tempo + (60*60)) goto _feito;
	mes "["+ strcharinfo(0) +"]";
	mes "Olá Sônia!";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "Fez minha Couraça?";
	next;
	mes "[Ninfa Sônia]";
	mes "\"Claro que não!\"";
	next;
	mes "[Ninfa Sônia]";
	mes "\"Volte daqui a "+ (((NinfaSonia_tempo + (60*60))-gettimetick(2))/60) +" minutos que eu te entrego!\"";
	next;
	mes "[Ninfa Sônia]";
	mes "\"Mas não se esqueça de meu pagamento!\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "Ok! Eu trago o dinheiro!";
close;
//-------------------------------------------------------------------------
_feito:
	mes "["+ strcharinfo(0) +"]";
	mes "Sônia, eu não acredito que você ainda não fez minha couraça?!";
	next;
	mes "[Ninfa Sônia]";
	mes "\"Caaaaaauma "+ strcharinfo(0) +"!";
	mes "Eu terminei sua couraça.\"";
	next;
_comgrana:
	mes "[Ninfa Sônia]";
	mes "\"Você trouxe o pagamento?\"";
	next;
	set NinfaSonia_capitulo, 3;
	if(zeny<50000) goto _sem_grana;
	mes "["+ strcharinfo(0) +"]";
	mes "Ok! Está aqui!";
	next;
	mes "[Ninfa Sônia]";
	mes "\"Certo, aqui está sua couraça.\"";
	getitem 3026, 1;
	set zeny, zeny - 50000;
	set NinfaSonia_capitulo, 4;
	set NinfaSonia_tempo, 0;
	next;
	mes "[Ninfa Sônia]";
	mes "=] \"Foi bom fazer negócio contigo!\"";
close;
//-------------------------------------------------------------------------
_sem_casulo:
	mes "[Ninfa Sônia]";
	mes "\"Deixe eu ver!\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "Tome!";
	next;
	mes "[Ninfa Sônia]";
	mes "=S \"Você não tem \"Casulos de Bicho-da-Seda\" suficientes.";
	mes "Está faltando "+ (100-countitem(718)) +" casulos.\"";
	next;
	mes "[Ninfa Sônia]";
	mes "\"Volte quando tiver encontrado todos os 100!\"";
close;
//-------------------------------------------------------------------------
_sem_couro:
	mes "[Ninfa Sônia]";
	mes "\"Deixe eu ver!\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "Tome!";
	next;
	mes "[Ninfa Sônia]";
	mes "=S \"Você não tem \"Couros de cobra da Montanha\" suficientes.";
	mes "Está faltando "+ (50-countitem(641)) +" couros.\"";
	next;
	mes "[Ninfa Sônia]";
	mes "\"Volte quando tiver encontrado todos os 50!\"";
close;
//-------------------------------------------------------------------------
_sem_grana_novamente:
	mes "["+ strcharinfo(0) +"]";
	mes "Olá Sônia!";
	next;
	mes "[Ninfa Sônia]";
	mes "\"Trousse meu 50kGP de pagamento, "+ strcharinfo(0) +"?\"";
	next;
_sem_grana:
	mes "["+ strcharinfo(0) +"]";
	mes "Eu só tenho "+ zeny +"GP, serve?";
	next;
	mes "[Ninfa Sônia]";
	mes "\"Eu não acredito, que você tem a petulância de vir pegar esta couraça, sem antes conseguir meu pagamento!\"";
	next;
	mes "[Ninfa Sônia]";
	mes "\"Vá buscar! Depois eu dou sua couraça!\"";
close;
//-------------------------------------------------------------------------

}
