//Criado por: André Caraciolo <andrecaraciolo@gmail.com>
// Revisado por: Lunovox Heavenfinder <rui.gravata@gmail.com>
// Importante principalmente por causa das pessoas na conversa.

hali001.gat,171,152,0	script	#Auron	300,7,7,{ 
//npctalk Reino+"←=→"+$PosseDeFroz;
if(Reino!=$PosseDeHalicarnazo) goto _Raiva;
if(BaseLevel < 40 || QUEST_ArmaduraDeTerranite >= 2) end;
	emotion 13; // Emote de quest!!! XD 
end;

_Raiva:
	emotion 11; // Emote de Raiva pelo invasor!!! XD 
	heal -(MaxHp/10),0; // NPC Ataca o invasor tirando 10% de HP!!
end;

}

//###################################################################################

hali001.gat,171,153,0	script	Auron	600,1,1,{
	set @Carvao, 1988;
	set @PetalaDeFogo, 3434;
	set @ArmaduraDeTerranite, 767;
	set @NomeDoReino$, "Halicarnazo";

	if(Reino!=$PosseDeHalicarnazo) goto _SugestaoAoInvasor;
	if(BaseLevel < 40) goto _SemNivel;
	if(QUEST_ArmaduraDeTerranite==0) goto _inicio;
	if(QUEST_ArmaduraDeTerranite==1) goto _Retorno;
	mes "[Auron]";
	mes "Está gostando da [Armadura de Terranite] que eu te dei?";
close;

_SugestaoAoInvasor:
	heal -(MaxHp/10),0;
	set @tmp,rand(4);
	if(@tmp == 0) npctalk "Saia daqui seu invasor de "+ @NomeDoReino$+"!";
	if(@tmp == 1) npctalk "Você não pode entrar em "+ @NomeDoReino$+"!";
	if(@tmp == 2) npctalk "Como você se atreve a tentar machucar as pessoas do Reino de "+ @NomeDoReino$+"?";
	if(@tmp == 3) npctalk "Eu te mostrarei do que o povo de "+ @NomeDoReino$+" é feito.";
end;

_SemNivel:
	mes "[Malet]";
	mes "\"Olá, como vai?\"";
close;

_inicio:
	mes "[Auron]";
	if(sex==1) mes "\"Olá Guerreiro!\"";
	if(sex==0) mes "\"Olá Guerreira!\"";
	next;
	mes "["+strcharinfo(0)+"]";
	mes "=) \"Olá!\"";
	next;
	mes "[Auron]";
	mes "\"Eu estou tentando fazer um Lamparinas Mágicas, e preciso de alguem para me ajudar aconseguir os materiais.\"";
	next;
	mes "[Auron]";
	mes "\"Você precisa de alguma coisa? Eu posso lhe ajudar a conseguir...\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"Não preciso de Nada.", -,
	"Eu preciso de uma armadura.", _Acordo;
	mes "";
	mes "[Auron]";
	mes "\"Ok! Volte sempre!!!\"";
close;

_Acordo:
	mes "";
	mes "[Auron]";
	mes "\"Posso fazer uma [Armadura de Terranite] para vc.\"";
	next;
	mes "[Auron]";
	mes "Mas em troca que quero isto...\"";
	mes " * 30 [Carvão]";
	mes " * 10 [Pétala de Fogo]";
	mes " * 400.000 GP";
	next;
	mes "[Auron]";
	mes "\"Aceita a acordo?\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"Sim, aceito!", _Aceito,
	"Não obrigado!", -;
close;

_Aceito:
	set QUEST_ArmaduraDeTerranite, 1;
	mes "";
	mes "[Auron]";
	mes "\"Então estou te esperando ancioso.\"";
close;

_Retorno:
	getinventorylist;
	if(countitem(@Carvao)<30 || countitem(@PetalaDeFogo)<10 || Zeny < 400000) goto _FaltaAlgo;
	if(@inventorylist_count >= 100) goto _LoteAbarrotado;
	delitem @Carvao, 30;
	delitem @PetalaDeFogo, 10;
	set Zeny, Zeny - 400000;
	getitem @ArmaduraDeTerranite, 1;
	set QUEST_ArmaduraDeTerranite, 2;
	mes "";
	mes "[Auron]";
	mes "\"Aqui está sua [Armadura de Terranite]. Eu espero que você goste dela! Aproveite bem!\"";
close;

_FaltaAlgo:
	mes "";
	mes "[Auron]";
	mes "=( \"Você não tem tudo o que eu preciso.\"";
	next;
	mes "[Auron]";
	mes "Em troca da [Armadura de Terranite] quero isto para fazer minha Lamparinas Mágicas...\"";
	mes " * 30 [Carvão]";
	mes " * 10 [Pétala de Fogo]";
	mes " * 400.000 GP";
close;

_LoteAbarrotado:
	mes "";
	mes "[Auron]";
	mes "=( \"Não posso te dar a [Armadura de Terranite] por que você está com a muchila muito cheia.\"";
	next;
	mes "[Auron]";
	mes "=) \"Volte quando tiver guardado alguns de seus itens no banco, ou em algum baú de hotel.\"";
close;

}
