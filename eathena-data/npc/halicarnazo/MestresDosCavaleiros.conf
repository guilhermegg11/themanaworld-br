//Quest by Cardinalli since 2009 playing TMW
//Editado e corrigido por Sky


halicarnazo.gat,70,38,0	script	Victor	602, {
	set @AntenaLuminosa, 612;
	set @AntenaRosa, 614;
	set @GrimoriumCavaleiro, 3207;

	if(QUEST_MestreDosCavaleiros == 0 && BaseLevel < 40) goto L_semLevel;
	if(QUEST_MestreDosCavaleiros == 1) goto L_Retorno;
	if(QUEST_MestreDosCavaleiros == 2) goto L_FimQuest;
goto L_iniciaQuest;

L_semLevel:
	mes "[Victor]";
	mes "\"Olá.\"";
close;

L_iniciaQuest:
	mes "Você vê um nobre cavaleiro. Sua armadura emana uma aura muito forte.";
	next;
	mes "[Victor]";
	mes "\"Olá viajante. Deseja aprender as técnicas milenares da batalha corpo a corpo?\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"Parece interessante, quem é você?", L_Explicacao,
	"Não preciso de sua ajuda, sei me virar só.", -;
	mes "";
	mes "[Victor]";
	mes "\"Tudo bem então.\"";
close;

L_Explicacao:
	mes "[Victor]";
	mes "\"Eu sou Victor o Mestre dos Cavaleiros do Mundo de Mana.\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"Você poderia me aceitar como aprendiz?", L_Aprendiz,
	"Blá blá blá T.T", -;
close;

L_Aprendiz:
	mes "";
	mes "[Victor]";
	mes "\"Talvez. Primeiro saiba que se você escolher ser da classes dos cavaleiros não poderá aprender outra classes. Também preciso saber se você é realmente excepcional, e se sabe o que está fazendo.\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"Como posso te provar que serei excelente?", L_Provar,
	"Deixa pra lá.", -;
close;

L_Provar:
	mes "";
	mes "[Victor]";
	mes "\"Para provar seu esforço, preciso que você me traga alguns itens. Eles são: 50 antenas luminosas e 30 antenas rosas.\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"Tudo bem, já te trago.", L_Trago,
	"Ahh, esquece.", -;
close;

L_Trago:
	mes "";
	mes "[Victor]";
	mes "\"Se apresse.\"";
	set QUEST_MestreDosCavaleiros, 1;
close;

L_Retorno:
	mes "[Victor]";
	mes "\"Trouxe os itens que te pedi?\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"Sim, aqui estão.", -,
	"Não, Esqueci quais são", L_Esqueci;
	if(countitem(@AntenaLuminosa)<50 || countitem(@AntenaRosa)<30) goto L_Esqueci;
	delitem @AntenaLuminosa, 50;
	delitem @AntenaRosa, 30;
	getitem @GrimoriumCavaleiro, 1;
	setskill 400, 1;
	set QUEST_MestreDosCavaleiros, 2;
	mes "";
	mes "[Victor]";
	mes "\"Parabéns, aprendiz. Você provou ser capaz de se esforçar. Agora você pertence a classe dos Cavaleiros e das Amazonas.\"";
	next;
	mes "[Victor]";
	mes "\"Eu te dou de presente esse Grimórium Azul que é o livro para vc anotar os segredos guia dos Cavaleiros e das Amazonas.\"";
close;

L_Esqueci:
	mes "";
	mes "[Victor]";
	mes "\"Bem, você nao trouxe o que eu te pedi. Estes são os itens...\"";
	mes " * 50 Antena Luminosa";
	mes " * 30 Antena Rosa";
close;

L_FimQuest:
	mes "[Victor]";
	mes "\"Boa sorte aprendiz.\"";
	close;
}
