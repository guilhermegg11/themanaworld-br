/**
 * Quest da Passagem do Mapa do Tesouro. O Velho Eremita irá ensinar a
 * encontrar o segredo das passagens para se chegar ao mapa dos esqueletos.
 *
 * @date quarta, 12 de janeiro de 2011
 * @author Diogo_RBG - http://diogorbg.blogspot.com/
 */

//007-1.gat,93,30,0	script	debug	116,{
//	callfunc "debug";
//	menu
//		"Continuar", -,
//		"Resetar", -;
//	if(@menu==2) set QUEST_warpSecreto2, 0;
//	if(@menu==2) set QUEST_MASK1, QUEST_MASK1 & ~MASK1_WARP_SECRETO; //< Seta 0 no bit da quest
//	close;
//}

007-1.gat,95,30,0	script	Velho Eremita	116,{
	if(QUEST_MASK1 & MASK1_WARP_SECRETO) goto _obrigado; //< Quest Warp Secreto concluída. Estado 6.

	if(QUEST_warpSecreto2==1) goto _sigaErrado;
	if(QUEST_warpSecreto2==2) goto _caminhoCerto;
	if(QUEST_warpSecreto2==3) goto _examinarMapa3;
	if(QUEST_warpSecreto2==4) goto _sigaCerto;
	if(QUEST_warpSecreto2>=5) goto _maisInfo;

	mes "[Velho Eremita]";
	mes "\"Não existe ninguém que conheça melhor estas cavernas que eu.\"";
	if(countitem(3006)>0) goto _comMapa;
	close;

_comMapa:
	next;
	menu
		"Pode me ajudar com este mapa?", _ajudaComMapa,
		"(esse velho tá se achando ¬¬)", -;
	mes "["+ strcharinfo(0) +"]";
	mes "> \"Ele conhece tanto as cavernas que até hoje não conseguiu sair dela! KKK!!!\"";
	close;

_ajudaComMapa:
	mes "[Velho Eremita]";
	mes "\"Sim, eu posso... ";
	mes "Estas são as minhas cavernas e não há nada que eu não saiba sobre elas!\"";
	next;
	if(countitem(3006)>0) goto _comMapa2;
	mes "[Velho Eremita]";
	mes "\"Você não tem um mapa!";
	mes "Só está brincando com este velho bobo.\"";
	close;

_comMapa2:
	mes "[Velho Eremita]";
	mes "\"Deixe-me ver este mapa...\"";
	mes "";
	mes "Então o velho toma o mapa de suas mãos.";
	delitem 3006, 1; //Mapa do Tesouro
	set QUEST_warpSecreto2, 1;
	next;
	mes "O velho olha para o mapa e faz uma cara de confuso. Ele parece não estar entendendo o mapa.";
	next;
	mes "[Velho Eremita]";
	mes "\"Há algo de errado com este mapa. Estas passagens não se ligam desta forma.";
	mes "Mas não tem problema, porque elas não interferem em nada.\"";
	next;
	mes "[Velho Eremita]";
	mes "\"Para chegar até este local marcado com um X basta seguir pela passagem da cachoeira. Ela fica logo depois da travessia do rio.\"";
	next;
	mes "[Velho Eremita]";
	mes "\"E leve o mapa com você. Pois você pode precisar dele.";
	mes "Boa sorte.\"";
	getitem 3006, 1; //Mapa do Tesouro
	close;

_sigaErrado:
	mes "[Velho Eremita]";
	mes "\"É como eu te disse. Basta seguir pela passagem da cachoeira. Ela fica logo depois da travessia do rio.\"";
	close;

_caminhoCerto:
	mes "[Velho Eremita]";
	mes "\"Você conseguiu encontrar o caminho?\"";
	next;
	menu
		"NÃO!", -,
		"Não mesmo!", -,
		"Você mal conhece estas cavernas.", -,
		"Você me mandou pelo caminho errado.", -;
	mes "[Velho Eremita]";
	mes "\"Calma, não se zangue!";
	mes "Meu erro foi confiar neste mapa... ele com certeza tem algum problema.";
	mes "Deixe-me vê-lo novamente.\"";
	next;
	menu
		"Só mais esta vez!", _examinarMapa,
		"Já teve sua chance.", -;
	mes "[Velho Eremita]";
	mes "\"Você deveria ter mais confiança nos mais velhos.\"";
	close;

_examinarMapa:
	if(countitem(3006)>0) goto _examinarMapa2;
	mes "[Velho Eremita]";
	mes "\"¬¬ Você não está com o mapa.\"";
	close;

_examinarMapa2:
	delitem 3006, 1; //Mapa do Tesouro
	set QUEST_warpSecreto2, 3;
_examinarMapa3:
	mes "O velho leva o seu mapa para bem próximo de uma tocha para poder examiná-lo melhor.";
	next;
	mes "[Velho Eremita]";
	mes "\"Estas passagens estão erradas! Qual o problema deste mapa?!\"";
	next;
	mes "De repente o mapa começa a se escurecer com o calor da chama. E curiosamente começam a aparecer novos desenhos no mapa!";
	next;
	mes "[Velho Eremita]";
	mes "\"Então é isso! O mapa escondia seus segredos em desenhos escritos com ácido. Uma técnica milenar... mas muito útil.\"";
	next;
	mes "O velho se descuida por um segundo e o mapa começa a pegar fogo!";
	mes "Ele tenta apagar o fogo, mas fora em vão!";
	next;
	mes "[Velho Eremita]";
	mes "\"Tenho uma boa e má notícia.";
	mes "A boa é que agora eu sei o caminho correto.";
	mes "A má é que você terá que se guiar sem o mapa.\"";
	next;
	menu
		"Tudo bem! Só preciso do caminho.", -,
		"Só me diga por onde seguir.", -;
	mes "[Velho Eremita]";
	mes "\"* Primeiro entre pela passagem da cachoeira.";
	mes "* Depois por uma passagem ao norte... ninguém costuma passar por ela.";
	mes "* E depois disso siga pela passagem da esquerda até chegar em um grande salão.\"";
	set QUEST_warpSecreto2, 4;
	next;
	mes "[Velho Eremita]";
	mes "\"A partir daí é fácil chegar até aquele local marcado com um X.";
	mes "Boa sorte!\"";
	close;

_sigaCerto:
	mes "[Velho Eremita]";
	mes "\"Agora eu tenho certeza:";
	mes "\"* Primeiro entre pela passagem da cachoeira.";
	mes "* Depois por uma passagem ao norte... ninguém costuma passar por ela.";
	mes "* E depois disso siga pela passagem da esquerda até chegar em um grande salão.\"";
	next;
	mes "[Velho Eremita]";
	mes "\"Não temos mais o mapa, mas a partir do salão é fácil prosseguir.";
	mes "Boa sorte!\"";
	close;

_maisInfo:
	mes "[Velho Eremita]";
	mes "\"Então. Você conseguiu encontrar a passagem?\"";
	next;
	menu
		"Sim, dessa vez eu consegui!", -;
	mes "["+ strcharinfo(0) +"]";
	mes "> \"Cheguei no salão que você me disse e continuei caminhando. Entrei por mais 3 passagens e encontrei uma caverna muito perigosa cheia de esqueletos. Oo Mal pude acreditar!\"";
	next;
	mes "[Velho Eremita]";
	mes "\"Você não viu nada. Este é só o começo da caverna.";
	mes "Em seu interior você irá encontrar a Caverna da Bruxa. Caverna que fica de frente para o Poço Sem Fundo.\"";
	next;
	mes "[Velho Eremita]";
	mes "\"Essa parte das cavernas é realmente muito perigosa. Só me aventurei por lá algumas raras vezes.";
	mes "Muito dos que se aventuraram por elas encontraram a Morte!\"";
	set QUEST_warpSecreto2, 0;
	set QUEST_MASK1, QUEST_MASK1 | MASK1_WARP_SECRETO;
	close;

_obrigado:
	mes "[Velho Eremita]";
	mes "\"Boa sorte em sua aventura!\"";
	close;

}
