//quest MESTRE NECROMANTE por Sky


halicarnazo.gat,74,38,0	script	Hadet	602,{
	set @AntenaLuminosa, 612;
	set @AntenaRosa, 614;
	set @GrimoriumNecromante, 3209;
	set @ClasseCavaleiro, 400;
	set @ClasseDruida, 401;
	set @ClasseMago, 402;
	set @ClasseNinja, 403;
	set @ClassePaladino, 404;
	set @ClasseNecromante, 405;

	if(getskilllv(@ClasseNecromante)==0 && (QUEST_Classe <2 || BaseLevel < 40)) goto L_SemNivel;
	if(getskilllv(@ClasseNecromante)==0 && QUEST_Classe == 3) goto L_Retorno;
	if(getskilllv(@ClasseNecromante)==0 && QUEST_Classe == 4) goto L_JaDoutraClasse;
	if(getskilllv(@ClasseNecromante)>=1) goto L_JaDestaClasse;
goto L_iniciaQuest;

L_JaDoutraClasse:
	mes "[Hadet]";
	mes "\"Agora você já pertence a classe...\"";
	callfunc "GerarNomeDaPatente",@ClasseCavaleiro; 
	if(@NomeDaPatente$ != "") mes " * " + @NomeDaPatente$;
	callfunc "GerarNomeDaPatente",@ClasseDruida; 
	if(@NomeDaPatente$ != "") mes " * " + @NomeDaPatente$;
	callfunc "GerarNomeDaPatente",@ClasseMago; 
	if(@NomeDaPatente$ != "") mes " * " + @NomeDaPatente$;
	callfunc "GerarNomeDaPatente",@ClasseNinja; 
	if(@NomeDaPatente$ != "") mes " * " + @NomeDaPatente$;
	callfunc "GerarNomeDaPatente",@ClassePaladino; 
	if(@NomeDaPatente$ != "") mes " * " + @NomeDaPatente$;
	callfunc "GerarNomeDaPatente",@ClasseNecromante; 
	if(@NomeDaPatente$ != "") mes " * " + @NomeDaPatente$;
close;

L_JaDestaClasse:
	callfunc "GerarNomeDaPatente",@ClasseNecromante; 
	mes "[Hadet]";
	if(Sex==0)mes "\"Meus parabéns! Agora você é uma "+@NomeDaPatente$+"!\"";
	if(Sex==1)mes "\"Meus parabéns! Agora você é um "+@NomeDaPatente$+"!\"";
close;

L_SemNivel:
	mes "[Hadet]";
	mes "\"Olá viajante.\"";
close;

L_iniciaQuest:
	mes "Você vê um homem com um manto preto, emamando uma aura sombria.";
	next;
	mes "["+strcharinfo(0)+"]";
	mes "(^_^) \"O senhor é o Mestre dos Necromantes?\"";
	next;
	mes "[Hadet]";
	mes "(^_^) \"Sou. Meu nome é Hadet, Mestre dos Necromantes do Mundo de Mana.\"";
	next;
	mes "["+strcharinfo(0)+"]";
	if(Sex==0) mes "(^_^) \"O Mestre Hadet poderia me ensinar a ser uma Necromante?\"";
	if(Sex==1) mes "(^_^) \"O Mestre Hadet poderia me ensinar a ser um Necromante?\"";
	next;
	mes "[Hadet]";
	mes "(^_^) \"Mas é claro...";
	next;
	mes "[Hadet]";
	mes "(T_T) ...que não.\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"('-') Por favor, belo mestre! O senhor gostará de minha ajuda!", -,
	"(T.T) Se não me aceitar, digo que o senhor colocou meleca do nariz no copo da princesa!", -,
	"(X.x) Preciso ser aceito ou levarei uma surra de minha mamãe!", -,
	"(-_-) Posso prová-lo que serei um excelente aprendiz!", L_Provar,
	"(^-^) Faço qualquer coisa se me aceitar! Exceto aos domingos, claro!", -,
	"(^-^) Tenho certeza um homem tão bacana como o senhor irá me aceitar! ", -,
	"(¬.¬) Não preciso de sua ajuda. Eu nem queria mesmo!", -;
	set @Resp,rand(7);
	mes "";
	mes "[Hadet]";
	if(@Resp==0) mes "(^-^) \"Tenho certeza absoluta que nunca ensinarei a alguem como você!\"";
	if(@Resp==1) mes "(T_T) \"Você uma pessoa tãaaaaaaaao esquisiiiiiita! Sai fora!\"";
	if(@Resp==2 && Sex==0) mes "(T_T) \"Você é tão feia que eu nunca encontrará um marido!\"";
	if(@Resp==2 && Sex==1) mes "(T_T) \"Você uma pessoa tão feia que nem um tufo idiota iria querem vc!\"";
	if(@Resp==3) mes "(¬_¬)==d \"Estou ocupado! Fale com minha mão!\"";
	if(@Resp==4) mes "('.') \"Ai meu Deus é cada peça que me aparece!\"";
	if(@Resp==5 && Sex==0) mes "(T_T) \"Vá perder alguns quilos e depois volte! Baleia!\"";
	if(@Resp==5 && Sex==1) mes "(¬_¬) \"Vc se entrigou da balança? Foi? Rabujo!\"";
	if(@Resp==6) mes "(-_-) \"Vc é uma pessoa tão inutil que não serviria nem como aprendiz de peso de papel!\"";
close;

L_Provar:
	mes "";
	mes "[Hadet]";
	mes "(^_^) \"Isso sim me parece interessante!\"";
	next;
	mes "[Hadet]";
	mes "(T_T) \"Mas Para provar seu valor, preciso que você me traga alguns itens.\"";
	next;
	mes "[Hadet]";
	mes "(¬.¬) \"Eles são: 50 antenas luminosas e 30 antenas rosas.\"";
	next;
	mes "[Hadet]";
	mes "(T_T) \"Mas antes saiba que se você escolher ser da classe dos Necromantes não poderá aprender outra classe depois. Também é preciso saber se você é realmente excepcional, e se sabe o que está fazendo.\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"(^-^) Como o senhor quiser! Eu já trago o que me foi ordenado.", L_Trago,
	"(T.T) Ahh, esquece! Desisto desta porcaria!", -;
close;

L_Trago:
	set QUEST_Classe, 3;
	mes "";
	mes "[Hadet]";
	mes "(¬.¬) \"Boa sorte Jamanta!\"";
close;

L_Retorno:
	mes "[Hadet]";
	mes "\"(^-^) Trouxe os itens que ordenei?\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"(^-^) Sim, aqui estão.", -,
	"(X.x) Não, Esqueci quais são!", L_Esqueci;
	if(countitem(@AntenaLuminosa)<50 || countitem(@AntenaRosa)<30) goto L_Esqueci;
	getinventorylist;
	if (@inventorylist_count >=100) goto L_semlugar;
	delitem @AntenaLuminosa, 50;
	delitem @AntenaRosa, 30;
	getitem @GrimoriumNecromante, 1;
	setskill @ClasseNecromante, 1;
	set QUEST_Classe, 4;
	mes "";
	mes "[Hadet]";
	mes "(^_^) \"Parabéns capach... digo aprendiz! Você provou ser capaz de se esforçar. Agora você é um aprendiz dos Necromantes.\"";
	next;
	mes "[Hadet]";
	mes "(^_^) \"Eu te dou de presente esse Grimorium Negro que é o livro para você anotar os segredos guia dos Necromantes.\"";
close;

L_PerdiItens:
	mes "["+strcharinfo(0)+"]";
	mes "(X.x) \"Opa! Acho q perdi alguns itens por aeee.\"";
	next;
L_Esqueci:
	mes "";
	mes "[Hadet]";
	mes "(¬_¬) \"Bem, você não trouxe o que eu te pedi.\"";
	next;
	mes "[Hadet]";
	mes "(T_T) \"Não esqueça que os itens são esses!";
	mes " * 50 Antenas Luminosa. [ID:612]";
	mes " * 30 Antenas Rosa. [ID:614]\"";
	next;
	mes "["+strcharinfo(0)+"]";
	mes "(X.x) \"Senhor! Sim senhor! Não esquecerei senhor! Ja volto senhor!\"";
close;

L_semlugar:
	mes "[Hadet]";
	mes "\"Sua bolsa está bem cheia não é? Libere espaço e depois volte para pegar sua recompensa.\"";
close;


}
