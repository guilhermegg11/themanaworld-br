//
// The pot where the player can deposit stuff for the cat.
//
// O pote no qual o jogador pode depositar itens para o gato.
// Traduzido e adaptado por: Sky!
//

org025-1.gat,37,29,0	script	pote	127, {

	if (Katze > 0)
		mes "É o mesmo pote velho.";
	if (Katze == 0)
		mes "É um pote.";
	next;

	if (Katze == 1 && KatzeBeenOutside == 1)
		goto L_NeedsFood;
	if (Katze == 1)
		goto L_HasMilk;
	if (Katze == 2 && KatzeBeenOutside == 1)
		goto L_NeedsFur;
	if (Katze == 2)
		goto L_HasFood;
	if (Katze == 3)
		goto L_NeedsWood;
	if (Katze == 4 && KatzeBeenOutside == 0)
		goto L_HasWood;
	if (Katze >= 4)
		goto L_Finished;

L_NeedsMilk:
	if (countitem("527") > 0)
		menu
			"Colocar um pouco de leite", L_GiveMilk,
			"Deixar pra lá", -;
	close;

L_GiveMilk:
	delitem "527", 1;
	set Katze, 1;
	set KatzeBeenOutside, 0;
	close;

L_HasMilk:
	mes "Tem leite dentro do pote.";
	close;

L_NeedsFood:
	mes "O leite sumiu!";
	next;

	if (countitem("562") > 0 && countitem("676") > 0)
		menu
			"Colocar uma Coxa de Frango dentro", L_GiveChicken,
			"Colocar um Filé dentro", L_GiveSteak,
			"Deixar pra lá", -;
	if (countitem("562") > 0 && countitem("676") == 0)
		menu
			"Colocar uma Coxa de Frango dentro", L_GiveChicken,
			"Deixar pra lá", -;
	if (countitem("562") == 0 && countitem("676") > 0)
		menu
			"Colocar um Filé dentro", L_GiveSteak,
			"Deixar pra lá", -;
	close;

L_GiveChicken:
	delitem "562", 1;
	set Katze, 2;
	set KatzeBeenOutside, 0;
	close;

L_GiveSteak:
	delitem "676", 1;
	set Katze, 2;
	set KatzeBeenOutside, 0;
	close;

L_HasFood:
	mes "Há um pouco de comida dentro.";
	close;

L_NeedsFur:
	mes "E está vazio!";
	next;

	if (countitem("611") > 0)
		menu
			"Por um pelo branco perto do pote", L_GiveFur,
			"Deixar pra lá", -;
	close;

L_GiveFur:
	mes "Você coloca o pelo perto do pote, mas a gata não parece reagir. Talvez há algo mais que você possa fazer. Você pega o pelo de volta.";
	close;

L_NeedsWood:
	if (countitem("569") > 0)
		menu
			"Por um toco de madeira perto do pote", L_GiveWood,
			"Deixar pra lá.", -;
	close;

L_GiveWood:
	delitem "569", 1;
	set Katze, 4;
	set KatzeBeenOutside, 0;
	mes "Você coloca o toco perto do pote. A gata olha com atenção, mas continua em seu lugar.";
	close;

L_HasWood:
	mes "Um toco de madeira está ao lado do pote.";
	close;

L_Finished:
	close;

}
