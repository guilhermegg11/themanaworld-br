/**
 * Quest da Poção para Queimaduras.
 * Quest destinada aos leveis iniciais, sem vínculo com nenhuma historia
 * do jogo, apenas para passar o tempo e ganhar experiência.
 *
 * @date quarta, 11 de janeiro de 2012
 * @author King
 */

//001b.gat,77,88,0	script	debug	384,{
//	callfunc "debug";
//	mes "QUEST_pocaoQueimadura: "+QUEST_pocaoQueimadura;
//	menu
//		"set QUEST_pocaoQueimadura, 0", -,
//		"set _perguntarPocao1, 1", -,
//		"set _checarItens1, 2", -,
//		"set _receberPocao, 3", -,
//		"set _vaLogo, 4", -,
//		"10 escopioes", _escorpiao,
//		"10 tartarugas", _tartaruga,
//		"set lvl18 + 1.380 XP", _setlvl,
//		"Sair", _close;
//	set QUEST_pocaoQueimadura, @menu-1;
//	close;
//
//_escorpiao:
//	set MOBS_queimaduraEscorpiao, MOBS_queimaduraEscorpiao+10;
//	mes "[Missão: Poção para Queimadura]";
//	mes "* Monstros: " +MOBS_queimaduraEscorpiao+ "/20 Escorpiões";
//	mes "* Monstros: " +MOBS_queimaduraTartaruga+ "/20 Tartarugas";
//	close;
//
//_tartaruga:
//	set MOBS_queimaduraTartaruga, MOBS_queimaduraTartaruga+10;
//	mes "[Missão: Poção para Queimadura]";
//	mes "* Monstros: " +MOBS_queimaduraEscorpiao+ "/20 Escorpiões";
//	mes "* Monstros: " +MOBS_queimaduraTartaruga+ "/20 Tartarugas";
//	close;
//
//_setlvl:
//	set BaseLevel, 18;
//	set BaseExp, BaseExp+1380;
//	close;
//
//_close:
//	close;
//}

001b.gat,80,88,0	script	Flora	384,{
	setarray @obrigado$, "Obrigada", "Obrigado";

	if (BaseLevel<18) goto _falas;

	if (QUEST_pocaoQueimadura==0) goto _falas;
	if (QUEST_pocaoQueimadura==1) goto _perguntarPocao1;
	if (QUEST_pocaoQueimadura==2) goto _checarItens1;
	if (QUEST_pocaoQueimadura==3) goto _receberPocao;
	if (QUEST_pocaoQueimadura<=5) goto _vaLogo;
	goto _falas;

_falas:
	mes "[Flora]";
	mes "Você encontra com uma jovem senhora em um profundo estado de meditação e não resiste em fazer uma pergunta.";
	next;
	mes "["+strcharinfo(0)+"]";
	mes "\"O que a senhora está fazendo?\"";
	next;
	mes "[Flora]";
	mes "\"Olá jovem. Estou apenas colhendo algumas ervas medicinais e também meditando um pouco.\"";
	next;
	mes "[Flora]";
	mes "\"Eu sou boticária da Vila dos Pescadores. Faço poções de todos os tipos utilizando ervas medicinais.";
	mes "E este local possui muitas das ervas que utilizo em minhas poções.\"";
	next;
	mes "["+strcharinfo(0)+"]";
	mes "\"Entendo. "+@obrigado$[sex]+" pela informação.\"";
	close;

_perguntarPocao1:
	mes "[Flora]";
	mes "Você encontra uma jovem senhora que parece estar colhendo ervas na encosta da montanha.";
	mes "Esta pode ser Flora.";
	next;
	mes "["+strcharinfo(0)+"]";
	mes "\"A senhora é Flora? A boticária da Vila dos Pescadores?\"";
	next;
	mes "[Flora]";
	mes "\"Sim, sou eu. Está precisando de alguma poção?\"";
	next;
	mes "["+strcharinfo(0)+"]";
	mes "\"Sim, uma poção para queimaduras, você tem?\"";
	next;
	mes "[Flora]";
	mes "\"Logo essa... não tenho mais nenhuma, me desculpe...\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
		"Tudo bem, tchau...", -,
		"Eu estou realmente precisando dela.", _perguntarPocao2;
	close;

_perguntarPocao2:
	mes "["+strcharinfo(0)+"]";
	mes "\"Uma amiga minha está morrendo de dor, não tem algum jeito?\"";
	next;
	mes "[Flora]";
	mes "\"Até tem um jeito sim... você pode pegar os ingredientes para que eu poça fazer a poção. E também pagar por meus serviços...\"";
	next;
	mes "["+strcharinfo(0)+"]";
	mes "\"Nossa, eu vou pegar tudo e você ainda vai cobrar... Aff...\"";
	next;
	mes "[Flora]";
	mes "\"Você disse que sua amiga está morrendo de dor. Então irá me ajudar com os ingredientes para aliviar sua dor o quanto antes.\"";
	next;
	mes "[Flora]";
	mes "\"E também, negócios são negócios... Mas irei cobrar apenas metade do valor.";
	mes "O que vai lhe custar apenas 1.500 GPs.\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
		"De jeito nenhum.", -,
		"Tudo bem, o que precisa?", _ingredientes,
		"Não tenho esta quantia.", -;
	close;

_checarItens1:
	mes "[Flora]";
	mes "\"Trouxe todos os itens para a poção?\"";
	menu
		"Sim. Todos.", _checarItens2,
		"Não. Pode repetir os itens?", _ingredientes;
	goto _ingredientes;

_checarItens2:
	set @cocoVerde, 3284;
	set @cascoTartaruga, 3403;
	set @peleTartaruga, 3402;
	set @ferraoEscorpiao, 507;
	set @gosmaVerme, 505;

	if (countitem(@cocoVerde)<1) goto _insuficiente1;
	if (countitem(@cascoTartaruga)<1) goto _insuficiente1;
	if (countitem(@peleTartaruga)<2) goto _insuficiente1;
	if (countitem(@ferraoEscorpiao)<2) goto _insuficiente1;
	if (countitem(@gosmaVerme)<5) goto _insuficiente1;
	if (zeny<1500) goto _insuficiente2;
	if (MOBS_queimaduraEscorpiao<20 || MOBS_queimaduraTartaruga<20) goto _insuficiente3;
	mes "[Flora]";
	mes "\"Isso mesmo, está tudinho aqui... Agora sim poderei fazer a poção.\"";
	delitem @cocoVerde, 1;
	delitem @cascoTartaruga, 1;
	delitem @peleTartaruga, 2;
	delitem @ferraoEscorpiao, 2;
	delitem @gosmaVerme, 5;
	set zeny, zeny-1500;
	set QUEST_pocaoQueimadura, 3;
	next;
	mes "[Flora]";
	mes "\"Agora espere enquanto preparo a poção... é rapidinho.\"";
	next;
	mes "["+strcharinfo(0)+"]";
	mes "\"Está bem, estarei aqui esperando.\"";
	close;

_insuficiente1:
	mes "[Flora]";
	mes "\"Você não tem todos os ingredientes para a minha poção.\"";
	next;
	goto _ingredientes;

_ingredientes:
	mes "[Flora]";
	mes "\"Para essa poção vou precisar de 1 [Coco Verde], 1 [Casco de Tartaruga], 2 pedacinhos de [Pele de Tartaruga], 2 [Ferrões de Escorpião] e 5 [Gosmas de Verme].\"";
	next;
	mes "[Flora]";
	mes "\"Estou te esperando, traga tudo e mais 1.500 GPs.\"";
	mes "";
	callsub _missaoPocaoQueimadura;
	close;

_insuficiente2:
	mes "[Flora]";
	mes "\"O combinado era de 1.500 GPs. E você não tem esta quantia.";
	mes "Estarei esperando que me traga toda a quantia para que eu poça fazer a poção.\"";
	mes "";
	callsub _missaoPocaoQueimadura;
	close;

_insuficiente3:
	mes "A quantidade de monstros é insuficiente.";
	mes "";
	callsub _missaoPocaoQueimadura;
	close;

//----------------------------------------------------------------------

_receberPocao:
	set @pocaoQueimadura, 3404;

	mes "["+strcharinfo(0)+"]";
	mes "\"E então?\"";
	next;
	getinventorylist;
	if( (@inventorylist_count + 1) >= 100) goto _cheio;
	mes "[Flora]";
	mes "\"A poção está pronta... Pegue aqui...\"";
	mes "";
	getitem @pocaoQueimadura, 1;
	set QUEST_pocaoQueimadura, 4;
	callsub _missaoCogumeloCompleta;
	next;
	mes "["+strcharinfo(0)+"]";
	mes "\"Muito obrigado, agora vou levar esta poção para minha amiga...\"";
	close;

_cheio:
	mes "[Flora]";
	mes "\"Você não tem espaço para a minha poção.";
	mes "Estarei esperando você voltar com mais espaço.\"";
	close;

//----------------------------------------------------------------------

_vaLogo:
	mes "[Flora]";
	mes "\"O que ainda faz aqui?!";
	mes "Leve logo a Poção de Queimadura para a sua amiga.\"";
	close;

//----------------------------------------------------------------------

_missaoPocaoQueimadura:
	if(QUEST_pocaoQueimadura==2) goto _pular;
		set QUEST_pocaoQueimadura, 2;
		set MOBS_queimaduraEscorpiao, 0;
		set @MOBS_queimaduraEscorpiao, 0;
		set MOBS_queimaduraTartaruga, 0;
		set @MOBS_queimaduraTartaruga, 0;
_pular:
	mes "[Missão: Poção para Queimadura]";
	mes "* Monstros: " +MOBS_queimaduraEscorpiao+ "/20 Escorpiões";
	mes "* Monstros: " +MOBS_queimaduraTartaruga+ "/20 Tartarugas";
	// Escorpião: 14XP, 14x20 = 2.80 XP
	// Tartaruga: 55XP, 14x20 = 1.100 XP
	//                        = 1.380 x 2 = 2.760 XP
	// RECOMPENSA >> 2.800 XP
	// LIMITADA PARA LVL 18.
	// FINALIZA COM LVL 20 e 92,5%
	return;

_missaoCogumeloCompleta:
	mes "* Você ganhou 2.800 pontos de experiência.";
	message strcharinfo(0), "Ganhei 2.800 pontos de experiência.";
	set BaseExp, BaseExp+2800;

	set QUEST_pocaoQueimadura, 4;
	set MOBS_queimaduraEscorpiao, 0;
	set @MOBS_queimaduraEscorpiao, 0;
	set MOBS_queimaduraTartaruga, 0;
	set @MOBS_queimaduraTartaruga, 0;
	return;

}
