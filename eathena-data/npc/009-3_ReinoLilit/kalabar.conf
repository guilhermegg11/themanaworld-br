//Quest Luva assassino por: Sky

009-3.gat,145,34,0	script	Kalabar	517, {
	set @CoxaDeFrango, 562; //-10UI
	set @OleoDeMonstro, 707; //-01UI
	set @AntidotoDeVeneno, 750; //-70UI
	set @LuvaDeAssassino, 756; //+01UI
	
	if (QUEST_Kalabar == 0 && BaseLevel < 95) goto L_semLevel;
	if (QUEST_Kalabar == 1) goto L_Retorno;
	if (QUEST_Kalabar == 2) goto L_Fim;
goto L_iniciaQuest;

L_semLevel:
	mes "[Kalabar]";
	mes "=] \"Oi!\"";
close;

L_iniciaQuest:
	mes "\"Você vê um garoto alegre.\"";
	next;
	mes "[Kalabar]";
	mes "\"Oi! Linda esta vista não é?\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu 
	"Não.", -, 
	"Quem é você?", L_Quem; //← menu usado de uma forma diferente!
	mes "";
	mes "[Kalabar]";
	mes "=[ \"Hum!\""; // ← Só se usa \" quando vai fazer o personagem falar algo!
close;

L_Quem:
	mes "[Kalabar]";
	mes "\"Eu sou Kalabar!\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"E o que você faz da vida?", L_Profissao,
	"Preciso ir agora.", -;
close;

L_Profissao:
	mes "[Kalabar]";
	mes "(>.>) \"Eu sou... um assassino! Mas não conte para ninguém, se não terei que te matar.\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"Você não parece ser capaz de machucar alguém u.u", L_Tbmsou,
	"*GULP* Tchauzinho", -;	
close;

L_Tbmsou:
	mes "[Kalabar]";
	mes "\"Não duvide de mim! Ei, quer me ajudar numa coisinha?\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"Que coisinha?", L_vouajudar,
	"Não posso.", -;
close;
	
L_vouajudar:
	mes "[Kalabar]";
	mes "\"Eu preciso de uns itens, meu chefe mandou eu vir buscá-los, mas está meio complicado... Se você me ajudar, eu te dou um presentinho!\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"Tudo bem, o que você precisa?", L_Oquequer,
	"Agora estou ocupado.", -;
close;

L_Oquequer:
	mes "[Kalabar]";
	mes "\"Eu já peguei uma parte, mas o que falta é: Um frasco de óleo de monstro, 70 poções antiveneno, e 10 coxas de frango.\"";
	next;
	mes "\"Traga-me 1 Óleo de Monstro, 70 poções antiveneno, e 10 coxas de frango.\"";
	next;
	mes "["+strcharinfo(0)+"]";
	mes "\"Já vou buscar.\""; // ← Se só tem uma opoção usa-se "mes".
	set QUEST_Kalabar, 1;
close;
	
L_Retorno:
	mes "[Kalabar]";
	mes "\"Trouxe os itens que eu te pedi?\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu 
	"Sim, aqui estão.", -,
	"Não, ainda não consegui =(", L_naotenho;
	if(countitem(@OleoDeMonstro)<1 || countitem(@AntidotoDeVeneno)<70 || countitem(@CoxaDeFrango)<10)  goto L_naotem;
	getinventorylist;
	if (@inventorylist_count >=100) goto L_semlugar;
	delitem @OleoDeMonstro, 1;
	delitem @AntidotoDeVeneno, 70;
	delitem @CoxaDeFrango, 10;
	getitem @LuvaDeAssassino, 1;
	set QUEST_Kalabar, 2;
	mes "[Kalabar]";
	mes "\"Valeuzão! Toma seu presentinho =P\"";
	next;
	mes "[Kalabar]";
	mes "\"Estas luvas te deixarão rápido como um foguete!\"";
close;

L_naotem:
	mes "[Kalabar]";
	mes "=[ \"Ei! Você não tem tudo. Pegue tudo e traga pra mim!\"";
close;

L_naotenho:
	mes "[Kalabar]";
	mes "\"Ei! Vamos logo com isso! Os itens são: 1 Óleo de Monstro, 70 poções antiveneno, e 10 coxas de frango.\"";
close;


L_semlugar:
	mes "[Kalabar]";
	mes "=S \"Não tem lugar na sua mochila. Libere espaço e depois volte para pegar seu presentinho =P\"";
close;

L_Fim:
	mes "[Kalabar]";
	mes "XD \"Lembre-se, não conte pra ninguém heim?\"";
close;

}
