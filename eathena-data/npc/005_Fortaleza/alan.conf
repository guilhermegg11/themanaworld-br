// QUEST do Arco da Floresta. Autor: Kamaitachi - Script: Alastrim
// Item: Arco da Floresta - Requisitos: Lvl > 40 + Madeira Resistente (JACK) + Corda Fina e Resistente (PESCADOR)
// Variáveis Usadas:
//
// PRINCIPAL - QUEST_arcoFloresta
//
//		Valor = 1 >> Jogador Aceitou fazer a quest.
//		Valor = 2 >> Jogador já entregou os Materiais e completou a quest.
//		Valor = 3 >> Aceitou fazer a quest do colar
//		Valor = 4 >> Aceitou a proposta do NPC Lobo Solitário para fazer a Pedra Olho de Aguia.
//
// SUBQUESTS - QUEST_madeiraResist - NPC Jack
//
//		Valor = 1 >> Jogador Aceitou fazer a subquest.
//		Valor = 2 >> Jogador já entregou os Materiais e completou a subquest.
//
//	     - QUEST_arcoFloresta_corda - NPC Pescador
//
//		Valor = 1 >> Jogador Aceitou fazer a subquest.
//		Valor = 2 >> Jogador já entregou os Materiais e completou a subquest.
//
//	     - QUEST_pedraAguia - NPC Lobo Solitário
//
//		Valor = 1 >> Jogador fez a pedra olho de aguia com o NPC.
//


005.gat,97,60,0	script	Alan	125, {

	if (BaseLevel < 40) goto _menorLvl40;
	if (QUEST_MASK1 & MASK1_ALAN) goto _obrigado;
	if (QUEST_arcoFloresta == 1) goto _materiais;
	if (QUEST_arcoFloresta == 2) goto _conselhosMestre;
	if (QUEST_arcoFloresta == 3 || QUEST_arcoFloresta == 4) goto _fazerColar;

_inicio:
	mes "À sua frente, você vê um homem que olha constantemente para o horizonte.";
	next;
	mes "Ele para de olhar para frente e presta atenção em você.";
	next;
	mes "[Alan]";
	mes "\"Oi, desculpe, não vi você, estava um pouco distraído.\"";
	next;
	menu
		"Oi, por que tanto olhava para o horizonte?", -;
	mes "\"Estava querendo saber até onde minhas flechas podiam chegar.\"";
	next;
	menu
		"Então quer dizer que você é arqueiro?", -;
	mes "[Alan]";
	mes "\"Digamos que trilho pelo caminho do arco e da flecha tentando ferir as nuvens. Mas deixemos de falar de mim. O que posso fazer por você?\"";
	next;
	menu
		"Bem, quando vi você tão distraído, quis saber se havia algo errado.", -;
	mes "[Alan]";
	mes "\"É muito gentil de sua parte ver se as pessoas estão bem. Mas mudando de assunto...o que você me diz de ter um bom arco para ser usado em caça?\"";
	next;
	menu
		"Acho que seria muito bom!", _seriaBom,
		"Acho que não sou muito bom com arco e flecha. Fica para depois.", -;
	mes "[Alan]";
	mes "\"Então tudo bem. Tenha um bom dia.\"";
	close;

_seriaBom:
	set QUEST_arcoFloresta, 1;
	mes "[Alan]";
	mes "\"Excelente! Então, para fazer o seu arco precisarei basicamente de um pedaço de madeira resistente. Além disso, preciso de uma corda fina.\"";
	next;
	mes "\"Há um lenhador chamado Jack que vive perto das florestas desta ilha, acho que ele poderá lhe ajudar a encontrar a madeira certa para o arco. E quanto à corda, há alguns pescadores que sabem fazer artesanato que podem lhe ajudar.\"";
	next;
	mes "\"Até mais e boa sorte... Ah, e por favor, não tente conseguir estes materiais no mercado negro... Só farei o arco se você conseguir tudo do modo como eu sugeri.\"";
	close;

_materiais:
	mes "[Alan]";
	mes "\"Trouxe os materiais que te pedi para fazer o seu arco?\"";
	menu
		"Sim, aqui estão.", _materiais1,
		"Ainda não.",-,
		"Quais são eles mesmo?", _materiais2;
	mes "[Alan]";
	mes "\"Eu preciso de um toco de madeira resistente e uma corda fina. Sem estes materiais não poderei te ajudar.";
	close;

_materiais1:
	if (countitem(3214) < 1 || countitem(3217) < 1) goto _semItens;
	if (QUEST_arcoFloresta_corda < 2 || (QUEST_MASK1 | MASK1_MADEIRA_RESIST) == 0) goto _naoLigitimos;
	//< RECOLHER MATERIAIS AQUI E SALVAR O ESTADO... POIS EXISTEM PAUSAS E O JOGADOR PODE TENTAR RETIRÁ-LOS DO INVENTÁRIO.
	//< SEM CONTAR QUE ALAN PRECISA DOS ITENS PARA FAZER O ARCO!
	goto _criarArco;

_semItens:
	mes "[Alan]";
	mes "\"Me parece que você não tem os itens necessários.\"";
	close;

_naoLigitimos:
	mes "[Alan]";
	mes "\"Hmmm... Estranho... Você tem os items que te pedi mas tenho certeza que não foi você que os pegou... Estes items foram originalmente destinados a outra pessoa, provavelmente você os comprou. Me desculpe, mas eu não posso aceitar estes itens. Por favor, consiga o que te pedi pelos meios que te falei.\"";
	close;

_criarArco:
	mes "[Alan]";
	mes "\"Hum, os materiais são muito bons. O seu arco será um arco muito forte. Mãos a obra então!\"";
	next;
	mes "Alan pega algumas ferramentas e começa a talhar o bloco de madeira até que atinja a forma de um arco.";
	next;
	mes "Logo depois, ele passa um material que deixa a madeira escura e bastante elegante para logo depois fixar a corda.";
	next;
	mes "Alan faz uns testes usando flechas de ferro e por fim, com um sorriso lhe diz:";
	next;
	mes "\"Finalmente está pronto o seu arco. Como ele foi feito de uma madeira que só existe nesta floresta, esse será um genuíno arco da floresta. Eu sinceramente espero que você goste deste arco. Foi um dos meus melhores trabalhos!\"";
	set QUEST_arcoFloresta, 2;
// Zerando variáveis das subquests, já que elas não são mais necessárias.
	set QUEST_madeiraResist, 0;
	set QUEST_arcoFloresta_corda, 0;
	delitem 3214, 1;
	delitem 3217, 1;
// Lembrar de mudar o ID 567 e 568 pelo ID correto
	getitem 545, 1;
	close;


_materiais2:
	mes "[Alan]";
	mes "\"Você precisa me entregar um toco de madeira resistente e uma corda fina. Sobre a madeira, você pode falar com o Jack, o lenhador que mora próximo a uma floresta desta ilha. E a corda pode ser feita por algum pescador dessa mesma ilha.\"";
	next;
	mes "[Alan]";
	mes "\"Então quando tiver todos os materiais, me procure que eu farei o seu arco.\"";
	close;

_conselhosMestre:
	mes "[Alan]";
	mes "\"Espero que você esteja satisfeito com o Arco da Floresta, ele é realmente um dos meu melhores trabalhos... E por falar no arco que fiz para você, como está se saído com ele?\"";
	next;
	menu
		"Ele é muito útil nas caçadas e no combate, foi um trabalho magistral.", _conselhosMestre1,
		"Ele até que é bom, mas prefiro lutar com minha espada.", _conselhosMestre2;
	goto _conselhosMestre1;

_conselhosMestre1:
	next;
	mes "[Alan]";
	mes "Alan dá uma gargalhada e diz: \"Realmente o arco é muito bom. Gosto de fazer o meu trabalho buscando sempre a perfeição.\"";
	next;
	goto _conselhosMestre3;

_conselhosMestre2:
	next;
	mes "[Alan]";
	mes "\"De fato, alguns guerreiros se saem melhor em combates corpo-a-corpo.\"";
	next;
	goto _conselhosMestre3;

_conselhosMestre3:
	mes "[Alan]";
	mes "\"Como mestre arqueiro, posso lhe dizer que o caminho do arco é para poucos. Não se trata apenas de se atirar uma flecha e acertar o alvo.\"";
	next;
	mes "\"O caminho do arco exige do aprendiz que ele compreenda a complexidade do todo, isto é, seu arco, sua flecha, sua força, seu alvo e a trajetória do seu disparo.\"";
	next;
	mes "\"Além disso, o arqueiro deve se tornar parte da natureza. E entendê-la e respeitá-la são os primeiros passos.\"";
	next;
	mes "\"Bom, já falei demais, mas, se você quer sentir um pouco do que os iniciados na arte do arco e da flecha sentem no seu treinamento, posso fazer um colar que reflete bem uma das qualidades necessárias para se tornar um bom arqueiro\"";
	next;
	menu
		"Claro! O que tenho que fazer?", _e1,
		"Não precisa, não sou muito bom com arcos.", -;
	mes "[Alan]";
	mes "\"Tudo bem. É uma escolha sua não seguir por este caminho. Mas o caminho que escolher, que lá esteja o seu coração.\"";
	close;

_e1:
	next;
	mes "[Alan]";
	mes "\"Bom, para que eu faça o colar, irei precisar de um pouco de ferro, uma pedra que simbolize a visão de um arqueiro e alguma coisa que sirva de polidor para a pedra.\"";
	next;
	mes "\"Bem, acho que já sei o que precisarei. Traga-me 20 minérios de ferro, 1 pedra olho de águia, 10 pérolas, 5 garrafas de água, 100 gosmas de verme, 20 cinzas, 1 retalho de couro de cobra, 10 tufos de pelo, 20 tocos de madeira e 200000 gp pelo serviço.\"";
	next;
	menu
		"Pedra olho de águia? Onde encontro isso?", -;
	next;
	mes "[Alan]";
	mes "\"Essa pedra é típica de uma tribo indígena que vivia nesta ilha. Agora, o único que sabe fazê-la é um índio que vive sozinho a leste daqui. Fale com ele, e quando tiver conseguido a pedra olho de águia, venha falar comigo.\"";
	set QUEST_arcoFloresta, 3;
	close;

_fazerColar:
	mes "[Alan]";
	mes "\"Conseguiu todos os materiais que te pedi para fazer o seu colar?\"";
	next;
	menu
		"Sim, estou com todos os materiais que me pediu", _fazerColar1,
		"Ainda não.", -;
	mes "[Alan]";
	mes "\"Ok, quando estiver com eles é só vir até mim.\"";
	close;

_fazerColar1:
	if ((countitem(640) >= 20 && countitem(3216) >= 1 && countitem(700) >= 10 && countitem(541) >= 5 && countitem(505) >= 100 && countitem(701) >= 20 && countitem(708) >= 1 && countitem(611) >= 10 && countitem(569) >= 20 && Zeny >= 200000) && (QUEST_pedraAguia < 1)) goto _fazerColar2;
	if (countitem(640) < 20 || countitem(3216) < 1 || countitem(700) < 10 || countitem(541) < 5 || countitem(505) < 100 || countitem(701) < 20 || countitem(708) < 1 || countitem(611) < 10 || countitem(569) < 20 || Zeny < 200000) goto _fazerColar3;
	goto _fazerColar4;

_fazerColar2:
	mes "[Alan]";
	mes "\"Ei... Você tem tudo o que eu pedi, mas essa pedra olho de águia você não conseguiu com o indio de que te falei... Não posso aceitar um item desses sem saber exatamente sua procedência. Por favor, vá falar com o indio e peça uma pedra para ele.\"";
	close;

_fazerColar3:
	mes "[Alan]";
	mes "\"Hmm... Você não tem tudo o que eu pedi. Lembre-se, eu preciso de 20 minérios de ferro, 1 pedra olho de águia, 10 pérolas, 5 garrafas de água, 100 gosmas de verme, 20 cinzas, 1 retalho de couro de cobra, 10 tufos de pelo, 20 tocos de madeira e 200000 gp pelo serviço.\"";
	close;

_fazerColar4:
	mes "[Alan]";
	mes "\"Então vou fazer o seu colar imediatamente.\"";
	next;
	mes "Alan pega os itens e os separa para facilitar o trabalho. Primeiro, em um aparato, acende um pequeno forno com a madeira que você entregou e funde os minérios de ferro, logo após adiciona carvão para separar as impurezas.";
	next;
	mes "Depois, utiliza algumas ferramentas para modelar a pequena chapa de ferro feita a partir dos minérios. Além de martelar o ferro, ele também prepara uma pequena corrente no qual ficará o amuleto. Quando finaliza de trabalhar o metal, ele começa a trabalhar em algo que parece mais alquimia do que técnica.";
	next;
	mes "Em um primeiro momento ele despeja água em um recipiente especial. Adiciona as gosmas, as cinzas e as pérolas em forma de pó e deixa em fervura até que tudo fique como um líquido viscoso de cor violeta.";
	next;
	mes "Pega os tufos e coloca em cima do retalho de couro de cobra, passa um pouco do líquido viscoso nos tufos, e segurando os tufos com o retalho, começa a polir a esfera em forma de olho de águia. No fim das contas, corta a esfera em duas partes iguais e encaixa no colar de metal. Lustra o colar como um todo usando a cera e faz os acabamentos para que ele fique perfeito.";
	next;
	mes "[Alan]";
	mes "\"Aqui está. Que o espírito dos arqueiros permaneça em seu caminho por muito tempo.\"";
	delitem 640, 20;
	delitem 3216, 1;
	delitem 700, 10;
	delitem 541, 5;
	delitem 505, 100;
	delitem 701, 20;
	delitem 708, 1;
	delitem 611, 10;
	delitem 569, 20;
	set Zeny, Zeny - 200000;
	getitem 3201, 1;
	set QUEST_pedraAguia, 0;
	set QUEST_arcoFloresta, 0;
	set QUEST_MASK1, (QUEST_MASK1 | MASK1_ALAN);
	close;

_menorLvl40:
	mes "[Alan]";
	mes "\"Vejo que você ainda não tem experiência para lidar com a arte da arquearia... Fale novamente comigo quando estiver um pouco mais forte.\"";
	close;

_obrigado:
	mes "[Alan]";
	mes "\"Olá " +strcharinfo(0)+ ". Que bom te rever! Espero que o espírito dos arqueiros continue com você ao longo de sua jornada. Boa sorte!\"";
	close;

}
