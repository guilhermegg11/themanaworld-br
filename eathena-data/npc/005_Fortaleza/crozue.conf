
////////////////////////////////////////////////////////////////////////////////////////////////
// NOME: Alquimista Crozué                                                                    //
// ETMOLOGIA: Crozué é um alquimista especialista capturar a alma de equipamentos de batalha. //
// Data: terça, 19 de julho de 2011                                                           //
// AUTHOR: Diogo_RBG - http://diogorbg.blogspot.com/                                          //
////////////////////////////////////////////////////////////////////////////////////////////////

//005.gat,132,60,0	shop	shop	153,579:1,3268:1,3055:1,3280:1,3056:1,3271:1,3053:1,3274:1,3054:1,3277:1

005.gat,135,60,0	script	Alquimista Crozué	153,{

	if(@pular==1) goto _menu;
	set @pular, 1;

	mes "[Alquimista Crozué]";
	mes "\"Olá, me chamo Crozué... alquimista especialista em equipamentos de batalha.";
	mes "Gostaria de reduzir/restaurar a energia da alma de sua espada hoje?\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	menu
		"¬¬ De jeito nenhum.", -,
		"Oo Isso não faz nenhum sentido.", _explicar,
		"Sim... estou atrás de seus serviços", _menu,
		"Hoje não... obrigado.", -;
	mes "[Alquimista Crozué]";
	mes "\"Ok... me procure quando precisar de meus serviços.\"";
	close;

_explicar:
	mes "[Alquimista Crozué]";
	mes "\"É normal a sua perplexidade. A maioria dos grandes ferreiros e alquimistas deste mundo também não tem conhecimento de minha técnica.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "\"Me explique como você poderia retirar a alma de minha espada e porque eu estaria interessado em fazer isso.\"";
	next;
	mes "[Alquimista Crozué]";
	mes "\"Todos querem ser poderosos e procuram por poder. Mas se esquecem que devem ser ainda mais poderosos que o poder que procuram... precisam ter o domínio do poder para não serem dominados por ele.\"";
	next;
	mes "[Alquimista Crozué]";
	mes "\"Não é raro se deparar com espadas lendárias de imenso poder e não ter força o suficiente para empunhá-la.";
	mes "Mas se parte desta imensidão de energia for extraída, existirá uma chance.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "\"Quer dizer que você pode me ajudar a controlar o poder de armas poderosas! Isso é muito bom.\"";
	next;
	mes "[Alquimista Crozué]";
	mes "\"Sim, mas existe um porém. Reduzir o poder da arma significa deixá-la um pouco mais fraca.";
	mes "Mas não há com o que se preocupar... posso restaurar o poder da arma novamente... :? se bem que isto é mais complicado.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "\"Complicado quanto?\"";
	next;
	mes "[Alquimista Crozué]";
	mes "\"É preciso transferir o poder de um objeto realmente poderoso para a arma... e também é preciso sorte.\"";
	close;

_menu:
	mes "[Alquimista Crozué]";
	mes "\"Quais de meus serviços você está interessado?\"";
	menu
		//"Restauração da energia da alma.", _menuRestaurar,
		"Redução da energia da alma.", _menuReducao,
		"Nenhum, obrigado.", -;
	mes "[Alquimista Crozué]";
	mes "\"Me procure sempre que precisar de meus serviços.\"";
	close;

_menuRestaurar:
	mes "[Alquimista Crozué]";
	mes "\"\"";
	close;

//= REDUÇÃO DE NÍVEL ===================================================

_menuReducao:
	mes "[Alquimista Crozué]";
	mes "\"A redução consiste em extrair parte da energia da alma acumulada na arma.";
	mes "Não é irreversível, poderei restaurar esta energia depois, mas teremos dificuldades para fazer isto.";
	mes "Isto dependerá principalmente da arma.\"";
	next;
	mes "[Alquimista Crozué]";
	mes "\"Você gostaria de reduzir o nível de que arma?\"";
	menu
		"Não quero reduzir arma nenhuma.", -,
		"* Rock Knife", _rockKnife,
		"* Espada dos Imortais", _espadaImortais,
		"* Espada Miguel Arcanjo", _espadaArcanjo,
		"* Espada Demoníaca", _espadaDemoniaca,
		"Não tenho nenhuma destas armas.", -;
	goto _menu;

_verificar1:
	set @cont, 0;
	if (countitem(@normal)>0) set @cont, @cont+1;
	if (countitem(@modif)>0) set @cont, @cont+1;
	if (countitem(@modif+1)>0) set @cont, @cont+1;
	if (countitem(@modif+2)>0) set @cont, @cont+4;
	return;

_selecionar1:
	set @old, 0;
	set @new, 0;
	if (countitem(@normal)>0) set @old, @normal;
	if (countitem(@normal)>0) set @new, @modif;
	if (countitem(@modif)>0) set @old, @modif;
	if (countitem(@modif)>0) set @new, @modif+1;
	if (countitem(@modif+1)>0) set @old, @modif+1;
	if (countitem(@modif+1)>0) set @new, @modif+2;
	return;

_reducao1:
	mes "[Alquimista Crozué]";
	mes "\"Irei reduzir o nível e também a força da sua arma '"+@nome$+"'.";
	mes "Deseja realmente fazer isto?\"";
	menu
		"Mudei de ideia", -,
		"Continue, por favor.", _reducao2;
	goto _menu;

_reducao2:
	mes "Crozué então começa a trabalhar em sua arma.";
	mes "Ele coloca uma mão sobre a outra e sem tocar sua arma a faz brilhar intensamente.";
	next;
	mes "O brilho é cada vez mais intenso... você mal pode olhar para ele.";
	mes "";
	mes "[Alquimista Crozué]";
	mes "\"A extração desta quantidade de energia da alma deve ser o suficiente.\"";
	next;
	callsub _verificar1;
	if(@cont!=1 && @cont!=5) goto _falasReducaoErro;
	callsub _selecionar1;
	delitem @old, 1;
	getitem @new, 1;
	mes "Crozué faz um movimento e de repente aquela enorme quantidade de energia estoura e se espalha pelo ar.";
	mes "Um fenômeno brilhante e ensurdecedor.";
	mes "";
	mes "[Alquimista Crozué]";
	mes "\"Sim... funcionou!";
	mes "A redução foi executada com sucesso.\"";
	close;

_falasReducaoErro:
	mes "[Alquimista Crozué]";
	if(@cont==0) mes "\"Você não tem esta arma.\"";
	if( (@cont>0 && @cont<4) || @cont>4)  mes "\"Você tem muitas armas. Só posso trabalhar em uma delas.\"";
	if(@cont==4)  mes "\"Não posso fazer outra redução nesta arma.\"";
	close;

//----------------------------------------------------------------------

_rockKnife:
	set @nome$, "Rock Knife";
	set @normal, 579; //RockKnife
	set @modif, 3268; //RockKnife_1
	callsub _verificar1;
	if(@cont==1 || @cont==5) goto _reducao1;
	goto _falasReducaoErro;

_espadaImortais:
	set @nome$, "Espada dos Imortais";
	set @normal, 3056; //EspadaImortal
	set @modif, 3271;  //EspadaImortal_1
	callsub _verificar1;
	if(@cont==1 || @cont==5) goto _reducao1;
	goto _falasReducaoErro;

_espadaArcanjo:
	set @nome$, "Espada Miguel Arcanjo";
	set @normal, 3053; //EspadaMiguelArcanjo
	set @modif, 3274;  //EspadaMiguelArcanjo_1
	callsub _verificar1;
	if(@cont==1 || @cont==5) goto _reducao1;
	goto _falasReducaoErro;

_espadaDemoniaca:
	set @nome$, "Espada Demoníaca";
	set @normal, 3054; //EspadaDemoniaca
	set @modif, 3277;  //EspadaDemoniaca_1
	callsub _verificar1;
	if(@cont==1 || @cont==5) goto _reducao1;
	goto _falasReducaoErro;

}
