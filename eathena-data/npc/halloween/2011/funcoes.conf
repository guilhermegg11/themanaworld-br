/**
 * Funções para o evento de Halloween 2011
 */

/**
 * Retorna qual o id do evento... eventos: 1..15, 0 para nenhum.
 * @return @evento informando o id do evento ativo. 0, caso nenhum esteja ativo.
 */
function	script	getEvento	{
	set @evento, 0;

	set @mes, gettime(6);
	set @dia, gettime(5);
	set @hora, gettime(3);
	set @min, gettime(2);
	if (@mes!=11 || @dia<2 || @dia>6 || @min>45) return;

	if (@dia==2 && @hora==11) set @evento, 1;
	if (@dia==2 && @hora==16) set @evento, 2;
	if (@dia==2 && @hora==22) set @evento, 3;
	if (@dia==3 && @hora==11) set @evento, 4;
	if (@dia==3 && @hora==16) set @evento, 5;
	if (@dia==3 && @hora==22) set @evento, 6;
	if (@dia==4 && @hora==11) set @evento, 7;
	if (@dia==4 && @hora==16) set @evento, 8;
	if (@dia==4 && @hora==22) set @evento, 9;
	if (@dia==5 && @hora==11) set @evento, 10;
	if (@dia==5 && @hora==16) set @evento, 11;
	if (@dia==5 && @hora==22) set @evento, 12;
	if (@dia==6 && @hora==11) set @evento, 13;
	if (@dia==6 && @hora==16) set @evento, 14;
	if (@dia==6 && @hora==22) set @evento, 15;
	return;
}

/**
 * Retorna qual o id do evento como global temporária... eventos: 1..15, 0 para nenhum.
 * @return @evento informando o id do evento ativo. 0, caso nenhum esteja ativo.
 */
function	script	getEvento2	{
	set $@evento, 0;

	set $@mes, gettime(6);
	set $@dia, gettime(5);
	set $@hora, gettime(3);
	set $@min, gettime(2);
	if ($@mes!=11 || $@dia<2 || $@dia>6 || $@min>45) return;

	if ($@dia==2 && $@hora==11) set $@evento, 1;
	if ($@dia==2 && $@hora==16) set $@evento, 2;
	if ($@dia==2 && $@hora==22) set $@evento, 3;
	if ($@dia==3 && $@hora==11) set $@evento, 4;
	if ($@dia==3 && $@hora==16) set $@evento, 5;
	if ($@dia==3 && $@hora==22) set $@evento, 6;
	if ($@dia==4 && $@hora==11) set $@evento, 7;
	if ($@dia==4 && $@hora==16) set $@evento, 8;
	if ($@dia==4 && $@hora==22) set $@evento, 9;
	if ($@dia==5 && $@hora==11) set $@evento, 10;
	if ($@dia==5 && $@hora==16) set $@evento, 11;
	if ($@dia==5 && $@hora==22) set $@evento, 12;
	if ($@dia==6 && $@hora==11) set $@evento, 13;
	if ($@dia==6 && $@hora==16) set $@evento, 14;
	if ($@dia==6 && $@hora==22) set $@evento, 15;
	return;
}

/**
 * Script para dropar as sementes durante os horários de evento e
 * script para classificação no top15.
 *
 * @var @evento informa qual dos evento está ativo. 0, caso nenhum esteja.
 * Primeiro deve executar a função getEvento: 'callfunc "getEvento";'
 * @date segunda, 26 de setembro de 2011
 * @author Diogo_RBG - http://diogorbg.blogspot.com
 */
function	script	dropSemente	{
	// Uma única mensagem quando o evento Top15 estiver inativo.
	if(@evento==0 && $@HLW3_evento2!=@evento)
		announce "* O evento 'Top15 de Halloween' está INATIVO.", 0;
	set $@HLW3_evento2, @evento;

	// Retorna se o evento Top15 não estiver ativo.
	if(@evento==0) return;

	// Rand para conseguir um ovo dourado.
	set @tmp, rand(100);
	if(@tmp>=6) return;

	// Verifica dia do evento.
	if(HLW3_dia!=@dia) set HLW3_diaItem, 0;
	if(HLW3_dia!=@dia) set HLW3_dia, @dia;
	// Verifica evento
	if(HLW3_evt!=@evento) set HLW3_evtItem, 0;
	if(HLW3_evt!=@evento) set HLW3_evt, @evento;

	// Aumenta o placar do evento e verifica se foi o melhor placar do dia.
	set HLW3_evtItem, HLW3_evtItem+1;
	if(HLW3_evtItem <= HLW3_diaItem) message strcharinfo(0), "^.^";
	if(HLW3_evtItem <= HLW3_diaItem) getitem 3357, 1;
	if(HLW3_evtItem <= HLW3_diaItem) return;
	// Aumenta o placar do dia e do top15 tbm!
	set HLW3_diaItem, HLW3_evtItem;
	set HLW3_item, HLW3_item+1;
	getitem 1995, 1;
	message strcharinfo(0), "Aeee!";

	// Identifica o primeiro jogador a conseguir um item no top15.
	if($@HLW3_evento==@evento) goto _pular0;
		set $@HLW3_evento, @evento;
		announce "* '"+strcharinfo(0)+"' coletou a primeira Semente de Abóbora do evento!", 0;
		gmcommand "@l #informacao '"+strcharinfo(0)+"' coletou a primeira Semente de Abóbora do evento!";
	_pular0:

	// Se puder entrar no final do placar || ser o primeiro a entrar na lista
	if ($HLW3_vetPlacar[$HLW3_numPlacar-1]<HLW3_item && $HLW3_numPlacar!=0) goto _pular1; //< Se não puder: _pula1
	//else
		set $HLW3_nomPlacar$[$HLW3_numPlacar], strcharinfo(0);
		set $HLW3_vetPlacar[$HLW3_numPlacar], HLW3_item;
		set $HLW3_lvlPlacar[$HLW3_numPlacar], BaseLevel;
		if ($HLW3_numPlacar<15) set $HLW3_numPlacar, $HLW3_numPlacar+1;
		return;
	_pular1:

	// Buscar nome no placar
	set @tmp, $HLW3_numPlacar; //< O padão é o ultimo do placar
	set @i, -1;
	_loop1: set @i,@i+1; if(@i>=$HLW3_numPlacar) goto _fimLoop1;
		if ($HLW3_nomPlacar$[@i]!=strcharinfo(0)) goto _loop1;
		//else
			set $HLW3_vetPlacar[@i], HLW3_item;
			set $HLW3_lvlPlacar[@i], BaseLevel;
			set @tmp, @i;
			goto _fimLoop1;
		//goto _loop1;
	_fimLoop1:

	// Subir no placar
	set @i, @tmp; //< Indice encontrado no loop1
	set @tmp, 0;
	_loop2: set @i,@i-1; if(@i<0) goto _fimLoop2;
		if ($HLW3_vetPlacar[@i]>=HLW3_item) set @i, -1;
			if(@i<0) goto _fimLoop2;
		//else
			set @tmp, 1;
			set $HLW3_nomPlacar$[@i+1], $HLW3_nomPlacar$[@i];
			set $HLW3_vetPlacar[@i+1], $HLW3_vetPlacar[@i];
			set $HLW3_lvlPlacar[@i+1], $HLW3_lvlPlacar[@i];
			set $HLW3_nomPlacar$[@i], strcharinfo(0);
			set $HLW3_vetPlacar[@i], HLW3_item;
			set $HLW3_lvlPlacar[@i], BaseLevel;
		goto _loop2;
	_fimLoop2:

	if (@tmp==1 && $HLW3_numPlacar<15) set $HLW3_numPlacar, $HLW3_numPlacar+1;
	return;
}
