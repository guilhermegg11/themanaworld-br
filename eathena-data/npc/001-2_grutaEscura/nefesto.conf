//Quest Bota assassino por: Sky

001-2.gat,66,61,0	script	Nefesto	519, {
	set @OleoDeMonstro, 707; //-01UI
	set @PocoesDaMorte, 3003; //-30UI
	set @FerraoDeAbelha, 3150; //-30UI
	set @BotasDeAssassino, 757; //+01UI
	
	if (QUEST_botaassassin == 0 && BaseLevel < 95) goto L_semLevel;
	if (QUEST_botaassassin == 1) goto L_Retorno;
	if (QUEST_botaassassin == 2) goto L_Fim;
goto L_iniciaQuest;

L_semLevel:
	mes "[Nefesto]";
	mes "\"Não me atrapalhe.\"";
close;

L_iniciaQuest:
	mes "Você vê um homem entusiasmado.";
	next;
	mes "[Nefesto]";
	mes "=] \"Olá viajante! O que faz por estas terras?\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"Nada não", -,
	"Quem é você?", L_Quem;
	mes "";
	mes "[Nefesto]";
	mes "\"Passar bem!\"";
close;

L_Quem:
	mes "[Nefesto]";
	mes "\"Eu sou o poderoso Nefesto.\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"E o que você faz da vida?", L_Profissao,
	"Ui, tchauzinho poderoso", -;
close;

L_Profissao:
	mes "[Nefesto]";
	mes "(u.u) \"Eu tiro vidas por dinheiro. Se é que você me entende.\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"Sério?", L_Tbmsou,
	"Fui!", -;	
close;

L_Tbmsou:
	mes "[Nefesto]";
	mes "\"Claro, não sou de mentir. Eu sei que nem te conheço ainda, mas quer fazer algo por mim que me ajudaria e muito?\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"Tudo bem, o que é?", L_vouajudar,
	"Não posso.", -;
close;
	
L_vouajudar:
	mes "[Nefesto]";
	mes "\"Eu estou planejando uma mistura para poder envenenar meus inimigos. Faltam alguns ingredientes, você poderia me ajudar a coletá-los?\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"Quais ingredientes?", L_Oquequer,
	"Agora estou ocupado.", -;
close;

L_Oquequer:
	mes "[Nefesto]";
	mes "\"Traga-me 1 Óleo de Monstro, 30 poções da morte, e 30 ferrões de abelha.\"";
	next;
	mes "["+strcharinfo(0)+"]";
	mes "\"Já vou buscar.\"";
	set QUEST_botaassassin, 1;
close;
	
L_Retorno:
	mes "[Nefesto]";
	mes "\"Trouxe os itens que eu te pedi?\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu 
	"Sim, aqui estão.", -,
	"Não, ainda não consegui =(", L_naotenho;
	if(countitem(@OleoDeMonstro)<1 || countitem(@FerraoDeAbelha)<30 || countitem(@PocoesDaMorte)<30)  goto L_naotem;
	getinventorylist;
	if (@inventorylist_count >=100) goto L_semlugar;
	delitem @OleoDeMonstro, 1;
	delitem @PocoesDaMorte, 30;
	delitem @FerraoDeAbelha, 30;
	getitem @BotasDeAssassino, 1;
	set QUEST_botaassassin, 2;
	mes "[Nefesto]";
	mes "\"Muito obrigado! Agora posso ser mais eficiente em meus serviços. Aceite esta recompensa por ter me ajudado.\"";
	next;
	mes "[Nefesto]";
	mes "\"Essas botas lhe darão a velocidade do vento.\"";
close;

L_naotem:
	mes "[Nefesto]";
	mes "\"Parece que você não tem tudo da lista. Pegue tudo e volte a falar comigo.\"";
close;

L_naotenho:
	mes "[Nefesto]";
	mes "\"Tudo bem, mas se apresse! Quero terminar isto logo. Os itens são: 1 Óleo de Monstro, 30 ferrões de abelha, e 30 poções da morte.\"";
close;

L_semlugar:
	mes "[Nefesto]";
	mes "\"Ora ora, parece que não há nenhum lugar para carregar sua recompensa. Libere espaço em sua mochila e volte aqui.\"";
close;

L_Fim:
	mes "[Nefesto]";
	mes "\"Obrigado viajante!\"";
close;

}
