//
// Teleporte... o jeito fácil de se locomover no jogo.
//

// Mais falas
// mes "Mas isto não é nenhuma novidade. Des de tempos remotos o homem viajou sendo guiado pelas estrelas.\"";

005.gat,122,74,0	script	Teleportadora	350,{
	mes "[Teleportadora Adhara]";
	mes "\"A ligação entre o nosso mundo e as estrelas é bem maior do que podemos imaginar.";
	mes "Eu acredito que os pontos deste mundo se ligam da mesma forma em que as estrelas se ligam no céu.\"";
	next;
	mes "[Teleportadora Adhara]";
	mes "\"Guiada pelas estrelas eu poço te levar a muitos lugares... basta existir uma conexão entre eles.";
	mes "Para onde gostaria de ir?\"";
	menu
		"Porto de Bhramir (3 kGP)", _portoBhramir,
		//"Vila dos Pescadores (3 kGP)", _vilaPescadores,
		"Polo Norte (3 kGP)", _poloNorte,
		"Terras Desconhecidas (5 kGP)", _terrasDesconhecidas,
		"Para Locais de Batalha.", _menuBatalha,
		"Desisti de viajar.", -;
	close;

//----------------------------------------------------------------------

_portoBhramir:
	callsub _pegarGrana, 3000;
	callsub _teleportar, "007", 105, 81;
	end;

_poloNorte:
	callsub _pegarGrana, 3000;
	callsub _teleportar, "010", 133, 61;
	end;

_terrasDesconhecidas:
	callsub _pegarGrana, 5000;
	callsub _teleportar, "org016", 99, 33;
	end;

//----------------------------------------------------------------------

_menuBatalha:
	mes "[Teleportadora Adhara]";
	mes "\"Também posso te levar direto para locais de batalha. Mas só para os locais que eu considerar seguro para você.";
	mes "Para onde gostaria de ir?\"";
	menu
		"Caverna do Eremita lvl 20 (3 kGP)", _cavernaEremita,
		"Caverna Luminosa lvl 30 (3 kGP)", _cavernaLuminosa,
		"Cavernas Alaguadas lvl 40 (3 kGP)", _cavernasAlagadas,
		//"Caverna das Aranhas lvl 50 (5 kGP)", _aranhas,
		//"Caverna dos Esqueletos lvl 60 (5 kGP)", _esqueletos,
		//"Caverna das Cobras das Cavernas lvl 70 (7 kGP)", _cobrasCavernas,
		//"Caverna ... lvl 80 (7 kGP)", _,
		//"Cemitério lvl 90 (10 kGP)", _cemiterio,
		"Melhor não arriscar!", -;
	close;

//----------------------------------------------------------------------

_cavernaEremita:
	callsub _lvl, 20;
	callsub _pegarGrana, 3000;
	callsub _teleportar, "007-1", 95, 35;
	end;

_cavernaLuminosa:
	callsub _lvl, 30;
	callsub _pegarGrana, 3000;
	callsub _teleportar, "009-2", 36, 66;
	end;

_cavernasAlagadas:
	callsub _lvl, 40;
	callsub _pegarGrana, 3000;
	callsub _teleportar, "008-1", 61, 89;
	end;

//----------------------------------------------------------------------

_teleportar:
	mes "[Teleportadora Adhara]";
	mes "\"Boa viagem\"";
	close2;
	warp getarg(0)+".gat", getarg(1), getarg(2);
	end;

_lvl:
	if (BaseLevel<getarg(0) ) goto _semLvl;
	return;

_semLvl:
	mes "[Teleportadora Adhara]";
	mes "\"Me desculpe, mas este não me parece um local seguro para você.\"";
	close;

_pegarGrana:
	if (zeny<getarg(0) ) goto _semGrana;
	set zeny, zeny-getarg(0);
	return;

_semGrana:
	mes "[Teleportadora Adhara]";
	mes "\"Me desculpe, mas você não tem dinheiro suficiente para esta viagem.\"";
	close;

}
