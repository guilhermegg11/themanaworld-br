/**
 * NPC com todas as informações do evento de Páscoa para os jogadores.
 *
 * @date domingo, 17 de abril de 2011
 * @author Diogo_RBG - http://diogorbg.blogspot.com
 */

005.gat,111,82,0	script	Guia de Páscoa	173,{
	callfunc "getEvento";
	callfunc "infoPascoa2";
	close;
}

005-1.gat,79,35,0	script	Guia de Páscoa	173,{
	callfunc "getEvento";
	callfunc "infoPascoa2";
	close;
}

007.gat,100,70,0	script	Guia de Páscoa	173,{
	callfunc "getEvento";
	callfunc "infoPascoa2";
	close;
}

010.gat,147,90,0	script	Guia de Páscoa	173,{
	callfunc "getEvento";
	callfunc "infoPascoa2";
	close;
}

012.gat,125,155,0	script	Guia de Páscoa	173,{
	callfunc "getEvento";
//	mes "evento: "+@evento;
	callfunc "infoPascoa2";
	close;

OnInit:
	initnpctimer;
	end;

//- OnMinute só pode ser usado uma única vez. Servirá para sincronizar o horário.
OnMinute00:
	callfunc "getEvento2";
	if($@evento!=0) announce "* O evento 'Top15 de Páscoa' está ATIVO!!!", 0;
OnTimer300000:
	setnpctimer 0;
	callfunc "getEvento2";
	goto _infoPlacar;

_aviso:
	announce "* Faltam "+(60-$@min)+" minutos para início do evento 'Top15 de Páscoa'.", 0;
	announce "- Vá para o mapa especial de páscoa e prepare-se para a competição.", 0;
	announce "- Mais informações com o 'Guia de Páscoa'.", 0;
	end;

_infoPlacar:
	if($@dia>=20 && $@dia<=24 && $@min>=49 && ($@hora==16 || $@hora==20)) goto _aviso;
	if($@dia>=23 && $@dia<=24 && $@min>=49 && ($@hora==10)) goto _aviso;
	if($@evento==0) end;

	announce "1-5: "+
		$PASCOA2_nomPlacar$[0]+" ("+$PASCOA2_vetPlacar[0]+"), "+
		$PASCOA2_nomPlacar$[1]+" ("+$PASCOA2_vetPlacar[1]+"), "+
		$PASCOA2_nomPlacar$[2]+" ("+$PASCOA2_vetPlacar[2]+"), "+
		$PASCOA2_nomPlacar$[3]+" ("+$PASCOA2_vetPlacar[3]+"), "+
		$PASCOA2_nomPlacar$[4]+" ("+$PASCOA2_vetPlacar[4]+")", 0;
	announce "6-10: "+
		$PASCOA2_nomPlacar$[5]+" ("+$PASCOA2_vetPlacar[5]+"), "+
		$PASCOA2_nomPlacar$[6]+" ("+$PASCOA2_vetPlacar[6]+"), "+
		$PASCOA2_nomPlacar$[7]+" ("+$PASCOA2_vetPlacar[7]+"), "+
		$PASCOA2_nomPlacar$[8]+" ("+$PASCOA2_vetPlacar[8]+"), "+
		$PASCOA2_nomPlacar$[9]+" ("+$PASCOA2_vetPlacar[9]+")", 0;
	announce "11-15: "+
		$PASCOA2_nomPlacar$[10]+" ("+$PASCOA2_vetPlacar[10]+"), "+
		$PASCOA2_nomPlacar$[11]+" ("+$PASCOA2_vetPlacar[11]+"), "+
		$PASCOA2_nomPlacar$[12]+" ("+$PASCOA2_vetPlacar[12]+"), "+
		$PASCOA2_nomPlacar$[13]+" ("+$PASCOA2_vetPlacar[13]+"), "+
		$PASCOA2_nomPlacar$[14]+" ("+$PASCOA2_vetPlacar[14]+")", 0;
	end;

}

//- Informações da Páscoa 2011 -----------------------------------------

function 	script	infoPascoa2	{

	mes "[Guia de Páscoa]";
	mes "\"Esteja preparado!!!";
	mes "Não perca os eventos que ocorrerão nos dias 20, 21, 22, 23 e 24.\"";
	if(@evento!=0)
		mes "* O evento 'Top15 de Páscoa' está ATIVO.";
	next;
	mes "[Guia de Páscoa]";
	mes "\"Horários do evento 'Top15 de Páscoa':\"";
	mes "* Dias 20, 21 e 22:";
	mes "-- 17:00 as 17:45";
	mes "-- 21:00 as 21:45";
	mes "* Dias 23 e 24:";
	mes "-- 11:00 as 11:45";
	mes "-- 17:00 as 17:45";
	mes "-- 21:00 as 21:45";
	next;
	goto _menu;

_menu:
	mes "* Hora certa: "+gettime(3)+":"+gettime(2)+":"+gettime(1)+".";
	mes "";
	mes "[Guia de Páscoa]";
	mes "\"O que mais gostaria de saber?\"";
	menu
		"Quero me tele-transportar.", _teleportar,
		"Me fale sobre a páscoa.", _sobrePascoa,
		"Onde posso conseguir [Ovos de Páscoa Dourado]?", _ondeOvosDourados,
		"Quantos [Ovos de Páscoa Dourado] coletei?", _contarOvosDourados,
		"Informações do Top15.", _InfoTop15,
		"Mostrar Placar Top15.", _placarTop15,
		"Sair.", -;
	mes "[Guia de Páscoa]";
	mes "\"Feliz Páscoa.\"";
	return;

_teleportar:
	mes "[Guia de Páscoa]";
	mes "\"Estarei à disposição para tele-transportá-lo para o mapa especial de páscoa e de volta.";
	mes "Gostaria de ser tele-transportado agora?\"";
	menu
		"Me tele-transporte agora mesmo.", _teleportar1,
		"Ops... era só curiosidade!", -;
	goto _menu;

_teleportar1:
	if( isin("012.gat", 125-30, 155-30, 125+30, 155+30)==1 ) goto _paraForaFloresta;
	mes "[Guia de Páscoa]";
	mes "\"Seja bem vindo à floresta dos coelhos!\"";
	warp "013.gat", 258, 39;
	close;

_paraForaFloresta:
	mes "[Guia de Páscoa]";
	mes "\"Volte sempre!\"";
	warp getsavepoint(0), getsavepoint(1), getsavepoint(2);
	close;

_sobrePascoa:
	mes "[Guia de Páscoa]";
	mes "\"Coelhos por toda parte, deliciosos ovos de chocolate com deliciosas castanhas enfeitados das mais diversas cores brilhantes dados como presente entre os jogadores.";
	mes "Esta é a páscoa do The Mana World Brasil.\"";
	next;
	mes "[Guia de Páscoa]";
	mes "\"Sem esquecer é claro das missões especiais e competições que só ocorrem em grandes datas comemorativas como a páscoa.\"";
	next;
	mes "[Guia de Páscoa]";
	mes "\"Não dá pra perder um evento deste... é sua chance de ganhar muitos prêmios.\"";
	next;
	goto _menu;

_ondeOvosDourados:
	mes "[Guia de Páscoa]";
	mes "\"Os ovos de páscoa dourados só podem ser coletadas de coelhos e lebres... e somente nos horários do evento 'Top15 de Páscoa'.\"";
	mes "* Lebre: 6.0%";
	mes "* Coelho: 6.0%";
	next;
	goto _menu;

_InfoTop15:
	mes "[Guia de Páscoa]";
	mes "\"Se você procura aventura, glória e riquezas você deve participar do desafio 'Top15 de Páscoa'. Mas esteja preparado, pois não será nada fácil. Você terá que se classificar entre os 15 primeiros colocados na busca dos [Ovos de Páscoa Dourado]\"";
	next;
	mes "[Guia de Páscoa]";
	mes "\"nem preciso dizer que todos estarão em busca dos ovos dourados e tentarão todos os tipos de estratégias e trapaças para vencer este desafio. Não basta ser bom, tem que ser o melhor!!!\"";
	next;
	mes "[Guia de Páscoa]";
	mes "\"Os 15 colocados serão aqueles que juntarem a maior quantidade de ovos dourados coletados nos dias e horários do evento top15. E serão bem recompensados com riqueza e glória.\"";
	next;
	mes "[Guia de Páscoa]";
	mes "\"Um baú cheio de preciosos itens (a serem repartidos entre os vencedores), muitas moedas de ouro, medalhas de ouro, prata e bronze e ainda ter seu nome eternizado em um memorial no Quartel General.\"";
	next;
	mes "[Guia de Páscoa]";
	mes "\"E ainda existe a possibilidade de ganhar um prêmio instantâneo... basta que você seja o primeiro a conseguir um ovo dourado. São 12 chances de ganhar e qualquer jogador poderá ser o sortudo!\"";
	next;
	mes "[Guia de Páscoa]";
	mes "\"É claro que os ovos dourados são intransferíveis. Comprar ovos dourados não trará benefícios a ninguém.";
	mes "Apos o evento de páscoa os ovos dourados serão comprados a preço de ouro... e você pode ganhar uma graninha extra.\"";
	next;
	goto _menu;

_contarOvosDourados:
	set @tmp, PASCOA2_item;
	if (countitem(3226)<@tmp) set @tmp, countitem(3226);
	mes "[Guia de Páscoa]";
	mes "\"Foram coletados "+@tmp+" [Ovos de Páscoa Dourado].\"";
	close2;
	npctalk "'"+strcharinfo(0)+"' coletou " +@tmp+ " [Ovos de Páscoa Dourado].";
	return;

//- Placar Top15 -------------------------------------------------------

_placarTop15:
	mes "[Guia de Páscoa]";
	mes ".:: Placar Top15 de Páscoa ::.";

	set @i, -1;
	_loop: set @i,@i+1; if(@i>=$PASCOA2_numPlacar) goto _fimLoop;
		mes (@i+1)+ "º - " +$PASCOA2_vetPlacar[@i]+ " - " + $PASCOA2_nomPlacar$[@i] +" (" +$PASCOA2_lvlPlacar[@i]+ ")";
		goto _loop;
	_fimLoop:

	next;
	goto _menu;

}
