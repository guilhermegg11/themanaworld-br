// Funções Storager

function	script	Storager	{

	mes "["+ @npcName$ +"]";
	if(Sex==1) mes "Bem Vindo!";
	if(Sex==0) mes "Bem Vinda!";
	next;
	if(@Custo<=0) goto _inicio;
	mes "["+ @npcName$ +"]";
	if (gettimetick(2) > #TempoDeposito) goto _cobrar;
	mes "\"Olá novamente! Posso te ajudar?\"";
	mes " ";

_inicio:
	mes "["+ strcharinfo(0) +"]";
	menu
		"Quero usar seu depósito.", _abrir_deposito,
		"Definir opções de segurança", L_Seguranca,
		"Nada, estou só de passagem....", -;
	goto _sair;

_cobrar:
	mes "\"Custa "+ @Custo +" GP para usar o depósito por 1 dia. Deseja pagar este custo?\"";
	menu
		"Sim, preciso usar o depósito", -,
		"Definir opções de segurança", L_Seguranca,
		"Não, está muito caro", _sair;
	if(zeny < @Custo) goto _sem_grana;
	set zeny, zeny - @Custo;
	set #TempoDeposito, gettimetick(2) + 86400;
	mes "["+ @npcName$ +"]";
	mes "Obrigado por confiar a mim os seus itens!";
	mes "Volte sempre!";

_abrir_deposito:
	if (#ContaDeposito == 1 && @DigitouSenhaDeposito == 0) goto L_deposito_Senha;
	set @restante, (#TempoDeposito - gettimetick(2))/60;
	if (@restante == 0) mes "\"Você pode usar o depósito por " + #TempoDeposito - gettimetick(2) + " segundo(s) sem nenhum custo adicional.\"";
	if (@restante < 60) mes "\"Você pode usar o depósito por " + @restante + " minuto(s) sem nenhum custo adicional.\"";
	if (@restante > 60) mes "\"Você pode usar o depósito por " + @restante/60 + " hora(s) sem nenhum custo adicional.\"";
	close2;
	openstorage;
	end;

_sem_grana:
	mes "["+ @npcName$ +"]";
	mes "Volte com "+ @Custo +"GP que eu abrirei o depósito!";
	close;

_sair:
	mes "["+ @npcName$ +"]";
	mes "Ok! Volte sempre!";
	close;

L_Seguranca:
	if (#ContaDeposito != 0) goto L_Modificar;
	menu
		"Definir senha de proteção", L_DefineSenha,
		"Voltar", _inicio;
	close;

L_Modificar:
	menu
		"Modificar senha de proteção", L_DefineNovaSenha,
		"Retirar senha de proteção", L_ZerarSenha,
		"Voltar", _inicio;
	close;


L_DefineSenha:
	mes "[" + @npcName$ + "]";
	mes "Tenha muito cuidado ao definir uma senha, pois, se você a esquecer, você não conseguirá acessar sua conta.";
	next;
	mes "\"Digite a senha desejada: \"";
	input @SenhaDeposito;
	mes "\"Digite novamente a senha desejada: \"";
	input @ConfirmaSenhaDeposito;
	if (@ConfirmaSenhaDeposito != @SenhaDeposito) mes "As senhas não conferem. Tente novamente";
	if (@ConfirmaSenhaDeposito != @SenhaDeposito) goto L_DefineSenha;
	set #SenhaDeposito, @SenhaDeposito;
	set #ContaDeposito, 1;
	goto _inicio;

L_DefineNovaSenha:
	mes "[" + @npcName$ + "]";
	mes "\"Digite a senha atual: \"";
	input @SenhaDeposito;
	if (@SenhaDeposito != #SenhaDeposito) goto L_SenhaErrada;
	mes "\"Digite a nova senha: \"";
	input #SenhaDeposito;
	goto _inicio;
	
L_ZerarSenha:
	mes "[" + @npcName$ + "]";
	mes "\"Digite a senha atual: \"";
	input @SenhaDeposito;
	if (@SenhaDeposito != #SenhaDeposito) goto L_SenhaErrada;
	mes "[" + @npcName$ + "]";
	mes "\"Senha de proteção retirada.\"";
	set #SenhaDeposito, "";
	set #ContaDeposito, 0;
	goto _inicio;

L_SenhaErrada:
	mes "[" + @npcName$ + "]";
	mes "\"Senha errada.\"";
	close;

L_deposito_Senha:
	mes "[" + @npcName$ + "]";
	mes "\"Digite a sua senha: \"";
	input @SenhaDeposito;
	if (@SenhaDeposito != #SenhaDeposito) goto L_SenhaErrada;
	set @DigitouSenhaDeposito, 1;
	goto _abrir_deposito;
}
