//######################################################################################################
//##   QUEST DO DRÁCULA PARA O HALLOWEEN
//######################################################################################################
//## QUESTARTS:
//##    * Selena<santos.alinnee@gmail.com>
//##    * Lunovox<rui.gravata@hotmail.com>
//## CRIAÇÃO: 19/10/2011
//## DESCRIÇÃO:
//##    * O Drácula pede para o jogador encontar 3 itens. Depois pede para encontrar mais um. Depois
//##    que o jogador encontra todos os 4 tem que voltar e esconder um madeira encantada nos mesmo
//##    locais onde ele encontrou cada um dos 4 itens.
//## IDs DE ITENS DEPENDENTES:
//##    * Boneca de Pano
//##    * Colar de Cruz
//##    * Retrato
//##    * Bússola
//##    * Madeira Enfeitiçada
//##    * Armadura (Selena não disse qual é a armadura)
//## CÓDIGO DEPENDENTE:
//##    * Falta por is ID de itens na configuração deste script.
//##    * Falta por script de pegar item encontrado e esconder madeira no lugar.
//######################################################################################################

013.gat,145,153,0	script	debug	368,{
	callfunc "debug";
	mes "Quest_Dracula: " + Quest_Dracula;
	menu
		"Set Quest_Dracula, 0", -,
		"Set Quest_Dracula, 1", -,
		"Set Quest_Dracula, 2", -,
		"Set Quest_Dracula, 3", -,
		"Set Quest_Dracula, 4", -,
		"Sair", -;
	if(@menu<0 || @menu>5) close;
	set Quest_Dracula, @menu-1;
	close;
}

013.gat,148,153,0	script	Drácula	368,{

_inicio:
//------------------------------------------------------------------------------------------------------
	//Seta IDs dos Itens
	set @BonecaDePano, 3322;
	set @ColarDeCruz, 3323;
	set @Bussola, 3324;
	set @Retrato, 3325;
	set @MadeiraEnfeiticada, 3326;
	set @Armadura, 3333;

	//Redirecionamento de Cenas
	if(Quest_Dracula==1 && countitem(@BonecaDePano)>=1 && countitem(@ColarDeCruz)>=1 && countitem(@Retrato)>=1) goto _QuaseTudo;
	if(Quest_Dracula==2 && countitem(@BonecaDePano)>=1 && countitem(@ColarDeCruz)>=1 && countitem(@Retrato)>=1 && countitem(@Bussola)>=1) goto _TemTudo;
	if(Quest_Dracula>=1 && Quest_Dracula<=2) goto _FaltaItens;
	if(Quest_Dracula>=3 && Quest_Dracula<=6) goto _NaoEscondeu;
	if(Quest_Dracula==7) goto _SimEscondeu; //Cada vez que o jogador esconder 1 das 4 madeiras, um ponto será incrementado na variável "Quest_Dracula".
	if(Quest_Dracula>=8) goto _FinalDaQuest;
//-------------------------------------------------------------------------------------------------------
	mes "Uma canção assoviada pode ser ouvida. Uma canção bonita e ao mesmo tempo sombria. Parece vir de um caixão. Não do caixão, proximo a ele. De uma figura escondida na escuridão. Um homem. Ele se aproxima um pouco da luz. Ele tem presas, como um animal. Mas do que isso, ele se parece muito com as figuras  das historias, aquelas conhecidas como vampiros.";
	next;
	mes "[Vampiro]";
	mes "O sujeito lhe dá um fraco sorriso.";
	mes "\"Ora ora, quem o deixou entrar em minha casa?\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "(O_o) \"Perdão senhor, a porta estava aberta.\"";
	next;
	mes "[Vampiro]";
	mes "\"WOH, deve ter sido alguma daquelas crianças travessas de novo. Pois bem, já que está aqui, em que posso ajudá-lo?\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "(X.x) \"O senhor vai me matar?\"";
	next;
	mes "[Vampiro]";
	mes "Sorri maliciosamente e diz:";
	mes "\"Por que o mataria?\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "(+_x) \"O senhor é um vampiro, não é mesmo?\"";
	next;
	mes "[Vampiro]";
	mes "\"Então percebestes. Sim, de fato. Sou o Conde Drácula. E não, não o matarei... hoje...\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "(-_-) \"Posso fazer uma pergunta?\"";
	next;
	mes "[Drácula]";
	mes "\"Estou me sentindo bondoso hoje,então, pode perguntar.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "\"Como se tornou um vampiro?\"";
	next;
	mes "[Drácula]";
	mes "Drácula te encara e diz:";
	mes "\"OW, é uma pergunta interessante essa. Sabe, eu era nobre. Senhor de muitas terras, uma esposa muito bonita, embora um pouco amarga. Mas em certo momento em nossas vidas fomos felizes. Mas tudo durou muito pouco. Houve um surto de peste negra onde viviamos. Minha mulher  fugiu para países vizinhos, para não ser pega pela desgraça. Então eu conheci Ezalia. Uma vampira. A primeira vampira, nascida, de fato, assim. Eu já havia perdido tudo, familia, amigos, e estava perdendo minha vida para a peste. Ezalia me conheceu na enfermaria. Ela se alimentava de homens recém falecidos, para não matar. Era uma mulher bondosa. Eu seria seu alimento naquela noite. Ela esperava que eu morresse. Mas eu ressisti.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "Você arregala os olhos surpreso, e não encontra palavras.";
	next;
	mes "[Drácula]";
	mes "Faz uma cara de como quem conta algo comum do dia-a-dia:";
	mes "\"Ela veio a mim como um anjo. Disse que eu não estava pronto para morrer ainda e me perguntou se eu queria uma segunda chance. Eu aceitei e assim me tornei um vampiro.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "(-.-) \"E o que houve a essa mulher?\"";
	next;
	mes "[Drácula]";
	mes ">=] \"Bem, nunca fui um homem muito correto em vida, por que seria em morte? Eu a matei e tomei o controle de tudo que ela possuia XD, depois criei mais vampiros e os deixei sob meu poder.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "Você arregala os olhos por perceber o quão perigoso é esse ser que está conversando com você. Mas você olha mais atentamente para o vampiro e percebe que ele tem olheiras profundas, pergunta:";
	mes "(-_-) \"Algum problema senhor?\"";
	next;
	mes "[Drácula]";
	mes "\"É tão obvio assim?... Estou um tanto chateado com um velho amigo. Ele tem entrado em minha propriedade e feito grande estrago.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "\"E por que alguém faria tal coisa?\"";
	next;
	mes "[Drácula]";
	mes "\"Ele está um pouco chateado, vamos dizer que... meus filhos pegaram alguns de seus pertences por brincadeiras e agora ele parece um tanto chateado e disposto a fazer certo estrago a minha propriedade e pessoa. O caso é que eu mesmo iria ter uma prosa com ele e colocá-lo em seu devido lugar. Mas ando tão indisposto ultimamente. Talvez seja fome.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "A menção a fome te faz tremer, o vampiro olha pra vc e lambe seus labios.";
	next;
	mes "[Drácula]";
	mes "Então ele sorri.";
	mes "\"É só uma piadinha, estou sem fome hoje, está seguro. Bem, preciso encontrar esses itens, antes que ele venha aqui novamente. Ele é um pouco cabeça quente, sabe. Talvez o tenha conhecido quando vinha para cá. Ele se chama Silver.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "(O.O) \"O lobisomem Silver?\"";
	next;
	mes "[Drácula]";
	mes "\"Esse mesmo. Olha, você poderia me ajudar procurando esses itens. São importantes para ele. E não estou disposto a começar uma guerra com aquele lobisomem temperamental. Se me ajudar a achar todos eu posso recompensá-lo e muito bem...\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "\"Isso parece ser um bom negocio. O que seus filhos esconderam?\"";
	next;
	mes "[Drácula]";
	mes "\"Hum! Deixe-me ver...";
	mes " * Uma boneca de pano, que era de sua filha.";
	mes " * Um colar em forma de cruz, de sua esposa.";
	mes " * Um retrato.";
	mes "Encontre-os para mim!.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "\"Sim, será um prazer ajudar.\"";
	next;
	mes "[Drácula]";
	mes "\"Mas ande depressa, loga será de manhã e terei de ir dormir. Vá, se apresse. As crianças podem ter escondido em qualquer lugar.\"";
	set Quest_Dracula, 1;
close;
//-------------------------------------------------------------------------------------------------------
_FaltaItens:
	mes "[Drácula]";
	mes "(T_T) \"Voce não conseguiu o que eu mandei? Só volte quando conseguir!\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "(X.x) \"Tá bem! Tá bem! Mas não se irrite!\"";
close;
//-------------------------------------------------------------------------------------------------------
_QuaseTudo:
	mes "[Drácula]";
	mes "\"Vejo que finalmente os achou. Mas temos um problema, eu esqueci de te falar sobre mais um item.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "(¬_¬') \"Mais um item?\"";
	next;
	mes "[Drácula]";
	mes "\"Uma bússola.\"";
	mes "Vá vá, tem que achar. Já é quase dia. Se apresse.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "=S \"Está bem.\"";
	set Quest_Dracula, 2;
close;
//-------------------------------------------------------------------------------------------------------
_TemTudo:
	mes "[Drácula]";
	mes "\"Muito bem, conseguiu tudo. Mas as crianças vão procura-los depois... Já sei...\"";
	next;
	mes "[Drácula]";
	mes "Drácula vai até seu caixão, pega algumas coisas e diz: \"Esconda essas [Madeiras Enfeitiçadas] no lugar. Se eles forem procurar então verão exatamente o que esconderam antes.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "=O \"Você também faz magia?\"";
	next;
	mes "[Drácula]";
	mes "(T_T) \"Mas é claro que não!\"";
	next;
	mes "[Drácula]";
	mes "Ele sorri e diz:";
	mes "(^_^) \"Comprei na loja do Frank, ele tem coisas muito uteis por lá. Passe por lá quando tiver uma oportunidade. Agora vá de uma vez esconder e esconda bem, e não deixe nenhum do meus filhos verem, ou podem acabar lanchando você.\"";
	next;
	if(Quest_Dracula!=2 || countitem(@BonecaDePano)<1 && countitem(@ColarDeCruz)<1 || countitem(@Retrato)<1 || countitem(@Bussola)<1) close;
	mes "["+ strcharinfo(0) +"]";
	mes "(X.x) \"Ok!\"";
	set Quest_Dracula, 3;
	delitem @BonecaDePano, 1;
	delitem @ColarDeCruz, 1;
	delitem @Retrato, 1;
	delitem @Bussola, 1;
	getitem @MadeiraEnfeiticada, 4;
	//OBS.: Cada vez que o jogador esconder 1 das 4 madeiras, um ponto será incrementado na variável "Quest_Dracula".
close;
//-------------------------------------------------------------------------------------------------------
_NaoEscondeu:
	mes "[Drácula]";
	mes "(^_^) \"Já escondeu todas as [Madeiras Enfeitiçadas] perto de onde as encontrou?\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "(O_o) \"Na verdade andei por tantos lugares, que nem lembro onde os encontrei.\"";
	next;
	mes "[Drácula]";
	mes "(T_T) \"Então, não venha acabar com minha paciência, e vá logo de uma vez!\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "(X_x) \"Tá bem! Tá bem! ja estou indo.\"";
close;
//-------------------------------------------------------------------------------------------------------
_SimEscondeu:
	mes "["+ strcharinfo(0) +"]";
	mes "\"Escondi tudo.\"";
	next;
	mes "[Drácula]";
	mes "\"Excelente! Agora vejamos o que poderia lhe dar de recompensa...\"";
	next;
	mes "[Drácula]";
	mes "(^_^) \"Que tal a vida eterna?\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "(O.o) \"Acho que algo mais simples já estaria bom.\"";
	next;
	mes "[Drácula]";
	mes "\"Já sei!\"";
	next;
	mes "[Drácula]";
	mes "Ele se tranforma em um morcego e voa para cima, se perdendo nas sombras. Você escuta alguns barulhos de metal então algo cai a centimetros de distância de você causando um barulho insurdecedor. Drácula aparece, se transforma de morcego para sua forma humana novamente, e diz: \"Aí está amigo, minha armadura antiga, de quando eu cavalgava bravamente por todas essas terras atrás de aventuras. Espero que faça bom uso dela.\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "\"Sim! Farei muito bom uso.\"";
	set Quest_Dracula, 8;
	getitem @Armadura, 1;
close;
//-------------------------------------------------------------------------------------------------------
_FinalDaQuest:
	mes "[Drácula]";
	mes "(^_^) \"Está aproveitando o que te dei?\"";
	next;
	mes "["+ strcharinfo(0) +"]";
	mes "(T_T) \"Sim! Estou aproveitando bem.\"";
close;

}
