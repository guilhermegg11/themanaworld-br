/**
 * A Cartola mágica é um jogo interativo onde todos podem participar.
 * Seja como jogador ou expectador.
 * Durante uma partida, vence aquele que tirar a cartola do coelho.
 *
 * Aqui encontram-se os scripts do mágico.
 *
 * @date 21 de setembro de 2010
 * @author Diogo_RBG - http://diogorbg.blogspot.com/
 */

007.gat,56,75,0	script	debug	323,{
	mes "$@CARTOLA: "+$@CARTOLA;
	mes "$@CARTOLA_senha$: "+$@CARTOLA_senha$;
	menu
		"inicia partida", _iniciaPartida,
		"Sair", -;
	close;
_iniciaPartida:
	set $@CARTOLA, rand($@CARTOLA-10)+1;
	close;
}

007.gat,58,75,0	script	Mágico	323,{

	set @itemBoneGM, 725;
	set @itemBilheteMagico, 3143;

	if( $@CARTOLA_ganhador$=="" ) goto _else;
		npctalk "'"+$@CARTOLA_ganhador$+"' ganhou a partida!!!";
		set $@CARTOLA_ganhador$, "";

_else:
	mes "[Mágico]";
	mes "\"Abracadabra... simsalabim... coelho apareça apara mim!\"";
	mes "...";
	mes "O mágico bate na cartola.";
	mes "\"Vamos... saia daí! aff!!!\"";
	//mes "";
	//mes "Você já ganhou: "+@CARTOLA_numVit + " partidas.";

_menu:
	setarray @opcoes$, "Criar nova partida.", "Participar do jogo.", "Opções do jogo.";
	if ($@CARTOLA==0) set @op, 0;
	if ($@CARTOLA!=0 && $@CARTOLA_partida!=@CARTOLA_partida) set @op, 1;
	if ($@CARTOLA!=0 && $@CARTOLA_partida==@CARTOLA_partida) set @op, 2;
	if ($@CARTOLA!=0 && $@CARTOLA_lider$==strcharinfo(0)) set @op, 2;
	if (countitem(@itemBoneGM)) goto _menuGM;
	menu
		@opcoes$[@op], -,
		"Comprar Bilhete Mágico.", _comprarBilhete,
		"Informações sobre o jogo.", _info,
		"Sair", -;
	if(@menu==1 && @op==0) goto _novaPartida;
	if(@menu==1 && @op==1) goto _participarPartida;
	if(@menu==1 && @op==2) goto _opcoesPartida;
	close;

_menuGM:
	menu
		@opcoes$[@op], -,
		"Opções de GM", _opcoesGM,
		"Comprar Bilhete Mágico.", _comprarBilhete,
		"Informações sobre o jogo.", _info,
		"Sair", -;
	if(@menu==1 && @op==0) goto _novaPartida;
	if(@menu==1 && @op==1) goto _participarPartida;
	if(@menu==1 && @op==2) goto _opcoesPartida;
	close;

//- Criar nova partida ------------------------------------------------

_novaPartida:
	if (countitem(@itemBilheteMagico)<1) goto _semBilhete;
	if ($@CARTOLA!=0) goto _jogoAndamento;

	delitem @itemBilheteMagico, 1;
	set $@CARTOLA, 9;                           //< Partida reservada
	set $@CARTOLA_partida, $@CARTOLA_partida+1; //< Id da partida
	set $@CARTOLA_lider$, strcharinfo(0);       //< Lider da partida
	callsub _zerarTodos, 0;

	mes "[Mágico]";
	mes "\"Você acaba de criar uma nova partida.";
	mes "Antes de seguir com o jogo vou precisar que você responda a duas perguntas.\"";
	next;
	mes "[Mágico]";
	mes "\"Você pode criar uma PALAVRA MÁGICA e com isso impedir que qualquer jogador participe do jogo.\"";
	mes "";
	mes "\"Qual a PALAVRA MÁGICA escolhida?\"";
	mes "Deixe em branco para não existir uma palavra mágica.";
	input $@CARTOLA_senha$;
	mes "[Mágico]";
	mes "\"Você pode PARTICIPAR da partida tirando uma cartola ou pode simplesmente gerenciá-la.";
	mes "Qual a sua escolha?\"";
	menu
		"Quero participar.", _queroParticipar,
		"Quero apenas gerenciar.", _queroGerenciar;
	goto _queroParticipar;

_queroParticipar:
	set $@CARTOLA, 10;
	callsub _subParticiparPartida;
	goto _continuacao;

_queroGerenciar:
	set $@CARTOLA, 10;
	goto _continuacao;

_continuacao:
	killmonster "007.gat", "Mágico::_onKill";
	npctalk "'"+strcharinfo(0)+"' iniciou uma nova partida.";

	mes "[Mágico]";
	mes "\"Agora é por sua conta. Convide jogadores para participarem desta partida.\"";
	close;

_semBilhete:
	mes "[Mágico]";
	mes "\"Infelizmente, para criar uma nova partida é necessário um [Bilhete Mágico]. E vejo que você não tem nenhum.";
	mes "Por favor compre um bilhete primeiro.\"";
	next;
	menu
		"Foi mal... esqueci do bilhete!", -,
		"É mesmo, o bilhete! Vou comprar um.", -,
		"Mas como você sabe que eu não tenho bilhete?", _souMagico;
	goto _menu;

_souMagico:
	mes "[Mágico]";
	mes "\"Ham? Como assim eu sei?! Esqueceu que sou mágico?";
	mes ";D\"";
	next;
	menu
		"Ops! Tinha me esquecido deste detalhe.", -,
		"Heheh... é véro!", -,
		"Oo", -;
	goto _menu;

_jogoAndamento:
	mes "[Mágico]";
	mes "\"Não posso criar uma nova partida. Pois a partida de '"+$@CARTOLA_lider$+"' está em andamento.";
	if ($@CARTOLA<9) mes "Eles ainda estão jogando.\"";
	if ($@CARTOLA>=9) mes "Eles ainda não iniciaram o jogo.\"";
	next;
	menu
		"Verifique de novo. Quero criar uma nova partida.", _novaPartida,
		"Ok... vou esperar mais um pouco.", -;
	close;

//- Participar da partida ---------------------------------------------

_participarPartida:
	if ($@CARTOLA_partida==@CARTOLA_partida) close; //< fazer: restringir e explicar o porque.
	callsub _subDigitarSenha;
	callsub _subParticiparPartida;
	mes "[Mágico]";
	mes "\"Você está participando do jogo. Aguarde até que o líder dê início à partida.\"";
	close;

_subDigitarSenha:
	if ($@CARTOLA_senha$=="") return;
	mes "[Mágico]";
	mes "\"Para participar deste jogo você deve dizer a PALAVRA MÁGICA:\"";
	input @senha$;
	if (@senha$==$@CARTOLA_senha$) return;
	if (@senha$!="") goto _subDigitarSenha;
	mes "[Mágico]";
	mes "\"Você realmente não conhece a PALAVRA MÁGICA. Peça ela ao líder da partida.\"";
	mes "Líder da partida: '" +$@CARTOLA_lider$+ "'";
	close;

_subParticiparPartida:
	if($@CARTOLA<10 || $@CARTOLA>=17) return;

	set $@CARTOLA, $@CARTOLA+1;
	set @CARTOLA_partida, $@CARTOLA_partida;
	callsub _zerarTodos, $@CARTOLA-10;
	return;

//- Opções da partida -------------------------------------------------

_opcoesPartida:
	if ($@CARTOLA_lider$!=strcharinfo(0)) goto _opcoesPartida2;
	mes "[Mágico]";
	mes "\"Escolha uma opção:\"";
	menu
		"Iniciar partida (minimo 3 jogadores)", _opcaoIniciaPartida,
		"Definir Palavra mágica", _opcaoPalavraMagica,
		"Participar do jogo", _opcaoParticipaJogo,
		"Finalizar partida", _opcaoFinalizarPartida,
		"Sair", -;
	close;

_opcoesPartida2:
	mes "[Mágico]";
	mes "\"Só posso de dar algumas informações sobre esta partida:";
	mes "Líder da partida: '" +$@CARTOLA_lider$+ "'\"";
	close;

_opcaoIniciaPartida:
	if ($@CARTOLA<13) goto _opcaoIniciaPartidaErro;
	set $@CARTOLA, rand($@CARTOLA-10)+1;
	close;

_opcaoIniciaPartidaErro:
	mes "[Mágico]";
	mes "\"Não é possível iniciar a partida a menos que 3 jogadores estajam participando.\"";
	mes "Número de jogadores: " +($@CARTOLA-10);
	next;
	goto _opcoesPartida;

_opcaoPalavraMagica:
	mes "[Mágico]";
	mes "\"Você pode criar uma PALAVRA MÁGICA e com isso impedir que qualquer jogador participe do jogo.\"";
	mes "";
	mes "\"Qual a PALAVRA MÁGICA escolhida?\"";
	mes "Deixe em branco para não existir uma palavra mágica.";
	input $@CARTOLA_senha$;
	close;

_opcaoParticipaJogo:
	if ($@CARTOLA_partida==@CARTOLA_partida) goto _opcoesPartida;
	callsub _subParticiparPartida;
	close;

_opcaoFinalizarPartida:
	set $@CARTOLA, 0;
	close;

_opcoesGM:
	mes "[Opções de GM]";
	mes "Um GM tem privilégios sobre as funções do NPC Mágico... use com seriedade.";
	mes "Partida em andamento: " +($@CARTOLA!=0);
	mes "Líder da partida: '" +$@CARTOLA_lider$+ "'";
	menu
		"Criar nova partida", _opcaoGMCriarPartida,
		"Iniciar partida (minimo 3 jogadores)", _opcaoIniciaPartida,
		"Finalizar partida", _opcaoFinalizarPartida,
		"Voltar", _menu;
	close;

_opcaoGMCriarPartida:
	mes "[Opções de GM > Criar nova partida]";
	mes "Partida em andamento: " +($@CARTOLA!=0);
	mes "Líder da partida: '" +$@CARTOLA_lider$+ "'";
	mes "";
	mes "Deseja realmente Criar uma nova partida?";
	menu
		"Não", _opcoesGM,
		"Sim (Cancela a partida atual)", -;
	set $@CARTOLA, 0;
	getitem @itemBilheteMagico, 1;
	goto _novaPartida;
	close;

//- Comprar bilhete ---------------------------------------------------

_comprarBilhete:
	mes "[Mágico]";
	mes "\"Um bilhete mágico custa apenas 500 GP.";
	mes "Quantos você gostaria de comprar?\"";
	//next; //< Aqui fica melhor sem next.
	menu
		"Apenas 1 (500 GP)", -,
		"Apenas 2 (1.000 GP)", -,
		"Apenas 3 (1.500 GP)", -,
		"Nenhum", _menu;
	if(@menu<1 || @menu>3) goto _menu;

	if (zeny < (500*@menu)) goto _semGrana;
	getinventorylist;
	if( (@inventorylist_count + (countitem(@itemBilheteMagico)==0)) >= 100 ) goto _semEspaco;

	mes "[Mágico]";
	mes "\"Aqui estão os seus bilhetes.\"";
	set zeny, zeny - (500*@menu);
	getitem @itemBilheteMagico, @menu;
	next;
	goto _menu;

_semGrana:
	mes "[Mágico]";
	mes "\"Você não tem grana o suficiente para comprar os bilhetes.";
	mes "Ops... ou eu estaria enganado!\"";
	mes "Ao dizer estas palavras o mágico tira uma moeda de sua orelha.";
	next;
	mes "[Mágico]";
	mes "\"Mas esta moeda não vale... ela é minha. XD\"";
	close;

_semEspaco:
	mes "[Mágico]";
	mes "\"Como você quer comprar um bilhete se não tem espaço nem em seus bolsos?";
	mes "Você quer que eu faça mágica?!\"";
	close;

//- Informações -------------------------------------------------------

_info:
	mes "[Mágico]";
	mes "\"A Cartola Mágica é um jogo onde todos podem participar. Seja como jogador ou expectador.";
	mes "Durante o jogo, vence aquele que tirar a cartola sorteada, então, fique de olho no coelho. =)\"";
	next;
_menuInfo:
	menu
		"Legal, mas como que se joga?", _comoJogar,
		"Eu posso criar uma partida?", _comoCriarPartida,
		"Onde consigo um Bilhete Mágico?", _ondeBilhete,
		"Voltar", _menu;
	goto _menu;

_comoJogar:
	mes "[Mágico]";
	mes "\"Primeiro você deve criar uma partida ou esperar que o líder de uma te convide.";
	mes "Se você receber o convite de um líder peça para 'Participar do jogo' aqui comigo.\"";
	next;
	mes "[Mágico]";
	mes "\"Pode ser que o líder crie uma partida particular. Neste caso você só poderá participar do jogo se puder dizer a 'palavra mágica', que só o líder poderá te informar.\"";
	next;
	mes "[Mágico]";
	mes "\"Ao participar do jogo irá aparecer mais uma cartola no jogo. Isso indica que um novo jogador está participando do jogo. (Não significa que você deverá tirar esta cartola... escolha a que quiser).";
	mes "Assim que 7 jogadores aceitarem participar do jogo, ou por decisão do líder, o jogo será iniciado.";
	mes "Até lá você deverá aguardar pacientemente. =).\"";
	next;
	mes "[Mágico]";
	mes "\"E finalmente, quando o jogo for iniciado, você escolherá uma cartola.";
	mes "Se dela surgir um coelho é porque você tem muita sorte... e que também acaba de ganhar o jogo. \\o/\"";
	next;
	goto _menuInfo;

_comoCriarPartida:
	mes "[Mágico]";
	mes "\"Sim... você pode criar uma nova partida.";
	mes "E o bom de se criar uma partida é que você será o LÍDER dela.";
	mes "Se este for o seu desejo peça para 'Criar nova partida' aqui comigo.\"";
	next;
	mes "[Mágico]";
	mes "\"Para criar uma partida será necessário um [Bilhete Mágico]. Se você não tiver nenhum basta comprá-lo aqui comigo.";
	mes "Este bilhete será cobrado no momento em que você me pedir para criar uma nova partida.\"";
	next;
	mes "[Mágico]";
	mes "\"Após a criação da partida irei lhe fazer duas perguntas:";
	mes "* Primeiro, precisarei saber se você irá usar uma palavra mágica ou se prefere deixar livre para que qualquer jogador participe.";
	mes "* Em seguida, saber se você gostaria de participar do jogo, ou simplesmente prefere apenas gerenciá-lo.\"";
	next;
	mes "[Mágico]";
	mes "\"Criada a partida, você deverá convidar jogadores para participarem dela. O número mínimo de jogadores é 3 e o máximo é 7.";
	mes "É seu dever iniciar a partida... e não demore muito a fazer isso, porque você terá apenas 3 minutos para iniciar a partida e mais 2 minutos para jogar. Estourado este tempo, qualquer jogador poderá interferir criando uma nova partida.\"";
	next;
	mes "[Mágico]";
	mes "\"Antes de iniciar a partida oriente os jogadores a escolherem suas cartolas ficando próximo a elas, ou que cada um tire a sua cartola só depois que outro tirar. Isso pode ser muito interessante para a dinâmica do jogo.";
	mes "Boa sorte. =)\"";
	next;
	goto _menuInfo;

_ondeBilhete:
	mes "[Mágico]";
	mes "\"O Bilhete Mágico pode ser comprado comigo. Eu ainda tenho alguns para vender.\"";
	next;
	menu
		"=) Ok... compro um depois", _menuInfo,
		"=( Achei que ia conseguir um de graça!", _deGraca;
	goto _menuInfo;

//- QUEST do Bilhete Mágico -------------------------------------------

_deGraca:
	if(@bilheteVerme==1) goto _deGraca1;
	if(@bilheteVerme==2) goto _deGraca2;

	mes "[Mágico]";
	mes "\"Eu perdi um bilhete não faz muito tempo... me descuidei por um segundo e um vento forte o levou para bem longe daqui.";
	mes "Se você puder recuperá-lo poderá ficar com ele.\"";
	next;
	menu
		"É só me dizer pra que lado ele foi.", -;
	mes "[Mágico]";
	mes "\"Isso é fácil de responder: Ele foi pra dentro!\"";
	next;
	menu
		"Pra dentro onde?", -,
		"Pra dentro de uma casa?", -;
	mes "[Mágico]";
	mes "\"Pra dentro do estômago de um Verme. Ele o devorou sem pensar duas vezes.";
	mes "Eu desisti de ir atrás deste bilhete justamente por isso!";
	mes "Mas me entregando este bilhete limpinho não tem problema nenhum. =)\"";
	if(@bilheteVerme==0) set @bilheteVerme, 1;
	next;
	goto _menuInfo;

_deGraca1:
	mes "[Mágico]";
	mes "\"Como eu disse. Você pode tentar recuperar o bilhete que perdi. Ele foi carregado pelo vento e devorado por um Verme.";
	mes "Se você puder recuperá-lo poderá ficar com ele.\"";
	next;
	goto _menuInfo;

_deGraca2:
	mes "[Mágico]";
	mes "\"Me parece que você já conseguiu encontrar um bilhete hoje... se quiser usá-lo é só falar comigo.\"";
	next;
	goto _menuInfo;

//---------------------------------------------------------------------

OnInit:
	callsub _zerarTodos, 7;
	set $@CARTOLA_ganhador$, "";
	end;

_zerarTodos:
	callsub _zeraCartola, 1, getarg(0);
	callsub _zeraCartola, 2, getarg(0);
	callsub _zeraCartola, 3, getarg(0);
	callsub _zeraCartola, 4, getarg(0);
	callsub _zeraCartola, 5, getarg(0);
	callsub _zeraCartola, 6, getarg(0);
	callsub _zeraCartola, 7, getarg(0);
	return;

_zeraCartola:
	disablenpc "Nº "+getarg(0)+"#puft";
	if(getarg(0)<=getarg(1)) enablenpc "Nº "+getarg(0);
	if(getarg(0)>getarg(1)) disablenpc "Nº "+getarg(0);
	return;

_onKill:
	end;
}
