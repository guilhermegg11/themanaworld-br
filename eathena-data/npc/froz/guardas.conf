froz.gat,94,72,0	script	#FrozPorta1Fechada	604,2,0,{
	if($Froz_InvasaoPermitida==1) disablenpc "#FrozPorta1Aberta";
	if($Froz_Porta1==3 || ($Froz_Porta1==2 && (Reino==3 || gettime(4)==6)) || ($Froz_Porta1==1 && Reino==3)) goto _entrar;
	mes "["+strcharinfo(0)+"]";
	menu
	"Toc! Toc! Quero entrar!", _TocToc,
	"Quero invadir este reino!", _Arrebentar,
	"Nada! Deixa quieto!", -;
close;

_TocToc:
	callfunc "TocTocFroz";
close;

_Arrebentar:
	if($Froz_InvasaoPermitida!=1) goto _FalhaAoArrebentar;
	if(Reino==3) goto _MensagemAoTraidor;
	if(Reino==0) goto _MensagemAoHeremita;
	//if(gettime(4)!=6) goto _FalhaDeMomento; //← desabilite essa linha para invadir todos dos dias!
	disablenpc "#FrozPorta1Fechada";
	misceffect 313;
	monster "froz.gat", 94, 73, "#GuardasDeFroz", 1378, 7, "";
	monster "froz.gat", 94, 72, "", 1376, 1, "ArrebentarFroz";
close;

_FalhaAoArrebentar:
	mes "";
	mes "["+strcharinfo(0)+"]";
	mes "=S \"Mas infelizmente o portão está protigido magicamente!\"";
close;

_FalhaDeMomento:
	mes "";
	mes "["+strcharinfo(0)+"]";
	mes "=D \"Mas vou invadir no sábado!\"";
close;

_MensagemAoHeremita:
	mes "";
	mes "["+strcharinfo(0)+"]";
	mes "\"Mas não posso, pois sou um heremita e não sou cidadão de nenhum reino!\"";
close;

_MensagemAoTraidor:
	mes "";
	mes "["+strcharinfo(0)+"]";
	mes "\"Mas seria tolice invadir meu próprio reino!\"";
close;

_entrar:
	misceffect 314;
	warp "froz.gat", 94, 69;
end;
}

froz.gat,94,72,0	script	ArrebentarFroz	-1,{
	enablenpc "#FrozPorta1Aberta";
}

froz.gat,94,72,0	script	#FrozPorta1Aberta	300,2,0,{
	if($Froz_InvasaoPermitida!=1) disablenpc "#FrozPorta1Aberta";
	if($Froz_InvasaoPermitida!=1) end;
	warp "froz.gat", 94, 69;
}

function	script	TocTocFroz	{
	//if (getgmlevel(0)>=80) goto _entrar;
	if($Froz_Porta1==3 || ($Froz_Porta1==2 && (Reino==3 || gettime(4)==6)) || ($Froz_Porta1==1 && Reino==3)) goto _entrar;
	
	if(Reino==0) set @NomeReino$, "lugares desconhecidos";
	if(Reino==1) set @NomeReino$, "Bhramir";
	if(Reino==2) set @NomeReino$, "Halicarnazo";
	if(Reino==3) set @NomeReino$, "Froz";

	mes "";
	mes "[Ohniralc]";
	if(Sex==0) mes ">=] \"Parada aeee elementa de "+@NomeReino$+"!";
	if(Sex==1) mes ">=] \"Parado aeee sujeito de "+@NomeReino$+"!";
   next;
	//npctalk "mensagem publica!";
	mes "[Ohniralc]";
	if($Froz_Porta1==0) mes "A entrada de todos em Froz ainda está impedida.\"";
	if($Froz_Porta1==1) mes "A entrada de visitantes aos sábados em Froz ainda está impedida.\"";
	if($Froz_Porta1==2) mes "A entrada de visitantes aos sábados em Froz ainda está permitida.\"";
	if($Froz_Porta1==3) mes "A entrada de todos em Froz ainda está permitida.\"";
close;

_entrar:
	warp "froz.gat", 94, 69;
end;
}

froz.gat,93,75,0	script	Ohniralc	601,{
	if(Reino==0) set @NomeReino$, "lugares desconhecidos";
	if(Reino==1) set @NomeReino$, "Bhramir";
	if(Reino==2) set @NomeReino$, "Halicarnazo";
	if(Reino==3) set @NomeReino$, "Froz";
	if (getgmlevel(0)>=80) goto _comprimetar;
	mes "[Ohniralc]";
	mes ">=] \"Pois bem cidadão de "+@NomeReino$+".";
	if($Froz_Porta1==0) mes "A entrada de todos em Froz ainda está impedida.\"";
	if($Froz_Porta1==1) mes "A entrada de visitantes aos sábados em Froz ainda está impedida.\"";
	if($Froz_Porta1==2) mes "A entrada de visitantes aos sábados em Froz ainda está permitida.\"";
	if($Froz_Porta1==3) mes "A entrada de todos em Froz ainda está permitida.\"";
close;

_comprimetar:
	mes "[Ohniralc]";
	if(Sex==0) mes "\"Olá! My Lady!\"";
	if(Sex==1) mes "\"Olá! My Lord!\"";
	next;
_ofereceAjuda:
	mes "";
	mes "[Ohniralc]";
	mes "=) \"Então, em que posso ajudar?\"";
	next;
	if($Froz_InvasaoPermitida!=1) set @mensagem$, "Permitir Invasão!";
	if($Froz_InvasaoPermitida==1) set @mensagem$, "Impedir Invasão!";
	mes "["+strcharinfo(0)+"]";
	menu
		" → Registre nova Nacionalidade!", _controlarNacionalidade,
		" → Como é o controle deste portão?", _controlarPortao,
		" → "+@mensagem$, -,
		"Nada! Dispensado!", -;
	if(@menu==3 && $Froz_InvasaoPermitida==1) goto _ProtecaoRetirar;
	if(@menu==3 && $Froz_InvasaoPermitida!=1) goto _ProtecaoAdicionar;

	mes "";
	mes "[Ohniralc]";
	mes "=O \"Vida longa a "+strcharinfo(0)+"!!!!!\"";
close;

_controlarNacionalidade:
	if(Reino==0) set @NomeReino$, "lugares desconhecidos";
	if(Reino==1) set @NomeReino$, "Bhramir";
	if(Reino==2) set @NomeReino$, "Halicarnazo";
	if(Reino==3) set @NomeReino$, "Froz";
	mes "";
	mes "[Ohniralc]";
	mes "=D \"Você pertence a "+@NomeReino$+".";
	next;
	mes "[Ohniralc]";
	mes "Selecione sua nova nacionalidade:\"";
	menu
		" → Bhramir", -,
		" → Halicarnazo", -,
		" → Froz", -,
		" → Nenhuma Nacionalidade", -,
		"Deixa para depois!", _ofereceAjuda;
	if(@menu>=1 && @menu<=3) set Reino,@menu;
	if(@menu<=0 || @menu>=4) set Reino,0;

	mes "";
	mes "[Ohniralc]";
	mes "\"Entendido!\"";
	next;
goto _ofereceAjuda;

_controlarPortao:
	mes "";
	mes "[Ohniralc]";
	if($Froz_Porta1==0) mes "\"A entrada de todos em Froz ainda está impedida.\"";
	if($Froz_Porta1==1) mes "\"A entrada de visitantes aos sábados em Froz ainda está impedida.\"";
	if($Froz_Porta1==2) mes "\"A entrada de visitantes aos sábados em Froz ainda está permitida.\"";
	if($Froz_Porta1==3) mes "\"A entrada de todos em Froz ainda está permitida.\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"Impeça a entrada de todos!", -,
	"Impeça a entrada de visitantes aos sábados!", -,
	"Permita a entrada de visitantes aos sábados!", -,
	"Permita a entrada de todos!", -,
	"Deixa para depois!", _ofereceAjuda;
	if(@menu>=1 && @menu<=4) set $Froz_Porta1,@menu-1;
	mes "";
	mes "[Ohniralc]";
	mes "\"Entendido!\"";
	next;
goto _ofereceAjuda;

_ProtecaoRetirar:
	disablenpc "#FrozPorta1Aberta";
	enablenpc "#FrozPorta1Fechada";
	set $Froz_InvasaoPermitida, 0;
	mes "";
	mes "[Ohniralc]";
	mes "\"Ok! A proteção mágica no portão foi removida!\"";
	next;
goto _ofereceAjuda;

_ProtecaoAdicionar:
	set $Froz_InvasaoPermitida, 1;
	mes "";
	mes "[Ohniralc]";
	mes "\"Ok! A proteção mágica no portão foi refeita!\"";
	next;
goto _ofereceAjuda;

}
