//Quest Camisa assassino por: Sky

froz001.gat,174,23,0	script	#Ares	300,10,10,{ 
//npctalk Reino+"←=→"+$PosseDeFroz;
if(Reino!=$PosseDeFroz) goto _Raiva;
if(BaseLevel < 95 || QUEST_Ares >= 2) end;
	emotion 13; // Emote de quest!!! XD 
end;

_Raiva:
	emotion 11; // Emote de Raiva pelo invasor!!! XD 
	heal -(MaxHp/4),0; // NPC Ataca o invasor tirando 25% de HP!!
end;

}

//#################################################

froz001.gat,174,24,0	script	Ares	516,1,1,{
	set @PocaoEnergetica, 567; //-50UI
	set @PocaoDeConcentracao, 568; //-50UI
	set @OleoDeMonstro, 707; //-02UI
	set @CamisaDeAssassino, 755; //+1UI
	
	if(Reino!=$PosseDeFroz) goto _SugestaoAoInvasor;
	if (QUEST_Ares == 0 && BaseLevel < 95) goto L_semLevel;
	if (QUEST_Ares == 1) goto L_Retorno;
	if (QUEST_Ares == 2) goto L_Fim;
goto L_iniciaQuest;

_SugestaoAoInvasor:
	heal -(MaxHp/4),0;
	set @tmp,rand(4);
	if(@tmp == 0) npctalk "Saia daqui seu invasor de Froz! (Chute!)";
	if(@tmp == 1) npctalk "Você não pode entrar em Froz! (Pontapé!)";
	if(@tmp == 2) npctalk "Como você se atreve a tentar machucar as pessoas do Reino de Froz? (Soco!)";
	if(@tmp == 3) npctalk "Como Ninja, tenho a obrigação de defender o meu reino! (Golpe!)";
end;

L_semLevel:
	mes "[Ares]";
	mes "\"Não me faça perder tempo contigo! Cho! Cho! Vaza! \"";
close;

L_iniciaQuest:
	mes "\"Você vê um rapaz jovem, mas de aparência envelhecida. Ele está afiando uma poderosa Espada de Kurogane.\"";
	next;
	mes "[Ares]";
	mes "\"O que faz aqui?\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"Nada não, já estou de saída", -,
	"Quem é você?", L_Quem;
	mes "";
	mes "[Ares]";
	mes "\"É bom mesmo.\"";
close;

L_Quem:
	mes "[Ares]";
	mes "\"Costumam me chamar de Ares.\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"E o que você faz?", L_Profissao,
	"Humm... Estou com pressa, até mais", -;
close;

L_Profissao:
	mes "[Ares]";
	mes "\"Digamos que eu seja... Um matador.\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"Uau! Digamos que eu também seja. U.u", L_Tbmsou,
	"Fui O.O", -;   
close;

L_Tbmsou:
	mes "[Ares]";
	mes "\"Se é assim, devo considerar que somos... Parceiros de trabalho.\"";
	next;
	mes "[Ares]";
	mes "\"Já que parceiros se ajudam, se você me ajudar eu posso te ajudar. O que acha?\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"Tudo bem, o que você precisa?", L_Oquequer,
	"Agora estou ocupado.", -;
close;

L_Oquequer:
	mes "[Ares]";
	mes "\"Bem, preciso de óleo para limpar minha espada, e um pouco de poções de concentração e energética.\"";
	next;
	mes "\"Traga-me 2 Óleos de Monstro, e 50 poções de cada tipo, energética e de concentração.\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"Já vou buscar.", -;
	set QUEST_Ares, 1;
close;

L_Retorno:
	mes "[Ares]";
	mes "\"Trouxe os itens que eu te pedi?\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"Sim, aqui estão.", L_Verificar,
	"Não, ainda não consegui =(", L_naotenho;
goto L_naotenho;

L_Verificar:
	if(countitem(@OleoDeMonstro)<2 || countitem(@PocaoEnergetica)<50 || countitem(@PocaoDeConcentracao)<50)  goto L_naotem;
	getinventorylist;
	if (@inventorylist_count >=100) goto L_semlugar;
	delitem @OleoDeMonstro, 2;
	delitem @PocaoEnergetica, 50;
	delitem @PocaoDeConcentracao, 50;
	getitem @CamisaDeAssassino, 1;
	set QUEST_Ares, 2;
	mes "[Ares]";
	mes "\"Muito obrigado. Aqui está o item que te prometi.\"";
	next;
	mes "[Ares]";
	mes "\"Faça bom proveito desta camisa. Ela lhe deixará mais ágil.\"";
close;

L_naotem:
	mes "[Ares]";
	mes ">=[ \"Você não tem o que eu te pedi. Não ouse tentar me enganar.\"";
	mes " * 02 [Óleo de Monstro]";
	mes " * 50 [Poção Energética]";
	mes " * 50 [Poção de Concentração]";
close;

L_naotenho:
	mes "[Ares]";
	mes "\"Por favor, se apresse. Realmente preciso desses itens. Eles são:\"";
	mes " * 02 [Óleo de Monstro]";
	mes " * 50 [Poção Energética]";
	mes " * 50 [Poção de Concentração]";
close;

L_semlugar:
	mes "[Ares]";
	mes "\"Bem, parece que você não tem nenhum lugar para guardar os itens que eu te dei. Libere espaço e depois volte para pegar seus itens.\"";
close;

L_Fim:
	mes "[Ares]";
	mes "\"Boa sorte companheiro.\"";
close;
}

