//Quest TURMALINA ROSA por: Sky

001-1.gat,22,62,0	script	Carolina	163,{
	set @RosaPreta, 666;
	set @TurmalinaRosa, 3412;
	if(QUEST_TurmalinaRosa == 0 && BaseLevel < 75) goto L_semLevel;
	if(QUEST_TurmalinaRosa == 1) goto L_Retorno;
	if(QUEST_TurmalinaRosa == 2) goto L_Fim; 
goto L_iniciaQuest;

L_semLevel:
	mes "[Carolina]";
	mes "(^_^) \"Olá, como vai?\"";
close;

L_iniciaQuest:
	mes "[Carolina]";
	mes "(^_^) \"Olá, como vai?\"";
	next;
	mes "[Carolina]";
	if(Sex == 1) mes "\"Bem-vindo a Vila dos Pescadores.";
	if(Sex == 0) mes "\"Bem-vinda a Vila dos Pescadores.";
	mes "Meu nome é Carolina. E o seu?\"";
	next;
	mes "["+strcharinfo(0)+"]";
	mes "\"Olá Carolina! Meu nome é "+strcharinfo(0)+".\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu	
	"Posso ajudar?", L_Ajudar,
	"Preciso ir embora, tchau!", L_Naoposso;
goto L_Naoposso;

L_Ajudar:
	mes "[Carolina]";
	mes "\"Veja bem, eu tenho uma mania.\"";
	next;
	mes "["+strcharinfo(0)+"]";
	mes "\"Qual mania?\"";	
	next;
	mes "[Carolina]";
	mes "\"Eu sou... Uma colecionadora de flores!\"";
	next;
	mes "["+strcharinfo(0)+"]";
	mes "(^_^)\"Uau! isso é muito bacana.\"";
	next;
	mes "[Carolina]";
	mes "\"Eu também acho =P Pois bem, eu queria te pedir um favor.\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"Qual favor?", L_Qualfavor,
	"Agora não posso te ajudar.", L_Naoposso;
close;

L_Naoposso:
	mes "[Carolina]";
	mes "\"Tudo bem, até mais!\"";
close;

L_Qualfavor:
	next;
	mes "[Carolina]";
	mes "\"Há muito tempo eu procuro uma rosa muito rara. Já que você é um aventureiro, você poderia procurá-lo pra mim? Eu posso te pagar uma boa recompensa.\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu
	"Claro, porque não?", L_Possoprocurar,
	"Não posso.", L_Naoposso;
close;

L_Possoprocurar:
	mes "[Carolina]";
	mes "\"Muito obrigada!! A flor que eu tanto almejo é uma rosa preta. "+
	"Ela é realmente dificílima de encontrar. "+
	"Talvez você possa achá-la perto de cemitérios, ou em locais com terra fértil. "+
	"Há uma lenda que diz que elas crescem dentro do esqueleto de algumas mulheres. Imagine só que estranho?\"";
	next;
	mes "[Carolina]";
	mes "(^-^)\"Muito obrigada por me ajudar!\"";
	set QUEST_TurmalinaRosa, 1;
close;

L_Retorno:
	mes "[Carolina]";
	mes "(*-*) \"Trouxe minha rosa preta?\"";
	next;
	mes "["+strcharinfo(0)+"]";
	menu 
	"Sim!! Consegui encontrá-la.", L_entregaItens,
	"Não consegui pegá-la ainda.", -;
	mes "";
	mes "[Carolina]";
	mes "=( \"Realmente, é bem difícil de conseguí-la. Se você conseguir, eu tenho uma boa recompensa para você!\"";
	next;
	mes "["+strcharinfo(0)+"]";
	mes "Vou tentar achar isso logo!";
close;

L_entregaItens:
	getinventorylist;
	if(countitem(@RosaPreta)<1) goto L_naotem;
	if(@inventorylist_count >= 100) goto L_semlugar;
	delitem @RosaPreta, 1;
	getitem @TurmalinaRosa, 1;
	getexp 20000, 0;
	set QUEST_TurmalinaRosa, 2;
	mes "";
	mes "[Carolina]";
	mes "=O\"Muito obrigada!! Você finalmente encontrou! Agora, como trato é trato, aqui está a recompensa que eu te prometi.\"";
	next;
	mes "[Carolina]";
	mes "=) \"Esta é uma Turmalina Rosa. É uma pedra extremamente rara, assim como minha rosa. "+
	"Dizem por ai que ferreiros especializados conseguem acoplar esta turmalina em seus equipamentos. Tente perguntar para algum.\"";
close;

L_naotem:
	mes "[Carolina]";
	mes "=[ \"Estava cheia de expectativa, mas parece que você não tem minha tão esperada rosa... Se você conseguí-lo eu te darei uma boa recompensa!\"";
close;

L_semlugar:
	mes "[Carolina]";
	mes "=S \"Sua mochila está totalmente cheia! Isso não pesa nas costas não? Libere um espacinho para que eu possa te dar sua recompensa.\"";
close;

L_Fim:
	mes "[Carolina]";
	mes "(^_^)\"Até mais! E muito obrigada!\"";
close;

}
